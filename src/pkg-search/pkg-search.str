\literate[pkg-search]

 Autobundle -- Tool suite for automated source tree composition

 Author: Merijn de Jonge (mdejonge@cs.uu.nl)

 $Id$

\begin{code}
module pkg-search
imports lib Autobundle package-reader autobundle-version

strategies
short-description(p) =
   !["Usage: ", <p>(), " [options]"]

long-description(p) = 
   !["This program searches for keywords in a list of package\n",
     "definition files. A list of all package definitions is\n",
     "returned which contain one or more matching keywords."]
                         
pkg-search = 
   option-wrap(Pkg-search-options, Pkg-search)

Pkg-search-options =
   ArgOption( "-I", 
      where(<post-extend-config>("-I", [<id>])),
      !"-I <dir>         Search for packages in <dir>" ) +
   ArgOption( "-p", 
      where(<extend-config>("-p", [<remove-extensions><id>])),
      !"-p <pkg>         Include package <pkg> in bundle" ) + 
   ArgOption( "-w", 
      where(<extend-config>("-w", [<id>])),
      !"-w <word>        Find occurences of <word> in keyword section" )

Pkg-search =
   where( <get-config>"-w"; map(lower-case) => words );
   where( <get-config>"-p"; map(read-pkg) => pkgs);
   <filter(search(!words))>pkgs;
   map( \x -> <printnl>(stdout, [x]) \ )

search(words) = 
   ?(file, PackageDef(Identification(_,_,_,_,_,Keywords(keyword-list)),_,_,_));
   <concat;concat-strings;lower-case>keyword-list => keywords;
   words;
   map( \x -> <is-substring(!x)>keywords \ );
   !file

\end{code}
% Copyright (C) 2002-2004 Merijn de Jonge <mdejonge@cs.uu.nl>
%
% This program is free software; you can redistribute it and/or modify it
% under the terms of the GNU General Public License as published by the Free
% Software Foundation; either version 2, or (at your option) any later
% version.
%
% This program is distributed in the hope that it will be useful, but
% WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
% or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
% for more details.
%
% You should have received a copy of the GNU General Public License along
% with this program; if not, write to the Free Software Foundation, Inc., 59
% Temple Place - Suite 330, Boston, MA 02111-1307, USA.
