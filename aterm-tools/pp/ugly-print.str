module ugly-print
imports Box annotations

strategies

  ugly-print =
    has-annotation < ugly-print-with-anno + ugly-print-without-anno

  ugly-print-without-anno =
     UP-Int 
	<+ UP-Str
	<+ UP-Lst(ugly-print) 
  <+ UP-EmptyTuple
  <+ UP-Tuple(ugly-print)
	<+ UP-Cnst	
	<+ UP-App(ugly-print)

  ugly-print-with-anno =
    ! |[
        V vs=0 [ ~<ugly-print-without-anno>
          H hs=1 ["{" ~<get-annotations; ugly-print> "}"]
        ]
      ]|

rules

  UP-EmptyTuple : 
    ""#([]) -> |[ "()" ]|
    
  UP-Tuple(s) : 
    ""#([x | xs]) ->
      |[  V vs=0 [
            H hs=1 ["(" ~<s> x]
            ~*children
            ")"
          ]
      ]|
      where <map(! |[ H hs=1 [ "," ~<s> ] ]| )> xs => children

  UP-Cnst : 
    f#([]) -> |[ ~lit:f ]|

  UP-App(s) : 
    f#([x | xs]) ->
      |[  V vs=0 [
            H hs=0 [ ~lit:f "(" ]
            H hs=1 [" " ~<s> x]
            ~*children
            ")"
          ]
      ]|
      where <map(! |[ H hs=1 [ "," ~<s> ] ]| )> xs => children
   
  UP-Str :
    x -> |[ H hs=0 ["\"" ~lit:x "\""] ]|
      where <is-string> x

  UP-Int :
    x -> |[ NUM [ ~lit:s ] ]|
      where <is-int> x
          ; <int-to-string> x => s

  UP-Lst(s) :
    [] -> |[ "[]" ]|

  UP-Lst(s) :
    [x | xs] -> |[ V vs=0 [ H hs=1 ["[" ~<s> x] ~*tail "]"] ]|
      where <map(! |[ H hs=1 [ "," ~<s> ] ]| )> xs => tail
   
  box-sepby-comma =
    <separate-by> (|[ "," ]|, <id>)


/**
 * Copyright (C) 1998-2002 Eelco Visser <visser@acm.org>
 * 
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
 * 02111-1307, USA.
 */