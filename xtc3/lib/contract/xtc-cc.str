/** 
 * XTC Contract Checking Module
 * 
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-cc

strategies

  /**
   * Perform contract checks  
   * 
   * @param s Strategy wrapped with contract checking
   */
  xtc-cc-wrap(s) = 
    if xtc-check-contract then 
      dbg(|"** xtc-cc-wrap [start]")
    ; xtc-cc(Fst)
    ; where(s => o)
    ; xtc-cc(Snd)
    ; !o
    ; dbg(|"** xtc-cc-wrap [end]")
    else 
      s
    end

  /** 
   * Check all contracts for the provided XTC registration and its 
   * configuration.
   * 
   * @param s [Fst|Snd] (Select whether to apply on input or output)  
   */
  xtc-cc(s) = 
    ?(XtcConf(i,o,e,_),r)
  ; where(
      <xtc-fetch-contracts(s)> r
    < map(<xtc-cc(s)> ((i,o,e),<id>))
    + id
    )

  /** 
   * Check input/output by provided contract specification
   * 
   * @param s [Fst|Snd] (Select input/output file)  
   */
  xtc-cc(s) =   
    ?((_,_,e),XtcContract(_,q,args))
  ; Fst 
  ; s => f // select input or output file with [Fst/Snd]
  ; <some-or-empty ; (?Arguments(<id>) + [])> args => args'
  ; ( !XtcConf(f,Some(FILE(<xtc-new-file>)),e,args')
    ; xtc-cc-disable(xtc-dispatch(|q))
   <+ xtc-enforce-contract
    )

  xtc-cc-disable(s) = 
    if <get-config> "--check" then 
      where(<rm-config> "--check")
    ; restore-always(s,where(<set-config> ("--check",())))
    else 
      s
    end

strategies

  /** 
   * Extract contracts from registation meta-information
   * 
   * @param s [Fst|Snd] (Select input/output contract)
   */
  xtc-fetch-contracts(s) =
    xtc-fetch-contracts
  ; filter(XtcContract(where(<eq> (<id>,<s> (Pre(),Post()))),id,id))
  ; dbg(|"XtcContracts")


  /** 
   * Determine whether contracts should be checked
   */
  xtc-check-contract = 
    where(
      <get-config> "--check" 
    ; notice(|"Performing contract checks during dispatch")
    )

  /** 
   * Determine whether contracts should be enforced
   */
  xtc-enforce-contract = 
    where(<get-config> "--enforce")
  < err(|"Contract checking failed, enforcing by failure")
  ; fail
  + warn(|"Contract checking failed")

  /** 
   * Contract checking options used in xtc wrappers
   */
  xtc-contract-options = 
    xtc-check-option
  + xtc-enforce-option

  /** 
   * Option used in xtc wrappers to enable contract checking
   */
  xtc-check-option = 
    Option("--check"
    , where(<set-config> ("--check",()))
    , !"--check          Toggle contract checking (default: off)"
    )

  /** 
   * Option used in xtc wrappers to enforce contracts by failure
   */
  xtc-enforce-option = 
    Option("--enforce"
    , where(<set-config> ("--enforce",()) ; <set-config> ("--check",()))
    , !"--enforce        Toggle contract enforcing (default: off)"
    )   
