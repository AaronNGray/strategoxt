/** 
 * XTC Option wrapping strategies
 * 
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-options

strategies

 /**
  * Read input file, transform, and write to output file
  */
  xtc-io(s) =
    xtc-temp-files( 
      (!FILE(<get-config> "-i") <+ !stdin)
    ; s
    ; xtc-write-output
    )

 /**
  * Stop in the middle of a transformation pipeline, copying the
  * current file to the output file.
  */
  xtc-io-exit =
    xtc-write-output
  ; <xtc-exit> 0

 /** 
  * Transform, and write to output file
  */
  xtc-output(s) =
    xtc-temp-files(
      s
    ; xtc-write-output
    )

 /**
  * Read input file, transform
  */
  xtc-input(s) =
    xtc-temp-files(
      (!FILE(<get-config> "-i") <+ !stdin)
    ; s
    )

/** 
 * io-wrap for XTC components
 * 
 * @note 'read' -i option, transform, and 'write' to -o option
 */
strategies

  xtc-iowrap(s) = 
    xtc-io-wrap(s)

  xtc-iowrap(extra-opts, s) = 
    xtc-io-wrap(extra-opts, s) 

  xtc-io-wrap(s) = 
    xtc-io-wrap(fail, s)

  xtc-io-wrap(s|deps) = 
    xtc-io-wrap(fail, s|deps)

  xtc-io-wrap(extra-opts, s) = 
    xtc-wrap(
      extra-opts
   <+ io-options
    , xtc-io(s)
    ) 

  xtc-io-wrap(extra-opts, deps, s) = 
    xtc-io-wrap(extra-opts, s | <deps>)

  xtc-io-wrap(extra-opts, s | deps) = 
    xtc-wrap(
      extra-opts 
   <+ io-options
    , <set-config> ("--preload",deps)
    ; xtc-preload
    ; xtc-io(s) 
    | deps
    )

/** 
 * Wrapper for XTC components
 * 
 * @note No input/output control
 */
strategies

  xtc-wrap(s) = 
    xtc-wrap(fail,s)

  xtc-wrap(s|deps) = 
    xtc-wrap(fail,s|deps)

  xtc-wrap(extra-opts,s) = 
    option-wrap(
      extra-opts 
   <+ xtc-contract-options
    , s
    )

  xtc-wrap(extra-opts,s|deps) = 
    option-wrap(
      extra-opts 
   <+ xtc-contract-options 
   <+ xtc-dependency-options
    , <set-config> ("--preload",deps)
    ; xtc-preload
    ; s
    )

/** 
 * Writes output to the requested file/stdout
 */
strategies

  xtc-write-output =
    ?FILE(_); copy-to(<get-config> "-o" <+ !stdout())

  xtc-write-output =
    ?stdin(); <copy-file> (stdin(), <get-config <+ !stdout()> "-o")

strategies

  /** 
   * Preloads (caches) all XTC dependencies
   * 
   * @note Exists with status -1 on failure 
   */
  xtc-preload = 
    if <get-config ; not([])> "--preload" => deps then
      <xtc-missing-dependencies> deps
    ; if [] then 
        if <get-config> "--dependencies" then 
          <echo> "All XTC registrations available"
        ; <exit> 0
        end
      else 
        debug(!"Broken XTC dependencies: ")
      ; <exit> 1
      end 
    end

  /** 
   * Options used in xtc wrappers to control dependency checking 
   * and preloading 
   */
  xtc-dependency-options = 
    Option(
      "--no-preload"
    , where(<set-config> ("--preload",[]) ; <set-config> ("--no-preload",()))
    , !"--no-preload     Toggle XTC preloading (default: on)"
    )
  + Option(
      "--dependencies"
    , where(<set-config> ("--dependencies", ()))
    , !"--dependencies   Check XTC dependencies and exit"
    )

  xtc-pass-options = 
    ![ "--no-preload", "--check", "--enforce" ] 
  ; filter(where(get-config))

  xtc-pass-verbose = 
    ![("--verbose", <subt; int-to-string>(<get-config <+ !1> "--verbose", 1))]
   
