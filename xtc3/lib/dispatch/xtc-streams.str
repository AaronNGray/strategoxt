/** 
 * XTC Streams Transformations
 * 
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-streams
imports xtc-repository

/** 
 * Rewrite streams to file descriptor for specified file
 */   
rules

  xtc-stream-to-fd(|f) : 
    |[ input = strm ]| -> <xtc-streamdef-to-fd(|f)> strm

  xtc-stream-to-fd(|f) : 
    |[ output = strm ]| -> <xtc-streamdef-to-fd(|f)> strm

  xtc-stream-to-fd(|f) : 
    |[ error = strm ]| -> <xtc-streamdef-to-fd(|f)> strm

/** 
 * Rewrite stream definitions to abstract arguments
 */   
rules

  xtc-streamdef-to-fd(|f) : 
    |[ standard ]| -> fd
    where <?Some(FILE(<open>))> f => fd

  xtc-streamdef-to-fd(|f) : 
    |[ standard k ]| -> fd 
    where <?Some(FILE(<open>))> f => fd

/** 
 * Rewrite streams to abstract arguments
 */   
rules

  xtc-stream-to-arg(|f) : 
    |[ input = strm ]| -> <xtc-streamdef-to-arg(|f)> strm

  xtc-stream-to-arg(|f) : 
    |[ output = strm ]| -> <xtc-streamdef-to-arg(|f)> strm

  xtc-stream-to-arg(|f) : 
    |[ error = strm ]| -> <xtc-streamdef-to-arg(|f)> strm

/** 
 * Rewrite stream definitions to abstract arguments
 */   
rules

  xtc-streamdef-to-arg(|f) : 
    |[ standard ]| -> []

  xtc-streamdef-to-arg(|f) : 
    |[ standard k ]| -> arg |[ k ]|*

  xtc-streamdef-to-arg(|f) : 
    |[ file ]| -> arg |[ FILE : s ]|*
    where <?FILE(<id>)> f => s

  xtc-streamdef-to-arg(|f) : 
    |[ file k ]| -> arg |[ (k,FILE : s) ]|*
    where <?FILE(<id>)> f => s

/** 
 * Extract requested stream from a descriptor
 */
rules 

  xtc-desc-input :
    |[ descriptor = exec strms ]| -> strm
    where <xtc-stream-input> strms => strm

  xtc-desc-output :
    |[ descriptor = exec strms ]| -> strm
    where <xtc-stream-output> strms => strm

  xtc-desc-error : 
    |[ descriptor = exec strms ]| -> strm
    where <xtc-stream-error> strms => strm

  xtc-desc-input : 
    |[ descriptor = xtservice ]| -> Some(|[ input = file "-i" ]|)

  xtc-desc-input : 
    |[ descriptor = http strms ]| -> strm
    where <xtc-stream-input> strms => strm

/** 
 * Extract requested stream from Streams term
 */
rules 

  xtc-stream-input : 
    Streams(i,_,_) -> i 

  xtc-stream-output : 
    Streams(_,o,_) -> o
 
  xtc-stream-error : 
    Streams(_,_,e) -> e

/** 
 * Extract configuration stream requests and arguments/commands
 */
rules

  xtc-conf-input : 
    XtcConf(Some(i),_,_,_) -> i

  xtc-conf-output : 
    XtcConf(_,Some(o),_,_) -> o

  xtc-conf-error : 
    XtcConf(_,_,Some(e),_) -> e

  xtc-conf-args : 
    XtcConf(_,_,_,a) -> a


