/** 
 * XTC Registration Module
 * 
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-register
imports xtc-repository

/** 
 * Registers XTC registrations/references in the run-time tables
 */
strategies
  
  xtc-register        = xtc-register(|[])

  xtc-register(|meta) =
    ?Registration(n,rs)
  ; <table-union> (XtcReg,n,<xtc-add-meta-all(|meta)> rs)

  xtc-register(|meta) =
    ?Reference(<xtc-add-meta-all(|meta)>)
  ; map(<table-put> (XtcRef,<Fst>,<Snd>))

  xtc-register-component        = xtc-register-component(|[])
  xtc-register-component(|meta) = 
    ?(n,loc)
  ; <xtc-register(|meta)> Registration(n,[(loc,[])])

  xtc-register-component(|meta) = 
    ?(n,loc,meta')
  ; <xtc-register(|meta)> Registration(n,[(loc,meta')])

  xtc-register-resource        = xtc-register-resource(|[])

  xtc-register-resource(|meta) = 
    ?(n,loc)
  ; <xtc-register(|meta)> Registration(n,[(loc,[])])

  xtc-register-resource(|meta) = 
    ?(n,loc,meta')
  ; <xtc-register(|meta)> Registration(n,[(loc,meta')])

  xtc-register-reference = xtc-register-reference(|[])
  xtc-register-reference(|meta) = 
    ?loc
  ; <xtc-register(|meta)> Reference([(loc,[])])

  xtc-register-reference(|meta) = 
    ?(loc,meta')
  ; <xtc-register(|meta)> Reference([(loc,meta')])


strategies

  /** 
   * Adds provided meta-information to all registrations/references
   */
  xtc-add-meta-all(|meta) =
    map(xtc-add-meta(|meta))

  /** 
   * Adds provided meta-information to a registration/reference
   */
  xtc-add-meta(|meta) =
    (id,<union> (<id>,meta))

  xtc-override-meta(|meta) =
    ?(loc,<id>) 
  ; xtc-filter-meta(|meta) 
  ; !(loc,<union> (<id>,meta))

  xtc-filter-meta(|meta) = 
    mapconcat({ c : 
      ?c#(_) 
    ; where(<not(one(?c#(_)))> meta)  
    ; ![<id>] 
   <+ ![] 
    })