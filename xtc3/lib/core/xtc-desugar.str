/** 
 * XTC Desugarings
 * 
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-desugar

strategies

  /** 
   * Model desugaring (descriptor and argument template)
   *
   * @inc xtc-desugar-model
   */
  xtc-desugar-model =
    innermost(xtc-desugar-desc + xtc-desugar-template + xtc-desugar-targ)

  /** 
   * XtcConf desugaring (transforms arguments to 
   * Option/ArgOption terms)
   */
  xtc-desugar-conf = 
    XtcConf(id,id,id,xtc-desugar-args)

 /** 
  * Desugars a list of abstract argument to a list containing only 
  * Option and ArgOption arguments
  */
  xtc-desugar-args = 
    rec r ( 
      ?[x,y | xs] 
    < if <?"--verbose"> x /* <+ <xtc-dashed-option> x ; <not(xtc-dashed-option)> y */ then
        ![<dbg(|"Merging options") ; xtc-desugar-arg> (x,y) | <r> xs]
      else                
        ![<xtc-desugar-arg <+ ![<id>]> x | <conc ; r> ([y],xs)] 
      end
    + [ xtc-desugar-arg <+ ![<id>] | r] <+ [] 
    ) 
  ; concat

  xtc-dashed-option = 
    string-as-chars(['-'|id])

/*
  xtc-desugar-args = 
    mapconcat(xtc-desugar-arg <+ ![<id>])
*/

/** 
 * Rules for argument desugaring
 * 
 * @inc xtc-desugar-arg
 */
rules

  xtc-desugar-arg : 
    (s1,vs) -> a
    where <is-string> s1
        ; <mapconcat({ s2 : \ s2 -> <xtc-desugar-arg> (s1,s2) \})> vs => a

  xtc-desugar-arg : 
    (s1,s2) -> arg |[ (s1,s2) ]|*
    where <is-string> s1
        ; <is-string> s2

  xtc-desugar-arg : 
    (s1,f) -> arg |[ (s1,FILE : s2) ]|*
    where <is-string> s1 
        ; <?FILE(<id>)> f => s2

  xtc-desugar-arg : 
    (s1,u) -> arg |[ (s1,URL : s2) ]|*
    where <is-string> s1 
        ; <?URL(<id>)> u => s2

  xtc-desugar-arg : 
    s -> arg |[ s ]|*
    where <is-string> s 

  xtc-desugar-arg : 
    f -> arg |[ FILE : s ]|*
    where <?FILE(<id>)> f => s

  xtc-desugar-arg : 
    u -> arg |[ URL : s ]|*
    where <?URL(<id>)> u => s

  xtc-desugar-arg : 
    arg |[ q ]| -> arg |[ FILE : s ]|*
    where <(XtcQuery(id) + XtcQuery(id,id))> q
        ; if xtc-find => s then 
            dbg(|"Query in argument resolved")
          else 
            err(|"Unable to resolve query in argument")
          end

  xtc-desugar-arg : 
    arg |[ (s1,q) ]| -> arg |[ (s1, FILE : s2) ]|*
    where <is-string> s1
        ; <(XtcQuery(id) + XtcQuery(id,id))> q
        ; if xtc-find => s2 then 
            dbg(|"Query in argument resolved")
          else 
            err(|"Unable to resolve query in argument")
          end
    
/** 
 * Rules for template argument desugaring
 * 
 * @inc xtc-desugar-targ
 */
rules

  xtc-desugar-targ : 
    |[ tvarTp? tks ]| -> |[ tvarTp? tks { } :: string]|

  xtc-desugar-targ : 
    |[ tvarTp  tks ]| -> |[ tvarTp  tks { } :: string]|

  xtc-desugar-targ : 
    |[ tvarTp+ tks ]| -> |[ tvarTp+ tks { } :: string]|

  xtc-desugar-targ : 
    |[ tvarTp* tks ]| -> |[ tvarTp* tks { } :: string]|

  xtc-desugar-targ : 
    |[ tvarTp? tks tvalTp ]| -> |[ tvarTp? tks { } tvalTp ]|

  xtc-desugar-targ : 
    |[ tvarTp  tks tvalTp ]| -> |[ tvarTp  tks { } tvalTp ]|

  xtc-desugar-targ : 
    |[ tvarTp+ tks tvalTp ]| -> |[ tvarTp+ tks { } tvalTp ]|

  xtc-desugar-targ : 
    |[ tvarTp* tks tvalTp ]| -> |[ tvarTp* tks { } tvalTp ]|

  xtc-desugar-targ : 
    |[ tvarTp? tks tdef ]| -> |[ tvarTp? tks tdef :: string ]|

  xtc-desugar-targ : 
    |[ tvarTp  tks tdef ]| -> |[ tvarTp  tks tdef :: string ]|

  xtc-desugar-targ : 
    |[ tvarTp+ tks tdef ]| -> |[ tvarTp+ tks tdef :: string ]|

  xtc-desugar-targ : 
    |[ tvarTp* tks tdef ]| -> |[ tvarTp* tks tdef :: string ]|

/** 
 * Rules for template desugaring
 * 
 * @inc xtc-desugar-template
 */
rules

  xtc-desugar-template :
    |[ tTp template {} ]| -> 
    |[ tTp template { layout = } ]|

  xtc-desugar-template : 
    |[ implicit template { layout = l } ]| -> 
    |[ regular template { layout = l' } ]|
    where 
      <union> (|[ $input, $output, $error, $arguments ]|*,l) => l'

/** 
 * Rules for descriptor desugaring
 * 
 * @inc xtc-desugar-desc
 */
rules

  xtc-desugar-desc :
    |[ descriptor = xt ]|  -> 
    |[ descriptor = exec 
       streams {
         input  = file "-i"
         output = file "-o"
       } 
    ]|

/** 
 * XTC query desugarings 
 * 
 * @note These terms are usually created by API users
 */
rules 

  xtc-desugar-query : 
    x -> |[ $x ]|
    where <is-string> x

  xtc-desugar-query : 
    (x,v) -> |[ $x[version = <v>] ]|
    where <is-string> x
        ; <is-string> v

  xtc-desugar-query : 
    (x,ms) -> |[ $x[ms] ]|
    where <is-string> x
        ; <is-list> ms

  xtc-desugar-query : 
    ms -> |[ $[ms] ]|
    where <is-list> ms







