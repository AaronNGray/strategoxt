/**
 * XTC Legacy repository conversion
 *  
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc-convert
imports xtc-repository xtc-defaults

/** 
 * Legacy XTC repository signature
 */
signature
  sorts XtcKey
  constructors
    Tool       : String -> XtcKey
    Import     : XtcKey

/** 
 * Convert legacy XTC repository term to new repository term
 */
strategies

  xtc-convert-repository = 
    !Repository(<xtc-convert>)

  xtc-convert =
    restore(
      filter(XtcConvertImport + XtcConvertTool)
    , <err(|"Unable to convert repository")> ()
    )

/** 
 * Convert legacy XTC repository structure to current structure
 *
 * @note Transforms unsupported entries to None() 
 */
rules

  XtcConvertImport : 
    (Import,loc) -> Reference(rs)
    where <map(!(<id>,[]))> loc => rs
    
  XtcConvertTool : 
    (Tool(name),rs) -> Registration(name,rs')
    where <map(XtcConvertToolEntry)> rs => rs'
   
  XtcConvertToolEntry : 
    (ver,loc) -> (loc,rs)
    where <conc> (|[ version = <ver> ]|*,<xtc-default-xt-meta> loc) => rs
