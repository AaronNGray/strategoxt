/**
 * Transformation Tool Composition (XTC) Command-Line Interface
 *  
 * @author Niels Janssen (njanssen@cs.uu.nl)
 */
module xtc 
imports lib xtc-lib
imports xtc-get xtc-call xtc-defaults 
imports Xtd 

overlays

  dependencies = ["parse-xtd"]

strategies

  main-xtc = 
    xtc-call
 <+ xtc-get
 <+ xtc-wrap(
      xtc-options + verbose-option 
    , ( xtc-load-deps ; xtc-reset <+ <set-config> ("no-xtd",()) )
    ; dbg(|"Preloading ready")
    ; xtc-tool
    )    

  xtc-load-deps = 
    where(<map(xtc-find)> dependencies)

  xtc-tool = 
    ( <get-config> "-r" < xtc-init(|<id>) + xtc-init )
  ; ( <get-config> "import"   ; xtc-xtc-reference
    + <get-config> "register" ; xtc-xtc-register
    + <get-config> "query"    
    ; ( <get-config> "-R" ; xtc-xtc-query-repository
      + <get-config> "-a" ; xtc-xtc-query-all 
      + xtc-xtc-query
      )
    )
  ; <exit> 0
 <+ <print> (stderr,["*** warning: xtc failed!\n"])
  ; <exit> 1

  xtc-xtc-register = 
    let 
      version = 
        !|[ version = <<get-config> "-V"> ]|* <+ ![]

      register(|t) =         
        if (<get-config> "-m" <+ <get-config> "--no-defaults") then 
          ![] /* No meta information */
        else 
          <xtc-default-xt-meta> t
        end 
      ; !( t, <concat-strings> [<get-config> "-l","/",t], <id>)       
      ; dbg(|"Registering using defaults")
      ; xtc-register-component(|<version>)

     in
      <get-config> "-t"
    ; map({t,loc,n,meta : 
        ?t
      ; <concat-strings> [<get-config> "-l","/",t] => loc
      ; ( not(<get-config> "no-xtd")
        ; <xtc-load-definition> t => Definition(n,meta)
        ; !(n,loc,meta)
        < dbg(|"Registering using definition file")
        ; ( if <xtc-has-meta(|"Version")> meta then
              xtc-register-component(|meta)
            else 
              xtc-register-component(|<conc> (meta,<version>))
            end <+ register(|t)
          )  
        + register(|t)
        )      
      })
    ; xtc-xtc-save
    end

  xtc-load-definition =
    ?t 
  ; <concat-strings> [<get-config> "-l","/",t,".xtd"] 
  ; if file-exists then 
      !FILE(<id>)
    ; dbg(|"Parsing definition")
    ; xtc-transform(!"parse-xtd")
    else 
      <get-config> "-I"
    ; map(<concat-strings> [<id>,"/",t,".xtd"])
    ; filter(file-exists)
    ; !FILE(<Hd>)
    ; dbg(|"Parsing definition")
    ; xtc-transform(!"parse-xtd")
    end     
  ; read-from
  ; dbg(|"Definition from file")

  xtc-xtc-reference = 
    <get-config> "import"
  ; map(xtc-register-reference)
  ; xtc-xtc-save

  xtc-xtc-query = 
    <get-config> "-t"
  ; map({ n :         
         ?n
       ; ( !(n,<get-config> "-V") <+ id )
       ; ( xtc-find-reg(id) 
         < some(
             where(<get-config> "-m")
           < xtc-has-meta(|<get-config> "-m")
           ; xtc-print(|n)
           + xtc-print(|n)
           )        
         + <print> (stdout, [n, " : no registration", "\n"])
         )
       })

  xtc-xtc-query-all = 
    <table-getlist> XtcReg 
  ; map ({ n : ?(n,<id>) ; map(xtc-print(|n)) })  

  xtc-xtc-query-repository = 
    <print> (stdout,["XTC_REPOSITORY=",<xtc-location>,"\n"])

  xtc-print(|n) = 
    where(<get-config> "-l")
  < <print> (stdout,[<Fst>,"\n"])
  + ?(loc,meta)
  ; (where(<get-config> "-M")
    < <print> (stdout,<conc>([n," ("],<separate-by(!", ")> meta,[") : ",loc,"\n"]))
    + <print> (stdout,[n," (",<xtc-fetch-version <+ !"*"> meta,") : ",loc,"\n"])
    ) 

  xtc-xtc-save = 
    <get-config> "-r" < xtc-save(|<id>) + xtc-save
 
  xtc-options = 
    ArgOption("-r" + "--repository",
        where(<set-config>("-r", <id>)),
        !"--repository rep | -r rep    Tool repository")
  + ArgOption("i" + "imp" + "import",
        where(<extend-config>("import", [<id>])),
        !"import r | i r               Import repository r\n")
  + Option("r" + "reg" + "register",
        where(<set-config>("register", <id>)),
        !"register | r                 Register a tool")
  + ArgOption("-t" + "--tool",
        where(<extend-config>("-t", [<id>])),
        !"--tool n | -t n              Tool name")
  + ArgOption("-l" + "--location",
        where(<set-config>("-l", <id>)),
        !"-l loc  | --location loc     Location of tool")
  + ArgOption("-V" + "--Version",
        where(<set-config>("-V", <id>)),
        !"--Version n | -V n           Version of tool")
  + ArgOption("-m" + "--meta",
        where(<extend-config>("-m", [<read-from-string>])),
        !"-m a | --meta a              Add ATerm a to meta information of tool")
  + Option("--no-defaults",
        where(<set-config>("--no-defaults", <id>)),
        !"--no-defaults                Toggle meta information defaults (default: on)")
  + ArgOption("-I" + "--Include",
        where(<extend-config>("-I", [<id>])),
        !"-I loc | --Include loc       Include XTC definitions from directory\n")

  + Option("q" + "query",
        where(<set-config>("query", <id>)),
        !"query | q                    Query the XTC repository")
  + Option("-a" + "--all",
        where(<set-config>("-a", ())),
        !"--all | -a                   List all registered tools")
  + Option("-L" + "--Location",
        where(<set-config>("-l", ())),
        !"--Location | -L              List only location of tool")
  + Option("-M" + "--Meta",
        where(<set-config>("-M", ())),
        !"-M | --Meta                  Show all meta-information of tool (default: off)")
  + Option("-R" + "--Repository",
        where(<set-config>("-R", ())),
        !"--Repository | -R            Location of repository\n")

  short-description(p) = 
    !["Usage: xtc [-r rep] register (-t tool)+ -V vers -l loc (-m aterm)*\n",
      "       xtc [-r rep] (import r)+\n",
      "       xtc [-r rep] query (-a | (-t tool)+ [-V version] (-m aterm)*) [-L | -M]"]
 
  long-description(p)  = 
    !["The xtc command supports the registration of tools in an XTC repository\n",
      "and querying XTC repositories"]


 