/* This module glues together the components that make up the Stratego
compiler. It is based on XTC, the transformation tool composition
model. */

module strc
imports lib xtc-lib strc-options sugar
strategies

  strc = 
  ( command-line-options
    ; profile'(if-verbose1(<printnl>(stderr, ["-----\ncompilation succeeded:   " | <id>])), 
        compile
      )
    ; if-verbose2(<printnl>(stderr, ["compilation succeeded (", <run-time>, " secs)"]))
    ; <exit> 0
  ) <+ <printnl>(stderr, ["compilation failed (", <run-time>, " secs)"])
       ; <exit> 1

  compile =
    xtc-input(
      front-end
      ; optimize
      ; back-end
      ; c-compile
    )

strategies

// Compiler components
	
  // Collect all modules into one abstract syntax tree

  get-infile :
    FILE(file) -> FILE(basein)
    where <basename> file => basein
	; if-verbose3(debug(!"basein: "))
        ; <set-config> ("basein",  basein)
        ; (<get-config; basename> "-o" <+ !basein) => baseout
	; if-verbose3(debug(!"baseout: "))
        ; <set-config> ("baseout", baseout)

  get-outfile(suffix) =
    <conc-strings>(<get-config> "baseout", <suffix>)

  dep-name =
    !["--dep", <<get-config> "-o" <+ <get-config> "baseout">]

strategies

  front-end =
    if-verbose2(debug(!"front-end: "));
    profile'(if-verbose1(<printnl>(stderr, ["front-end succeeded:     " | <id>])), 
      parse
      ; output-ast
      ; add-main
      ; frontend
      ; output-frontend
      ; extract
      ; warnings
      ; stratego-nf 
      ; rename-defs
    )

  parse =
    get-infile
    ; xtc-transform(!"pack-stratego", 
	   <concat> [<dep-name>, <get-config> "-I", <pass-verbose>, <pass-keep>])

  output-ast =
    try(where(<get-config> "--ast")
       ; copy-to(get-outfile(!".ast") => astfile)
       ; where(<echo> ["abstract syntax (after parsing) written to file"])
       ; xtc-io-exit
    )

  output-frontend =
    try(where(<get-config> "-F")
       ; (extract-all <+ <fatal-error> ["extracting all definitions failed"])
       ; copy-to(get-outfile(!".tree") => file)
       ; say(!["abstract syntax (after frontend) written to file"])
       ; xtc-io-exit
    )

  // Adding main strategy
  // What happens if the specification already contains a main strategy? 

  AddMain(m) : 
    Specification(sects) -> 
    Specification([Strategies([SDef("main", [], Call(SVar(<m>()), []))])
                  | sects])

  add-main =
    try(where(<get-config> "-m" => m; if-verbose2(debug(!"main strategy is: ")))
        ; xtc-io-transform(AddMain(!m)))
    ; if-keep3(
        copy-to(get-outfile(!".with-main") => file)
        ; say(!["abstract syntax (before frontend) written to ", file])
      )

  frontend = 
    xtc-transform(!"frontend", !["-b" | <pass-verbose> ])
    ; if-keep2(
        copy-to(get-outfile(!".fro") => file)
        ; say(!["abstract syntax (after frontend) written to ", file])
      )

  extract = 
    xtc-transform(!"extract", !["-b" | <pass-verbose> ])

  warnings = 
    xtc-transform(!"stratego-warnings", 
       <concat>[<pass-verbose>, ["-b"]|
	        <get-config; map(!["--warning", <id>])> "--warning"])

  extract-all = 
    xtc-transform(!"extract-all", !["-b" | <pass-verbose> ])

  rename-defs = 
    xtc-transform(!"rename-defs", !["-b" | <pass-verbose> ])

  stratego-nf = 
    xtc-transform(!"Stratego-Normal-Format", !["-b" | <pass-verbose> ])

strategies

  optimize =
    if-verbose2(debug(!"optimize: "));
    profile'(if-verbose1(<printnl>(stderr, ["optimization succeeded:  " | <id>])), 
        olevel1(optimize1)
        ; olevel2(try(where(not(<get-config> "--fusion")); fusion))

        ; olevel4(inline)
	; olevel4(dead-def-elim)

        ; olevel4(optimize2)
        ; define-congruences
        ; olevel4(const-prop; bound-unbound-vars; dead-var-elim)
        ; olevel4(optimize2)

        ; olevel2(compile-match)
	; olevel2(desugar-case)

        ; olevel3(optimize2)
        ; olevel3(const-prop; bound-unbound-vars; dead-var-elim)
        ; olevel1(optimize2)

        ; stratego-of
        ; output-optimized
    )

  optimize1 = 
    xtc-transform(!"optimize1", !["-b" | <pass-verbose> ])

  fusion = 
    xtc-transform(!"fusion", !["-b" | <pass-verbose> ])

  inline = 
    xtc-transform(!"inline", !["-b" | <pass-verbose> ])

  dead-def-elim = 
    xtc-transform(!"dead-def-elim", !["-b" | <pass-verbose> ])

  compile-match = 
    xtc-transform(!"compile-match", !["-b" | <pass-verbose> ])

  desugar-case = 
    xtc-transform(!"desugar-case", !["-b" | <pass-verbose> ])

  const-prop = 
    xtc-transform(!"const-prop", !["-b" | <pass-verbose> ])

  bound-unbound-vars = 
    xtc-transform(!"bound-unbound-vars", !["-b" | <pass-verbose> ])

  dead-var-elim = 
    xtc-transform(!"dead-var-elim", !["-b" | <pass-verbose> ])

  optimize2 = 
    xtc-transform(!"optimize2", !["-b" | <pass-verbose> ])

  define-congruences = 
    xtc-transform(!"define-congruences", !["-b" | <pass-verbose> ])

  stratego-of = 
    xtc-transform(!"Stratego-Optimized-Format", !["-b" | <pass-verbose> ])

  canonicalize = 
    xtc-transform(!"canonicalize", !["-b" | <pass-verbose> ])
 
  bound-unbound-vars = 
    xtc-transform(!"bound-unbound-vars", !["-b" | <pass-verbose> ])
 
  output-optimized =
    try(where(
	  debug(!"output-optimized: ");
          where(<get-config> "--output-optimized")
          ; copy-to(get-outfile(!".opt") => file1)
          ; xtc-transform(!"pp-stratego", !["--abstract" | <pass-verbose> ] )
          ; copy-to(get-outfile(!".opt.txt") => file2)
          ; say(![" (after frontend) written to file"])
    ))

  output-canonical =
    try(where(
	  debug(!"output-canonical: ");
          where(<get-config> "--output-canonical")
          ; copy-to(get-outfile(!".can") => file1)
          ; xtc-transform(!"pp-stratego", !["--abstract", "--annotations" | <pass-verbose> ] )
          ; copy-to(get-outfile(!".can.txt") => file2)
          ; say(![" (after frontend) written to file"])
    ))

strategies

  back-end =
    if-verbose2(debug(!"back-end: "));
    profile'(if-verbose1(<printnl>(stderr, ["back-end succeeded:      " | <id>])), 
      canonicalize
      ; olevel1(bound-unbound-vars)
      ; stratego-cf
      ; output-canonical
      ; s2c
      ; ac2abox
      ; abox2text
      ; rename-to(get-outfile(!".c"))
    )

  stratego-cf = 
    xtc-transform(!"Stratego-Canonical-Format", !["-b" | <pass-verbose> ])

  s2c =
    xtc-transform(!"s2c",  
	 <concat>[<pass-verbose>, ["-b" ]
	         |<get-config; map(!["--C-include", <id>])> "--C-include"])
			  
		  // <TraceAllFuns; !["--trace-all"] <+ get-traced-funs> () )

  get-traced-funs =
    <table-getlist> "TraceFun";
    map(\ (f, _) -> ["-t", f] \ );
    concat;
    if-verbose1([] <+ debug(!"traced functions: "))


  // Pretty-printing generated code

  ac2abox = 
    xtc-transform(!"ast2abox", !["-p", <xtc-find> "C-pretty.pp"])

  abox2text = 
    xtc-transform(!"abox2text")


strategies

  // Compile generated C code

  c-compile =
     where(<get-config> "-c")
  <+ profile'(if-verbose1(<printnl>(stderr, ["C compilation succeeded: " | <id>])), 
       c-to-object-code 
       ; link-object-code
     )

  c-to-object-code :
    FILE(cfile) -> FILE(ofile)
    where if-verbose2(say(!"compiling C code"))
	; get-outfile(!".o") => ofile
        ; <if-verbose3(debug); call> 
	   ("gcc", <conc> (<get-config> "-CI", 
			  ["-c", cfile,"-o", ofile]))

  link-object-code :
    FILE(ofile) -> FILE(target)
    where if-verbose2(say(!"linking object code"))
	; (<get-config> "-o" <+ get-outfile(!"")) => target
        ; <if-verbose3(debug); call> 
	    ("gcc", [ofile, "-o", target | <get-config> "-CL"])

