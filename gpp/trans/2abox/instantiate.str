/**
 * @author Merijn de Jonge (mdjonge@cwi.nl)
 */
module instantiate
imports pp-table
rules

  instantiate:
    (t, args) -> instantiated
    where
      <bottomup(try(Instantiate(|args) + flat-list ))> t => instantiated

  Instantiate(|args) :
    Arg(n) -> <index> (n, args)

  Instantiate(|args) :
    Arg2(n, m) -> <index> (m, list)
    where
      <index> (n, args) => list

strategies

  instantiate-list(|pp-entry) =
    rec x(
      {sym,xs:
        [] <+ ?[sym|xs]; [<instantiate> (pp-entry, [sym]) | !xs;x ]
      }
    )
  
  instantiate-sep-list(|pp-entry) =
    rec x(
      {sym,sep,xs:
         []
      <+ [id]
      <+ ?[sym,sep|xs]
         ; [<instantiate> (pp-entry, [sym, sep]) | !xs;x ]
      }
    )
