module abox2latex
imports options xtc-lib

strategies

  io-abox2latex =
    option-wrap(abox2latex-options, xtc-io(abox2latex))

  abox2latex-options =
        input-option
    +  output-option
    + general-options
    + ArgOption("-t"
      , where(<extend-config> ("--latex-tables", [<id>]))
      , !"-t file   Table containing box to latex macro mappings"
      )
    + ArgOption("-w", <set-config> ("--width",  <id>))

  abox2latex =
xtc-io-transform-text(
    ![ <create-header> "Test"
      , <xtc-transform(!"Abox-2-latex",
        create-abox2latex-args)>
      ])

  create-abox2latex-args =
      !["-t", <create-table-file> () | <pass-verbose> ()]
    ; try(!["-w", <<get-config> "--width"> | <id>])

  create-table-file =
      (<get-config> "--latex-tables" <+ ![])
    ; map(ReadFromFile)
    ; ![ [["->"   , "\\ensuremath{\\rightarrow}"],
          [ "\\/" , "\\ensuremath{\\vee}"],
          [ "/\\" , "\\ensuremath{\\wedge}"],
          [ "=="  , "\\ensuremath{\\equiv}"],
          [ "++"  , "\\ensuremath{+\\kern-.4em+}"],
          [ "context-free syntax" , "context-free~syntax"]
         ]
       | <id> ]
    ; concat
    ; write-to 
    ; ?FILE(<id>)

  create-header = 
    !["% \n",
      "% This file is generated automatically on `date +%c` by \n",
      "% \n",
      "%    abox2latex (C) 2001 Merijn de Jonge (mdejonge@cwi.nl) \n",
      "% \n",
      "% Tokens from the BOX term are formatted using the LaTeX macros  \n",
      "% defined in the table(s):  \n",
      <tables2text>(),
      "% \n",
      "% To use this file add: \n",
      "%    \\usepackage{boxenv} to your documents preamble \n",
      "% and  \n",
      "%    \\input{<this_file>}  \n",
      "% to your LaTeX document. \n",
      "% \n",
      "% If you don't have boxenv.sty already, generate it with:: \n",
      "%    gen-latex-boxstyle -o boxenv.sty \n",
      "% \n"
    ]; concat-strings


// Create list of abbreviation tables
tables2text =
      (<get-config> "--latex-tables" <+ ![]);
      map( !["%   ", <id>, "\n"]; concat-strings );
      concat-strings
      

/*
usage:
  abox2latex [-h] [-i abox] [-o latex] [-t table ...] [-w width]

where
  -h          Displays usage information.
  -i abox     Specifies the BOX term to transform.
  -o latex    Specifies the file name of the resulting LaTeX term.
  -t table    Table containing box to latex macro mappings
  -w          Specifies max width of output

This tool reads from standard input (or from the file specified with the
-i option) a BOX term containing formatting information and writes to
standard output (or to the file specified with the -o option)
corresponding LaTeX code.
*/
