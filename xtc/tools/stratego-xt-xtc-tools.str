module stratego-xt-xtc-tools
imports xtc-proc

strategies

  xtc-implode-asfix =
    xtc-transform(!"implode-asfix", pass-verbose)

  /* box */

  xtc-abox-format =
    xtc-transform(!"abox-format", pass-verbose)

  xtc-ast2abox(pps) =
    xtc-transform(!"ast2abox",
      <conc> (
        <pass-verbose> ()
      , <pps; map(xtc-find); <add-arg-flags> ("-p", <id>)> ()
      )
    )

  xtc-abox2text =
    xtc-transform(!"abox2text", pass-verbose)

  xtc-abox2text(width) =
    xtc-transform(!"abox2text", <conc> (<width> (), <pass-verbose> ()))

  xtc-parse-pp-table =
    xtc-transform(!"parse-pp-table")

  /* sdf */

  xtc-parse-sdf =
      xtc-sglr(!"sdf.cons.tbl")
    ; xtc-implode-asfix

  xtc-pp-sdf =
      xtc-transform(!"sdf-bracket", pass-verbose)
    ; xtc-ast2abox(!["sdf.cons.pp.af"])
    ; xtc-abox2text

  // watch out: does also implode-asfix
  xtc-pack-sdf(includes) =
      xtc-transform(!"pack-sdf",
        <conc> (
          <pass-verbose> ()
        , <includes; <add-arg-flags> ("-I", <id>)> ()
        )
      )
    ; xtc-implode-asfix

  /* sglr */

  xtc-sglr(tbl) =
    xtc-transform(!"sglr", !["-2tA", "-p", <tbl; xtc-find> () | <pass-v-verbose> ()])

  xtc-sglr(tbl, sort) =
    xtc-transform(!"sglr", !["-2tA", "-p", <tbl; xtc-find> (), "-s", <sort> () | <pass-v-verbose> ()])

  /* tools */

  pass-v-verbose =
      ![]; if-verbose3(!["-v"])

  // :: String * [String] -> [String]
  add-arg-flags =
       \ (_   , []    ) -> [] \
    <+ \ (flag, values) -> [flag | <<separate-by> (flag, <id>)> values ] \


