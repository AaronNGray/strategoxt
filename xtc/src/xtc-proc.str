module xtc-proc
imports xtc-rep config scoped-finite-map options

signature
  constructors
    FILE : String -> File
    DIR  : String -> File
    TEMP : XtcKey

    TempFiles : Table

strategies

  // reading and writing files

  rename-to(name) :
    FILE(oldname) -> FILE(newname)
    where name => newname
	; not(stdout)
        ; <rename-file>(oldname, newname)

  rename-to(name) :
    FILE(oldname) -> stdout
    where name => stdout
	; <copy-file> (oldname, stdout)
	; <remove-file> oldname

  copy-to(name) :
    FILE(oldname) -> FILE(oldname)
    where name => newname
	; not(stdout + stderr)
        ; <copy-file>(oldname, newname)

  copy-to(name) :
    FILE(oldname) -> FILE(oldname)
    where name => file
        ; (stdout + stderr)
	; <copy-file> (oldname, file)

  read-from :
    FILE(name) -> t
    where <ReadFromFile> name => t

  read-from :
    stdin -> t
    where <ReadFromFile> stdin => t

  write-to :
    t -> FILE(name)
    where xtc-new-file => name
        ; <WriteToBinaryFile> (name, t)
        ; <close-file> name

  write-to-text :
    t -> FILE(name)
    where xtc-new-file => name
        ; <WriteToTextFile> (name, t)
        ; <close-file> name

  // :: [String] -> FILE
  print-to :
    strings -> FILE(name)
      where xtc-new-file => name
          ; <print> (name, strings)
          ; <close-file> name

  xtc-find-file = !FILE(<xtc-find>)


  // XTC-TEMP-FILES : scoped temporary files

  xtc-new-file =
    new-file => f
    ; where(<open-file>(f, "w")
	    ; <assert(!TempFiles)> (f, ()))

  xtc-remove-temporaries =
    where(
      <table-get>(XTC, TEMP) => fs
      ; map(try(remove-file))
    )

  xtc-temp-files(s) = 
    begin-scope(!TempFiles)
    ; restore-always(s,
        where((<table-get>(TempFiles, Scopes) <+ ![[]]) => [scope | scopes]
            ; <map(try(remove-file))> scope)
        ; end-scope(!TempFiles)
      )


  // XTC-IO : 'read' input file, transform, and 'write' to output file
  xtc-io(s) =
    xtc-output(
        (!FILE(<get-config> "-i") <+ !stdin)
      ; s
    )

  // XTC-OUTPUT : transform, and 'write' to output file
  xtc-output(s) =
    xtc-temp-files(
        s
      ; copy-to(<get-config> "-o" <+ !stdout)
    )

  // XTC-INPUT : 'read' input file, transform
  xtc-input(s) =
    xtc-temp-files(
        (!FILE(<get-config> "-i") <+ !stdin)
      ; s
    )

  // io-wrap for xtc tools: 'read' -i option, transform, and 'write' to -o option
  xtc-io-wrap(s)             = xtc-io-wrap(fail, s)
  xtc-io-wrap(extra-opts, s) = option-wrap(extra-opts <+ io-options, xtc-io(s))

  xtc-iowrap(s)              = xtc-io-wrap(s)
  xtc-iowrap(extra-opts, s)  = xtc-io-wrap(extra-opts, s)

  // XTC-COMMAND
  // Executing an external process
  // a -> String :: List(String) -> List(String)

  xtc-command(tool) = 
    where(tool; xtc-find-warning => loc)
    ; where(<call> (loc, <id>))

  // Transparent transformation of terms and files by external processes

  xtc-transform(tool) =
    xtc-transform(tool, ![])

  xtc-transform(tool, args) =
    (FILE(id) + stdin)
    < xtc-transform-file(tool, args)
    + xtc-transform-term(tool, args)

  // Transforming a file with an external process

  xtc-transform-file =
    ?(tool, args, file)

  xtc-transform-file(tool) =
    xtc-transform-file(tool, ![])

  xtc-transform-file(tool, args) :
    FILE(f) -> FILE(g)
    where <xtc-new-file> f => g
        ; <conc; xtc-command(tool)> (<args>, ["-i", f, "-o", g])
        ; <close-file> g

  xtc-transform-file(tool, args) :
    stdin -> FILE(g)
    where xtc-new-file => g
        ; <conc; xtc-command(tool)> (<args>, ["-o", g])
        ; <close-file> g

  // Transforming a term with an external process

  xtc-transform-term(tool, args) = 
    write-to
    ; xtc-transform-file(tool, args)
    ; read-from

  // Transforming a file with an internal strategy

  xtc-io-transform(s) =
    read-from; s; write-to

  // Transforming a file (containing an ATerm) to text with an internal strategy
  xtc-io-transform-text(s) =
      read-from; s; print-to

  // Generating a file with an external process
  xtc-generate(tool) =
    xtc-generate(tool, ![])

  xtc-generate(tool, args) :
    _ -> FILE(g)
    where <xtc-new-file> () => g
        ; <conc; xtc-command(tool)> (<args>, ["-o", g])
        ; <close-file> g

