module xtc-proc
imports xtc-rep scoped-finite-map

signature
  constructors
    FILE : String -> File
    DIR  : String -> File
    TEMP : XtcKey

    TempFiles : Table

strategies

  // reading and writing files

  rename-to(name) :
    FILE(oldname) -> FILE(newname)
    where name => newname
	; not(stdout)
        ; <rename-file>(oldname, newname)

  rename-to(name) :
    FILE(oldname) -> stdout
    where name => stdout
	; <copy-file> (oldname, stdout)
	; <remove-file> oldname
        //; <ReadFromFile> oldname => term
	//; <WriteToTextFile> (stdout, term)

  read-from :
    FILE(name) -> t
    where <ReadFromFile> name => t

  read-from :
    stdin -> t
    where <ReadFromFile> stdin => t

  write-to :
    t -> FILE(name)
    where xtc-new-file => name
        ; <WriteToTextFile> (name, t)


  // XTC-IO

  // 'read' input file, transform, and 'write' to output file

  xtc-io(s) =
    (!FILE(<get-config> "-i") <+ !stdin)
    ; s
    ; rename-to(<get-config> "-o" <+ !stdout)


  // XTC-TEMP-FILES

  // scoped temporary files

  xtc-new-file =
    new-file => f
    ; where(<assert(!TempFiles)> (f, ()))
    //; where(<table-push>(XTC, TEMP, f))

  xtc-remove-temporaries =
    where(
      <table-get>(XTC, TEMP) => fs
      ; map(try(remove-file))
    ) 

  xtc-temp-files(s) = 
    begin-scope(!TempFiles)
    ; s
    ; where((<table-get>(TempFiles, Scopes) <+ ![[]]) => [scope | scopes]
            ; <map(try(remove-file))> scope)
    ; end-scope(!TempFiles)


  // XTC-COMMAND
  // Executing an external process
  // a -> String :: List(String) -> List(String)

  xtc-command(tool) = 
    where(tool; xtc-find-warning => loc)
    ; where(<call> (loc, <id>))


  // Transparent transformation of terms and files by external processes

  xtc-transform(tool) =
    xtc-transform(tool, ![])

  xtc-transform(tool, args) =
    xtc-transform-file(tool, args)
    <+ xtc-transform-term(tool, args)
  
  // Transforming a file with an external process

  xtc-transform-file(tool) =
    xtc-transform-file(tool, ![])

  xtc-transform-file(tool, args) :
    FILE(f) -> FILE(g)
    where <xtc-new-file> f => g
        ; <xtc-command(tool)> ["-i", f, "-o", g | <args>]

  xtc-transform-file(tool, args) :
    stdin -> FILE(g)
    where xtc-new-file => g
        ; <xtc-command(tool)> ["-o", g | <args>]

  // Transforming a term with an external process

  xtc-transform-term(tool, args) = 
    write-to
    ; xtc-transform-file(tool, args)
    ; read-from

  // Transforming a file with an internal strategy

  xtc-io-transform(s) =
    read-from; s; write-to
