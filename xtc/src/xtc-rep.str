module xtc-rep
imports exec tables config
signature
  sorts XtcKey
  constructors
    Tool       : String -> XtcKey
    Repository : XtcKey 
    Import     : XtcKey
    Imported   : String -> XtcKey

    XTC	       : Table

strategies

  // Loading the repository

  xtc-location =
    <get-config> "-r"
    <+ <getenv> "XTC_REPOSITORY"
    <+ prim("XTC_REPOSITORY")

  xtc-read = ?rep
    ; not(<table-get>(XTC, Imported(rep)))
    ; where(<table-put>(XTC, Repository, [rep]))
    ; if-verbose4(debug(!"Reading repository: "))
    ; (file-exists ; ReadFromFile <+ ![])
    ; if-verbose6(say(!"Repository read: "))
    ; <table-putlist(table-append)>(XTC, <id>)
    ; if-verbose6(say(!"Repository stored in table: "))
    ; <table-put>(XTC, Imported(rep), [])
    ; if-verbose4(say(!"Repository read: "))

  xtc-import = 
    try(<table-get>(XTC, Import)
        ; map(try(xtc-read)))

  xtc-load =
    if-verbose5(debug(!"xtc-load: ")); 
    where(
      <table-get>(XTC, Repository)
      <+ xtc-location
         ; xtc-read
	 ; xtc-import
    )
    ; if-verbose5(debug(!"xtc-loaded: "))

  xtc-store =
    if-verbose1(say(!"  Saving repository "))
    ; <WriteToTextFile> (<get-config> "-r", <table-getlist> XTC)



