module xtc-rep
signature
  sorts XtcKey
  constructors
    Tool       : String -> XtcKey
    Repository : XtcKey 
    Import     : XtcKey
    Imported   : String -> XtcKey

    XTC	       : Table

/**
 * Loading the repository
 */
strategies

  /**
   * Returns the location of the XTC repository.
   *
   * @type _ -> String
   */
  xtc-location =
    <get-config> "-r"
    <+ <getenv> "XTC_REPOSITORY"
    <+ GetInternalDefaultXtcRepository

  external GetInternalDefaultXtcRepository(|)

  xtc-read = 
      ?rep
    ; not(<table-get>(XTC, Imported(rep)))
    ; where(<table-put>(XTC, Repository, [rep]))
    ; log(|Vomit(),<conc-strings>("Reading repository ",rep))
    ; read-repository-file
    ; log(|Vomit(),<conc-strings>("Read repository ",rep))
    ; <table-putlist(table-append)>(XTC, <id>)
    ; log(|Vomit(),"Repository stored in table",<id>)
    ; <table-put>(XTC, Imported(rep), [])
    ; log(|Vomit(),"Registered repository as imported")

  read-repository-file = 
    if can-read-file then
      ReadFromFile
    else
      if file-exists then
          log(|Error(), <conc-strings> ("no read permission for repository ", <id>))
      else
        if not(<get-config> "register") then
          log(|Error(), <concat-strings> ["repository ", <id> , " does not exist"])
        end
      end
      ; ![]
    end

  xtc-import = 
    where(
      repeat(<table-get>(XTC, Import)
             ; filter(xtc-read)
             ; not([]))
    )

  xtc-load =
    log(|Debug(),"Loading repository") 
  ; where(
      <table-get>(XTC, Repository)
      <+ xtc-location
         ; xtc-read
	 ; xtc-import
    )
  ; log(|Vomit(),"Loaded repository ")

  xtc-store =
      log(|Debug(),<conc-strings>("Saving repository ",<id>))
    ; <get-config> "-r"
    ; if can-write-repository then
       <WriteToBinaryFile> (<id>, <table-getlist> XTC)
      else
        log(|Error(), <conc-strings> ("no write permission for repository ", <id>))
      ; fail
      end

  can-write-repository =
    file-exists < can-write-file + id
