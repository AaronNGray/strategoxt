module xtc-query
imports xtc-lib config
strategies

  xtc-query = 
    repository-query
    <+ all-query
    <+ tool-query

  repository-query =
    <get-config> "-R"
    ; xtc-location
    ; <print>(stdout, ["XTC_REPOSITORY=", <id>, "\n"])

  all-query =
    <get-config> "-a"
    ; xtc-import
    ; <table-getlist> XTC
    ; map(try((Tool(id),id); list-tool))

  list-tool =
    ?(Tool(tool), <id>)
    ; map({version,loc: ?(version, loc);
           (<get-config> "-l"; <print>(stdout, [loc, "\n"])
            <+ <print>(stdout, [tool, " (", version, ") : ", loc, "\n"]))})

  tool-query =
    <get-config> "-t"
    ; xtc-import
    ; map({tool: ?tool;
           (if-verbose3(debug(!"Retrieving tool: "))
            ; <table-get> (XTC, Tool(tool)) => entries
            ; <version-query <+ list-tool> (Tool(tool), entries)
            <+ <print>(stdout, [tool, " : no registration", "\n"]))})

  version-query =
    where(<get-config> "-V" => version)
    ; (?Tool(tool), fetch(?(version, loc)))
    ; <list-tool> (Tool(tool), [(version,loc)])

