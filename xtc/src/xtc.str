module xtc
imports lib xtc-lib xtc-query xtc-call xtc-get
strategies

  xtc = 
    xtc-xtc-call
    <+ xtc-xtc-get
    <+ xtc-main

  xtc-main = 
    parse-options(xtc-options)
    ; xtc-location
    ; log(|Notice(),<conc-strings>("Using repository ",<id>))
    ; xtc-read
    ; ((<get-config> "register"; xtc-register
        <+ <get-config> "import"; register-import)
       ; xtc-store
       <+ <get-config> "query"; xtc-query)
    ; <exit> 0
   <+ err(|"xtc failed! ")

  xtc-options =
    ArgOption("-r" + "--repository",
        where(<set-config>("-r", <id>)),
        !"--repository rep | -r rep    Tool repository")

  + ArgOption("i" + "imp" + "import",
        where(<extend-config>("import", [<id>])),
        !"import r | i r               Import repository r\n")

  + Option("r" + "reg" + "register",
        where(<set-config>("register", <id>)),
        !"register | r                 Register a tool")

  + ArgOption("-t" + "--tool",
        where(<extend-config>("-t", [<id>])),
        !"--tool n | -t n              Tool name")

  + ArgOption("-l" + "--location",
        where(<set-config>("-l", <id>)),
        !"-l loc  | --location loc     Location of tool")

  + ArgOption("-V" + "--Version",
        where(<set-config>("-V", <id>)),
        !"--Version n | -V n           Version of tool\n")

  + Option("q" + "query",
        where(<set-config>("query", <id>)),
        !"query | q                    Query the XTC repository")

  + Option("-a" + "--all",
        where(<set-config>("-a", ())),
        !"--all | -a                   List all registered tools")

  + Option("-L" + "--Location",
        where(<set-config>("-l", ())),
        !"--Location | -L              List only location of tool")

  + Option("-R" + "--Repository",
        where(<set-config>("-R", ())),
        !"--Repository | -R            Location of repository\n")

  + ArgOption("--verbose",
        where(<set-config> ("--verbose", <verbose-to-int>)),
        !"--verbose i                  Verbosity level i (default 1)")


  short-description(p) = 
    !["Usage: xtc [-r rep] register -t tool -V vers -l loc\n",
      "       xtc [-r rep] (import r)+\n",
      "       xtc [-r rep] query (-a | -t tool [-V version]) [-L]"]
 
  long-description(p)  = 
    !["The xtc command supports the registration of tools in an XTC repository\n",
      "and querying XTC repositories"]


/*
 * Temp functions to avoid bootstrapping problems
 */
   verbose-to-int = 
      string-to-level
        <+
      string-to-int

  string-to-level : "emergency" -> level
    where <verbose-level> Emergency() => level
    
  string-to-level : "alert"     -> level
    where <verbose-level> Alert() => level

  string-to-level : "critical"  -> level
    where <verbose-level> Critical() => level
    
  string-to-level : "error"     -> level
    where <verbose-level> Error() => level
    
  string-to-level : "warning"   -> level
    where <verbose-level> Warning() => level
    
  string-to-level : "notice"    -> level
    where <verbose-level> Notice() => level
    
  string-to-level : "info"      -> level
    where <verbose-level> Info() => level
    
  string-to-level : "debug"     -> level
    where <verbose-level> Debug() => level
    
  string-to-level : "vomit"     -> level
    where <verbose-level> Vomit() => level
