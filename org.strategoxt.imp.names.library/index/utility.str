module index/utility

rules
  
  /**
   * Gets the URI part for given term. Can be extended by defining a index-uri-impl rule. If no index-uri-impl rule
   * is defined for the given term the first subterm is used as the URI. Fails if no URI can be retrieved from the
   * given term.
   *
   * Example:
   *   <index-uri> DefData([Entity(), "Bar", "Baz"], Type(), TYPE("Bar")) => [Entity(), "Bar", "Baz"]
   *
   * @type elem -> ?uri
   *
   * @see index-uri-impl
   * @see index-uri-generic
   */ 
  index-uri = 
    index-uri-impl <+ index-uri-generic
    
  /**
   * Gets the value part for given term. Can be extended by defining a index-value-impl rule. If no index-value-impl 
   * rule is defined for the given term the second subterm is used as the value. Fails if no value can be retrieved
   * from the given term.
   *
   * Example:
   *   <index-value> DefData([Entity(), "Bar", "Baz"], Type(), TYPE("Bar")) => TYPE("Bar")
   *
   * @type elem -> ?value
   *
   * @see index-value-impl
   * @see index-value-generic
   */  
  index-value = 
    index-value-impl <+ index-value-generic

rules /** @internal */

  /** @internal */
  index-uri-impl:
    [_|_] -> <id>

  /** @internal */
  index-value-impl:
    [_|_] -> <id>

  /** @internal */
  index-uri-generic:
    term -> <?_#(<?[<id>|_]>)> term
    
  /** @internal */
  index-value-generic:
    term -> <?_#(<?[_, <id>|_]> )> term
