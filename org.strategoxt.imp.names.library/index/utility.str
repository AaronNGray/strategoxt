module index/utility

rules
  
  /**
   * Gets the URI part for given term. Can be extended by defining a index-uri-impl rule. If no index-uri-impl rule
   * is defined for the given term the first subterm is used as the URI. Fails if no URI can be retrieved from the
   * given term.
   *
   * Example:
   *   <index-uri> DefData([Entity(), "Bar", "Baz"], Type(), TYPE("Bar")) => [Entity(), "Bar", "Baz"]
   *
   * @type elem -> ?uri
   *
   * @see index-uri-impl
   * @see index-uri-generic
   */ 
  index-uri = 
    index-uri-impl <+ index-uri-generic
    
  index-uris =
  	index-uris-impl <+ index-uris-generic
    
  /**
   * Gets the value part for given term. Can be extended by defining a index-value-impl rule. If no index-value-impl 
   * rule is defined for the given term the second subterm is used as the value. Fails if no value can be retrieved
   * from the given term.
   *
   * Example:
   *   <index-value> DefData([Entity(), "Bar", "Baz"], Type(), TYPE("Bar")) => TYPE("Bar")
   *
   * @type elem -> ?value
   *
   * @see index-value-impl
   * @see index-value-generic
   */  
  index-value = 
    index-value-impl <+ index-value-generic
    
  index-values = 
    index-values-impl <+ index-values-generic

rules /** @internal */

  /** @internal */
  index-uri-impl = fail

  /** @internal */
  index-uri-generic:
    term -> <?_#(<?[<id>|_]>)> term
    
 
  /** @internal */
  index-uris-impl = fail 
    
  /** @internal */
  index-uris-generic:
    terms -> <filter(index-uri)> terms
    where
    	<is-list> terms
    	
  /** @internal */  	
  index-uris-generic:
    term -> [<index-uri> term]
    where
    	not(<is-list> term)
    
    
  /** @internal */
  index-value-impl = fail
    
  /** @internal */
  index-value-generic:
    term -> <?_#(<?[_, <id>|_]> )> term
    

  /** @internal */
  index-values-impl = fail
    
  /** @internal */
  index-values-generic:
    terms -> <filter(index-value)> terms
    where
    	<is-list> terms
    	
  /** @internal */  	
  index-values-generic:
    term -> [<index-value> term]
    where
    	not(<is-list> term)
