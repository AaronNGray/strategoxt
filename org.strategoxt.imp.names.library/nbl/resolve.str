module nbl/resolve

imports
  
  nbl/collect
  nbl/tasks
  nbl/query
  nbl/lookup
  nbl/uri
  nbl/entries
  index/core
  
signature

  constructors
    
    COMPLETION : String -> COMPLETION
      
rules // Reference resolution
  
  nbl-resolve = 
    has-annos;
    get-annos;
    collect-one(nbl-resolve)
  
  nbl-resolve:
    Use(Result(number)) -> definition
    where
      uri        := <nbl-get-value> DoneTask(number, ());
      definition := <nbl-get> Def(uri)

rules // Code completion

  nbl-unresolved-uris = 
    has-annos;
    get-annos;
    collect-all(nbl-unresolved-uri)
      
  nbl-unresolved-uri:
    Use(result) -> <nbl-resolve-result> result

  nbl-propose-completions:
    ast -> proposals'
      where
        item        := <collect-one(?COMPLETION(_))> ast;
        index-transaction(
          uris      := <nbl-unresolved-uris> item;
          proposals := <mapconcat(nbl-visible-definitions)> uris
        );
        proposals'  := <map(nbl-uri; nbl-uri-name)> proposals

rules // Resolving URI's
  
  nbl-resolve-result:
    Result(number) -> <nbl-get-value> DoneTask(number, ())
  
  nbl-resolve-result:
    Result(number) -> <nbl-get-value> FailTask(number, ())

rules // Conditions
	
  nbl-is-reference =
    ?Use(_)
    
  nbl-has-reference =
  	collect-one(nbl-is-reference)
 
  nbl-is-unresolved =
  	?node;
    has-annos;
    get-annos;
    nbl-has-reference;
    <not(nbl-resolve)> node
