	module nbl/tasks

imports 
	
	nbl/-
	
signature

	sorts
	
		Task Result Instruction
	
	constructors
		
		Task  : String * List(String) * Instruction -> Task
		Result: String         											-> Result
		DoneTask: String * Term											-> DoneTask
		
		Resolve  : List(Segment) * List(Prop) * ListImport()	-> Instruction
		Resolve  : Segment * Term * List(Prop) * ListImport() -> Instruction
		PropCalc : Property * Term                          	-> Instruction
		Match		 : ID * Term			                          	-> Instruction
		Done     : Term                                   		-> Instruction

rules
	
	new-task = new-task(|<dependent-tasks>)
		
	new-task(|dep):
		instruction -> (Result(name), <debug(!"created task ")> Task([name], dep, instruction))
		where
			name := <newname> ""
			
	dependent-tasks = collect-all(dependent-task, union); flatten-list; make-set
	dependent-task = ?Result(<id>) 
	dependent-task =
		has-annos; get-annos; dependent-tasks
	
rules
	
	merge-tasks =
		   [] 
		<+ ?[Task(names, dep, instr)|<fetch(Task(!(names, <id>); conc, !(dep, <id>); union, ?instr)); merge-tasks>]
		<+ [id|merge-tasks]
	
	perform-tasks:
		(tasks, elems) -> (tasks'', elems'')
		with
			if 
				(ts1, t, ts2) := <split-fetch-keep(Task(id, [], perform-task(|elems)))> tasks 
			then
			  Task(names, [], Done(tr))	:= t;
				nr-map                		:= <map(!(<id>, tr))> names;
				elems'                		:= <map(!DoneTask(<id>, tr))> names;
				tasks'                		:= <conc; map(try(resolve-dependencies(|names, nr-map)))> (ts1, ts2);
				(tasks'', elems'')    		:= <perform-tasks> (tasks', <conc> (<distribute-results(|nr-map)> elems, elems'))
			else
				tasks'' := tasks;
				elems'' := elems
			end
				
rules
	
	perform-task(|elems): Resolve(path, [], []) 		-> Done(<nbl-lookup(|elems)> path)
	perform-task(|elems): Resolve(s, path, [], []) 	-> <perform-task(|elems)> Resolve([s|path], [], []) where <is-list> path
	perform-task(|elems): PropCalc(prop, term) 			-> Done(<calc-property(|elems)> (prop, term))
	perform-task(|elems): Match(key, term) 					-> Done(<match-term> (key, term))
	
rules

	calc-property(|elems) = nbl-lookup-property(|elems)
	
	match-term = fail
	
rules
	
	split-tasks =
		partition(?Task(_, _, _))
		
	split-results = 
		partition(task-result; unzip; (concat, id))
	
	task-result: Task(names, _, Done(result)) -> (<map(!(<id>, result))> names, result)
	
	resolve-dependencies(|solved, results):
		Task(names, deps, task) -> Task(names, deps', task')
		where
			deps' := <diff; not(?deps)> (deps, solved);
			task' := <try(distribute-results(|results))> task
		
	distribute-results(|results) = 
		alltd(insert-result(|results))
	
	insert-result(|results): Result(n) -> <lookup> (n, results)

	insert-result(|results) = 
		has-annos; 
		!(<id>, <get-annos; map(try(insert-result(|results)))>); 
		set-annos
		
