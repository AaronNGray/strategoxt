module nbl/incremental

imports
	
	index/query
	nbl/collect
	nbl/query
	nbl/uri
	nbl/entries
      
rules // Dependencies
  
  /**
   * Gets the set of partitions with read dependencies to the given entries.
   *
   * Example:
   *   <nbl-get-dependent-partitions> [Def([Entity(), "Bar"]), ...] => [("fullpath/file.ext", "partition"), ...]
   *
   * @type List(entry) -> List(partition)
   */
  nbl-get-dependent-partitions = 
    nbl-get-dependent-partitions(nbl-get-all-reads)
          
rules /** @internal Storing reads */
  
  nbl-collect-reads(s) =
    {| NBL-ReadSet:
    	rules(NBL-ReadSet := <new-iset>);
    	try(s);
    	NBL-ReadSet;
    	iset-elements
  	|}
    	
rules /** @internal Entry difference */
  
  nbl-diff-entry =
    ?Def(_)
    
  nbl-diff-entry =
    ?Prop(_, _, _)
      
  nbl-diff-eq:
    (entry1, entry2) -> <id>
    where
      <nbl-uri-eq> (<nbl-uri> entry1, <nbl-uri> entry2)

  nbl-diff:
    (defs1, defs2) -> (added, removed)
    with
      added   := <diff(nbl-diff-eq)> (defs2, defs1);
      removed := <diff(nbl-diff-eq)> (defs1, defs2)

rules /** @internal */
  
  nbl-get-dependent-partitions(construct-from-uri):
    entries -> partitions
    where
      references := <mapconcat(construct-from-uri)> entries;
      partitions := <make-set> <mapconcat(index-get-partitions-of)> references
