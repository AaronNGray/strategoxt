module nbl/collect

imports
	
	nbl/-
	
signature 

	sorts
	
		Unique Import DefScope RefScope

	constructors
	
	Unique     : Unique
	NonUnique  : Unique
	
	Single     : Namespace * Name -> Import
	All        : Namespace        -> Import
	Imported   : Namespace        -> Import
	
	
	Current    :            DefScope
	Subsequent :            DefScope
	DefScope   : Unknown -> DefScope

	Candidate  : Namespace * List(Property) * RefScope * List(Import) -> Use
	
	Current    :                                     							 RefScope
	Surrounding:                                  	 							 RefScope
	Context    : String * Namespace * List(Property) * RefScope	-> RefScope

	Def        : URI                    -> Def
	Use        : URI                    -> Use
	Prop       : URI * Property * Value -> Prop
	
strategies // generic traversal
	
	nbl-collect(|lang) = 
		where (new-iset; ?elems);
		nbl-collect(id|lang, elems, []); 
		!(<id>, <iset-elements> elems)
	
	nbl-collect(spaths|lang, elems, paths) =
		vdebug(!"analyse def ");
		nbl-def-collect(?cpaths, spaths|lang, elems, paths);
		vdebug(!"visit children ");
		preserve-annos(force-origins(id#(nbl-siblings(|lang, elems, cpaths))));
		vdebug(!"analyse use ");
		try(nbl-use-site(|lang, elems, paths));
		vdebug(!"analyse prop ");
		try(nbl-prop-site(|elems, paths));
		vdebug(!"done ")
	
	nbl-siblings(|lang, elems, paths) = 
		[]
	
	nbl-siblings(|lang, elems, paths) =
		[nbl-collect(?spaths|lang, elems, paths)|nbl-siblings(|lang, elems, spaths)]

rules // generic analysis of defs
	
	nbl-def-collect(cpaths, spaths|lang, elems, paths) =
		nbl-def-site(cpaths, spaths|lang, elems, paths) <+ 
		(
			match(cpaths|paths);
			match(spaths|paths)
		)
		
	// interface to specific analysis
	nbl-def-site(cpaths, spaths|lang, elems, paths) = fail
	
	// call back to generic analysis
	nbl-def(cpaths, spaths|lang, elems, paths, ns, unique, defscope, scoped):
		a@name{anno*} -> name{d,anno*}
		where
			defscope := [Current()];
			ppath    := <lookup <+ <nbl-base-uri> lang> (ns, paths);
			path     := <nbl-extend-uri(|ID(ns, name, unique))> ppath;
			d        := <new-def(|elems, path)> a;
		  match(spaths|paths);
			match(cpaths|<replace-scope-paths(|path)> (scoped, paths))
			
	nbl-def(cpaths, spaths|lang, elems, paths, ns, unique, defscope, scoped):
		a@name{anno*} -> name{d, anno*}
		where
			defscope := [Subsequent()];
			segment  := Subsequent(<new>);
			ppath    := <lookup <+ <nbl-base-uri> lang> (ns, paths);
			path     := <nbl-extend-uri(|segment); nbl-extend-uri(|ID(ns, name, unique))> ppath;
			d        := <new-def(|elems, path)> a;
			match(spaths|<extend-scope-paths(|lang, segment)> ([ns], paths));
		  match(cpaths|paths)
	
	// call back for generic analysis of anonymous scopes
	nbl-anonymous-scope(cpaths, spaths|lang, elems, paths, nss) =
		match(spaths|paths);
		match(cpaths|<extend-scope-paths(|lang, Anonymous(<new>))> (nss, paths))
		
rules // generic analysis of uses
	
	nbl-use-collect(|lang, elems, paths) =
		try(nbl-use-site(|lang, elems, paths))
	
	// interface to specific analysis
	nbl-use-site(|lang, elems, paths) = fail
	
	// call back to generic analysis
	nbl-use(|lang, elems, paths, candidates):
		name -> name'
		where
			name' := <foldl(nbl-use-candidate(|lang, elems, paths))> (candidates, name)
		
	nbl-use-candidate(|lang, elems, paths):
		(Candidate(ns, props, Current(), imports), name{anno*}) -> name{u, anno*}
		where
			ppath  := <lookup <+ <nbl-base-uri> lang> (ns, paths);
			path   := <nbl-extend-uri(|ID(ns, name))> ppath; 
			result := <new-task(|elems)> Resolve(path, props, imports);
		  u      := <new-use(|elems, result)>
	
	nbl-use-candidate(|elems, paths):
		(Candidate(ns, props, Context(name', ns', props', refscope'), imports), name{anno*}) -> name{u, anno*}
		where	
			_{tempresult} := <nbl-use-candidate(|elems, paths)> (Candidate(ns', props', refscope', []), name'{});
			result				:= <new-task(|elems)> Resolve((ns, name{}), tempresult, props, imports);
		  u      				:= <new-use(|elems, result)>

rules // generic analysis of properties
			
	nbl-prop-collect(|elems, paths) =
		try(nbl-prop-site(|elems, paths))
	
	// interface to specific analysis
	nbl-prop-site(|elems, paths) = fail
	
	// call back to generic analysis
	nbl-props(|elems, prop*) =
		?name{Def(path)};
		<map(extend-prop(|elems, path))> prop*
		
	extend-prop(|elems, path):
		Prop((ns, name), prop, val) -> <new-prop(|elems, path, prop, val)> 
		where
			name':= name{};
			<Hd> path;
			(
				?(ns, name') +
				?(ns, name', _)
			)
			
rules // helpers
	
	replace-scope-paths(|path)         = foldl(update-scope-paths(!path|<nbl-uri-language> path))
	extend-scope-paths(|lang, segment) = foldl(update-scope-paths(nbl-extend-uri(|segment)|lang))
	
	update-scope-paths(update|lang):
		(key, paths) -> result
		where
			result := <fetch((?key, update))> paths <+ 
			result := [(key, <update; nbl-base-uri> lang)|paths]
			
	force-origins(s) =
    ![<id>]; all(s); ?[<id>]
