module nbl/analysis

imports
	
	index/core
	index/query
	nbl/collect
	nbl/tasks
	nbl/incremental
	nbl/uri
	
signature

  constructors
  
    File   : Path * AST                                       -> File
    Result : Partition * AST * AST * List(Entry) * List(Task) -> File
    	
rules // Single file analysis
	
	nbl-analyze(|language, path, project-path):
		ast -> (ast', entries', errors, dependent)
		with // Setup index
			partition := $[[project-path]/[path]];
			index-setup(|language, [project-path], partition);
			oldEntries := <filter(nbl-diff-entry)> <index-get-all-in-partition> partition;
      <index-clear-partition> partition
		with // Collect tasks
			(ast', entries, tasks) := <nbl-collect(|Language(language))> ast;
			 
			<index-add-all(|partition)> entries
	  with // Perform tasks
	  	nbl-collect-reads(
				tasks'             := <merge-tasks> tasks;
				(errors, entries') := <perform-tasks(|partition)> (tasks', entries)
			) => reads;
					
			<index-add-all(|partition)> reads
		with // Dependent partitions
		  newEntries       := <filter(nbl-diff-entry)> entries';
      (added, removed) := <nbl-diff> (oldEntries, newEntries);
      changed          := <conc> (added, removed);
      dependent        := <nbl-get-dependent-partitions> changed

rules // Multi file analysis

  nbl-analyze-multiple(parse-file|language, project-path):
  	path* -> file'*
  	with
  	  file*  := <map(nbl-analyze-parse-file(parse-file))> path*;
  	  file'* := <nbl-analyze-multiple-files(|language, project-path)> file*

  nbl-analyze-parse-file(parse-file):
    path -> File(path, ast)
    with
      if not(ast := <file-exists; parse-file> path) then
        ast := ()
      end

  nbl-analyze-multiple-files(|language, project-path):
    files -> collect'*
    with // Setup index
      index-setup(|language, [project-path], ".")
    with // Collect entries and tasks for all files.
    	collect*  := <map(nbl-analyze-multiple-collect(|language, project-path))> files
    with // Perform tasks for all files.
      collect'* := <map(nbl-analyze-multiple-perform(|language))> collect*
    	
  nbl-analyze-multiple-collect(|language, project-path):
  	File(path, ast) -> Result(partition, ast, ast', entry*, <merge-tasks> task*) // TODO: Merge entire list of tasks instead of just for this file.
  	with
  	  partition := $[[project-path]/[path]];
  	  <index-set-current-partition> partition;
  		(ast', entry*, task*) := <nbl-collect(|Language(language))> ast;
  		<index-clear-partition> partition;
  		<index-add-all(|partition)> entry*
  
  nbl-analyze-multiple-perform(|language):
    Result(partition, ast, ast', entry*, task*) -> Result(partition, ast, ast', entry'*, error*)
    with
      <index-set-current-partition> partition;
      nbl-collect-reads(
        (error*, entry'*) := <perform-tasks(|partition)> (task*, entry*)
      ) => reads;
      <index-add-all(|partition)> reads
