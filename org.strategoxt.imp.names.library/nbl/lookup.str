module nbl/lookup

imports
  
  nbl/collect
  nbl/resolve
  nbl/query
  nbl/uri
  
rules // Index-based lookup
  
  nbl-lookup-definition:
    containsURI -> <nbl-lookup(nbl-definition-template)> containsURI
    
  nbl-lookup-property(|type):
    containsURI -> containsURI //<nbl-lookup(nbl-property-template(|type))> containsURI
  
rules // Index based visibility
  
  nbl-visible-definitions:
    containsURI -> <nbl-visible(nbl-definition-template)> containsURI
    
  nbl-visible-properties(|type):
    containsURI -> <nbl-visible(nbl-property-template(|type))> containsURI
  
rules /** @internal Index-based lookup */
  
  nbl-lookup(create-template|):
    containsURI -> entry
    where
      <with(uri             := <nbl-uri> containsURI   | "Could not extract URI from given term.")> [containsURI];
      <with(parentURI       := <nbl-uri-parent> uri    | "Could not construct a parent URI.")> [uri];
      <with(targetNamespace := <nbl-uri-namespace> uri | "Could not extract target namespace.")> [uri];
      <with(targetName      := <nbl-uri-name> uri      | "Could not extract target name.")> [uri];
      entry                 := <nbl-lookup-uri(create-template|targetNamespace, targetName)> parentURI
      
  nbl-lookup-uri(create-template|namespace, name):
    uri -> entry
    where
      entry*       := <nbl-lookup-uri-scoped(create-template|namespace, name)> uri;
      if not(entry := <Hd> entry*) then
        parentURI  := <nbl-uri-parent> uri;
        entry      := <nbl-lookup-uri(create-template|namespace, name)> parentURI
      end
    
  nbl-lookup-uri-scoped(create-template|namespace, name):
    uri -> [entry*, nonUniqueEntry*, importEntry*]
    with
      // Standard lookups
      entry*          := <nbl-standard-lookup(create-template|namespace, name)> uri;
      nonUniqueEntry* := <nbl-standard-lookup-nonunique(create-template|namespace, name)> uri;
      
      // Imports & aliases
      importEntry*    := <nbl-lookup-imported-entries(create-template|<nbl-uri-language> uri, namespace, name)> uri
      
      // TODO: External (defines x in y) definitions
      
      // TODO: Implicit (this, super) definitions
      
  nbl-standard-lookup-nonunique(create-template|namespace, name) =
    nbl-extend-uri(|namespace, name);
    create-template;
    nbl-get-all
    
  nbl-standard-lookup(create-template|namespace, name) =
    nbl-extend-uri(|namespace, name, Unique()); // TODO: Properly lookup unique entries.
    create-template;
    nbl-get-all

rules /** @internal Index based visibility */
      
  nbl-visible(create-template|):
    containsURI -> entries
    with
      <with(uri             := <nbl-uri> containsURI   | "Could not extract URI from given term.")> [containsURI];
      <with(parentURI       := <nbl-uri-parent> uri    | "Could not construct a parent URI.")> [uri];
      <with(targetNamespace := <nbl-uri-namespace> uri | "Could not extract target namespace.")> [uri];
      <with(targetPrefix    := <nbl-uri-name> uri      | "Could not extract target prefix.")> [uri];
      entries               := <nbl-visible-uri(create-template|targetNamespace, targetPrefix)> parentURI
      
  nbl-visible-uri(create-template|namespace, prefix):
    uri -> allEntries
    with
      entries      := <nbl-visible-uri-scoped(create-template|namespace, prefix)> uri;
      if parentURI := <nbl-uri-parent> uri then
        entries2   := <nbl-visible-uri(create-template|namespace, prefix)> parentURI;
        allEntries := <conc> (entries, entries2)
      else
        allEntries := entries
      end
      
  nbl-visible-uri-scoped(create-template|namespace, prefix):
    uri -> [entry*, importEntry*]
    with
      // Standard lookup
      entry*       := <nbl-standard-visible(create-template|namespace, prefix)> uri;
      
      // Imports & aliases
      importEntry* := <nbl-visible-imported-entries(create-template|<nbl-uri-language> uri, namespace, prefix)> uri
      
      // TODO: External (defines x in y) definitions
      
      // TODO: Implicit (this, super) definitions

  nbl-standard-visible(create-template|namespace, prefix) =
    create-template;
    nbl-get-children(|namespace, prefix)

rules /** @internal Imports */

  nbl-lookup-imported-entries(create-template|language, namespace, name):
    uri -> <conc> (namedEntry*, unnamedEntry*)
    with
      <debug(!"uri              ")> uri;
      <debug(!"language         ")> language;
      <debug(!"namespace        ")> namespace;
      <debug(!"name             ")> name;
      namedImportURI*   := <debug(!"namedImportURI      ")> <nbl-get-import-uris(|language, namespace, name)> uri;
      namedEntry*       := <debug(!"namedEntry          ")> <mapconcat(create-template; nbl-get-all)> namedImportURI*;
      unnamedImportURI* := <debug(!"unnamedImportURI    ")> <nbl-get-import-uris(|language, namespace)> uri;
      unnamedEntry*     := <debug(!"unnamedEntry        ")> <mapconcat(nbl-standard-lookup(create-template|namespace, name))> unnamedImportURI*
      
  nbl-visible-imported-entries(create-template|language, namespace, prefix):
    uri -> unnamedEntry*
    with
      unnamedImportURI* := <nbl-get-import-uris(|language, namespace)> uri;
      unnamedEntry*     := <mapconcat(nbl-standard-visible(create-template|namespace, prefix))> unnamedImportURI* 

  nbl-get-import-uris(|language, namespace):
    uri -> <conc> (importURI*, transitiveImportURI*)
    with
      importResult*        := <debug(!"importResult        ")> <nbl-get-all-properties(|Import(language, namespace))> uri;
      importURI*           := <debug(!"importURI           ")> <filter(nbl-resolve-result)> importResult*;
      importedResult*      := <debug(!"importedResult      ")> <nbl-get-all-properties(|Import(language, Imported(namespace)))> uri;
      importedURI*         := <debug(!"importedURI         ")> <filter(nbl-resolve-result)> importedResult*;
      transitiveImportURI* := <debug(!"transitiveImportURI ")> <mapconcat(nbl-get-import-uris(|language, namespace))> importedURI*
      // TODO: prevent loops
    
  nbl-get-import-uris(|language, namespace, name):
    uri -> importURI*
    with
      importResult* := <debug(!"importResult        ")> <nbl-get-all-properties(|Import(language, namespace, name))> uri;
      importURI*    := <debug(!"importURI           ")> <filter(nbl-resolve-result)> importResult*

rules /** @internal Helpers */
  
  nbl-definition-template:
    uri -> Def(uri)
    
  nbl-property-template(|type):
    uri -> Prop(uri, type, ())
 