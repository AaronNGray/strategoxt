module nbl/lookup

imports
  
  nbl/collect
  nbl/resolve
  nbl/query
  nbl/uri
  nbl/entries
  
rules // Index-based lookup
  
  nbl-lookup-definitions:
    containsURI -> <nbl-lookup(nbl-definition-template, id)> containsURI
    
  nbl-lookup-definitions(|namespace, name):
    uri -> <nbl-lookup-uri(nbl-definition-template, id|namespace, name)> uri

  nbl-lookup-definitions(constraint|namespace, name):
    uri -> <nbl-lookup-uri(nbl-definition-template, constraint|namespace, name)> uri
  
rules // Index based visibility
  
  nbl-visible-definitions:
    containsURI -> <nbl-visible(nbl-definition-template, id)> containsURI
  
rules /** @internal Index-based lookup */
  
  nbl-lookup(create-template, constraint|):
    containsURI -> entry*
    where
      <with(uri             := <nbl-uri> containsURI                  | "Could not extract URI from given term.")> [containsURI];
      <with(targetNamespace := <nbl-uri-namespace> uri                | "Could not extract target namespace.")> [uri];
      <with(targetName      := <nbl-uri-name> uri                     | "Could not extract target name.")> [uri];
      <with(parentURI       := <nbl-uri-parent(|targetNamespace)> uri | "Could not construct a parent URI.")> [uri];
      entry*                := <nbl-lookup-uri(create-template, constraint|targetNamespace, targetName)> parentURI
      
  nbl-lookup-uri(create-template, constraint|namespace, name):
    uri -> entry'*
    where
      entry*         := <nbl-lookup-uri-scoped(create-template, constraint|namespace, name)> uri;
      if []          := entry* then
        if parentURI := <nbl-uri-parent(|namespace)> uri then
          entry'*    := <nbl-lookup-uri(create-template, constraint|namespace, name)> parentURI
        else
          entry'*    := []
        end
      else
        entry'*      := entry*
      end
    
  nbl-lookup-uri-scoped(create-template, constraint|namespace, name):
    uri -> [entry*, importEntry*]
    with
      // Standard lookup
      entry*       := <nbl-standard-lookup(create-template|namespace, name); filter(constraint)> uri;
      
      // Imports & aliases
      importEntry* := <nbl-lookup-imported-entries(create-template|<nbl-uri-language> uri, namespace, name); filter(constraint)> uri
      
      // TODO: External (defines x in y) definitions
      
      // TODO: Implicit (this, super) definitions
      
  nbl-standard-lookup-nonunique(create-template|namespace, name) =
    nbl-extend-uri(|namespace, name, NonUnique());
    create-template;
    nbl-get-all
    
  nbl-standard-lookup-unique(create-template|namespace, name):
    uri -> entry*
    where
      if alias* := <nbl-get-all-values> Alias(<nbl-extend-uri(|namespace, name, NonUnique())> uri, ()) then
        entry* := <mapconcat(create-template; nbl-get-all)> alias*
      else
        entry* := []
      end

  nbl-standard-lookup(create-template|namespace, name):
    uri -> [entry*, nonUniqueEntry*]
    where
      entry*          := <nbl-standard-lookup-unique(create-template|namespace, name)> uri;
      nonUniqueEntry* := <nbl-standard-lookup-nonunique(create-template|namespace, name)> uri
      
rules /** @internal Index based visibility */
      
  nbl-visible(create-template, constraint|):
    containsURI -> entries
    with
      <with(uri             := <nbl-uri> containsURI                  | "Could not extract URI from given term.")> [containsURI];
      <with(targetNamespace := <nbl-uri-namespace> uri                | "Could not extract target namespace.")> [uri];
      <with(targetPrefix    := <nbl-uri-name> uri                     | "Could not extract target prefix.")> [uri];
      <with(parentURI       := <nbl-uri-parent(|targetNamespace)> uri | "Could not construct a parent URI.")> [uri];
      entries               := <nbl-visible-uri(create-template, constraint|targetNamespace, targetPrefix)> parentURI
      
  nbl-visible-uri(create-template, constraint|namespace, prefix):
    uri -> allEntries
    with
      entries      := <nbl-visible-uri-scoped(create-template, constraint|namespace, prefix)> uri;
      if parentURI := <nbl-uri-parent(|namespace)> uri then
        entries2   := <nbl-visible-uri(create-template, constraint|namespace, prefix)> parentURI;
        allEntries := <conc> (entries, entries2)
      else
        allEntries := entries
      end
      
  nbl-visible-uri-scoped(create-template, constraint|namespace, prefix):
    uri -> [entry*, importEntry*]
    with
      // Standard lookup
      entry*       := <nbl-standard-visible(create-template|namespace, prefix); filter(constraint)> uri;
      
      // Imports & aliases
      importEntry* := <nbl-visible-imported-entries(create-template|<nbl-uri-language> uri, namespace, prefix); filter(constraint)> uri
      
      // TODO: External (defines x in y) definitions
      
      // TODO: Implicit (this, super) definitions

  nbl-standard-visible(create-template|namespace, prefix) =
    create-template;
    nbl-get-children(|namespace, prefix)

rules /** @internal Imports */

  nbl-lookup-imported-entries(create-template|language, namespace, name):
    uri -> [namedEntry*, unnamedEntry*]
    with
      namedImportURI*   := <nbl-get-import-uris-named(|language, namespace, name)> uri;
      namedEntry*       := <mapconcat(create-template; nbl-get-all)> namedImportURI*;
      unnamedImportURI* := <nbl-get-import-uris-unnamed(|language, namespace)> uri;
      unnamedEntry*     := <mapconcat(nbl-standard-lookup(create-template|namespace, name))> unnamedImportURI*
      
  nbl-visible-imported-entries(create-template|language, namespace, prefix):
    uri -> unnamedEntry*
    with
      unnamedImportURI* := <nbl-get-import-uris-unnamed(|language, namespace)> uri;
      unnamedEntry*     := <mapconcat(nbl-standard-visible(create-template|namespace, prefix))> unnamedImportURI* 

  nbl-get-import-uris-unnamed(|language, namespace) = nbl-get-import-uris-unnamed(|[], language, namespace)

  nbl-get-import-uris-unnamed(|seen, language, namespace):
    uri -> [importURI*, transitiveImportURI*]
    with
      importResult*        := <nbl-get-all-properties(|Import(language, namespace))> uri;
      importURI*           := <mapconcat(nbl-resolve-result)> importResult*;
      importedResult*      := <nbl-get-all-properties(|Import(language, Imported(namespace)))> uri;
      importedURI*         := <mapconcat(nbl-resolve-result)> importedResult*;
      transitiveImportURI* := <concat> <filter(nbl-get-import-uris-unnamed-transitive(|seen, language, namespace))> importedURI*

  nbl-get-import-uris-unnamed-transitive(|seen, language, namespace):
    uri -> transitiveImportURI*
    where
      not(<fetch(?uri)> seen);
      transitiveImportURI* := <nbl-get-import-uris-unnamed(|[uri|seen], language, namespace)> uri
    
  nbl-get-import-uris-named(|language, namespace, name):
    uri -> importURI*
    with
      importResult* := <nbl-get-all-properties(|Import(language, namespace, name))> uri;
      importURI*    := <filter(nbl-resolve-result)> importResult*

rules /** @internal Helpers */
  
  nbl-definition-template:
    uri -> Def(uri)
 