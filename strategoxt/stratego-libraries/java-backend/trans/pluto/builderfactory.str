module pluto/builderfactory

imports libstrategolib
imports cleardep/fileutils

strategies
	
	init-builder-factory-store = 
		prim("InitBuilderFactoryStore", Factories|)
	
	builder-factory(description-strategy, persistent-path-strategy, build-strategy | factory-name) =
		
		let 
			// The dynamic rule Builder is used by require/provide etc to refer to the running builder
			scope-builder-and-build = {builder, input, output: {|Builder:
				  ?(builder, input)
				; rules(Builder : _ -> builder)
				; <build-strategy> input => output
				; !output
			|}}
			
			flat-persistent-path = 
				
	    		log(|Info(), ["Persistent path for ", <id>]);
	    		persistent-path-strategy;
	    		log(|Info(), ["Persistent path " , <id>]);
	    		path-get-full-path;
	    		log(|Info(), ["Flat ", <id>])
	    in
			
		<Factories> factory-name <+ (
			  prim("PlutoInterface_MakeBuilderFactory", description-strategy, flat-persistent-path, scope-builder-and-build | factory-name) => generated-factory
			; rules(Factories: factory-name -> generated-factory)
			; !generated-factory
			)
	    end
	    