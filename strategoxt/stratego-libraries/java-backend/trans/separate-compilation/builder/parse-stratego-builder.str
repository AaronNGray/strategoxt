module parse-stratego-builder

imports libstrategolib
imports libstrc
imports cleardep/fileutils
imports pluto/builder
imports pluto/builderfactory
imports separate-compilation/utils
imports separate-compilation/fileutils

imports separate-compilation/builder/deps-path

signature
	constructors
		ParseStrategoInput : Path * IncludePath -> ParseStrategoInput
		ParseStrategoOutput : ModuleName * AST -> ParseStrategoOutput
		
strategies
	
	parse-stratego-builder-factory =  let
	
		description :
			ParseStrategoInput(RelativePath(_, src-file), _) -> <conc-strings> ("Parse ", src-file)
			
		persistent-path :
			ParseStrategoInput(file, _) -> <get-dep-path(|"parse")> file
			
		build = let
			extract-module-name : (_, Module(name,_)) -> name
		 in
		  	?ParseStrategoInput(file, pack-include-path);
			<require> file;
			<path-get-full-path> file => file-path;
			!(IncludePathName(file-path),pack-include-path);
			(separate-compilation-parse-file 
			<+ (log(|Error(), ["File not found: ", <id>]); fail)) => ast;
			<extract-module-name> ast => module-name;
			!ParseStrategoOutput(module-name, ast)
     	end
	
	in 
		builder-factory(
			description,
			persistent-path,
			build | "ParseStrategoBuilderFactory"
		)
	end
	
	
			
	
	
	