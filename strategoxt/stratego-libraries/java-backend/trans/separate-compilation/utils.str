module separate-compilation/utils

strategies

	fold-on-imports(import-predicate, import-transform, collect-transform) =
		{| CollectedImports:
		let
			transform-ast = bottomup(
				(?Imports(<id>);mapconcat(pack-expand-import);map(\s -> Import(s)\)) <
				map(
					if import-predicate
						then where(rules(CollectedImports := [<collect-transform> | <CollectedImports>]));import-transform
						else id;log(|Info(), ["Import predicate failed for ", <id>])
					end);
				!Imports(<id>) + id) 
			collect-imports = collect-all(where(import-predicate))
		in	  id => Module(name, body)
			; !Module(name, body) => ast
			; rules(CollectedImports := [])
			; !ast
			; transform-ast
			; !(<id>, <CollectedImports;reverse>)
		end
		|}
		
	import-is-file = 
		  \Import(s) -> (IncludeFromPath(s),<pack-include-path>)\
		; separate-compilation-check-file
		
	save-and-set-options(s|working-package,working-output,filename, modulename) = 
		where(input := <get-config> "-i"; output := <get-config> "-o"; package := <get-config> "-p"; islib := <get-config<+!0> "--library"; baseout := <get-config> "baseout");
		where(	<set-config> ("-i",filename));
		where(	<set-config> ("-p", <concat-strings>[working-package, ".", <to-library-name;jify;string-char-replace(|'/','.');string-char-replace(|'-','_')> modulename]));
		where(	<set-config>("-o", <to-java-compilation-abs-path(|working-output); (mkdirs <+ err(|"Creating dir failed"))> modulename));
		where(	<set-config> ("baseout" ,<to-java-compilation-abs-file(|working-output)> modulename));
		where(  <set-config> ("--library" ,""));
		s;
		where(<set-config> ("-i", input); <set-config> ("-o", output); <set-config> ("-p", package); <(?0;!"--library";rm-config)<+id> islib; <set-config> ("baseout", baseout))
	 