module separate-compilation/unit-cache

imports cleardep/cleardep
imports cleardep/fileutils
imports separate-compilation/fileutils

strategies

	
	// Cache compilation unit queries by name because reading from clear dep interface involves a consistency check which is slow
	
	create-compilation-unit-for-module(|module-name) = 
		  create-compilation-unit(|<separate-compilation-get-relative-file;where(path-exists)> module-name) => unit
		; where(rules(CompilationUnitCache: module-name -> unit))
	
	read-compilation-unit-for-module(|module-name) = 
		(   !module-name
		  ; CompilationUnitCache) 
		<+
		(   read-compilation-unit(|<separate-compilation-get-relative-file;where(path-exists)> module-name) => unit
		  ; where(rules(CompilationUnitCache: module-name -> unit)) )
	
	read-or-create-compilation-unit-for-module(|module-name) =
		  read-compilation-unit-for-module(|module-name)
		<+ create-compilation-unit-for-module(|module-name)
			
	create-if-not-in-cache-or-read-compilation-unit-for-module(|module-name) =
		(   !module-name
		  ; not(CompilationUnitCache)
		  ; create-compilation-unit-for-module(|module-name))
		<+
		  read-compilation-unit-for-module(|module-name)
