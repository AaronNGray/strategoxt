module separate-compilation

imports cleardep/cleardep
imports cleardep/fileutils
imports separate-compilation/utils
imports separate-compilation/process-deps
imports separate-compilation/separate-compilation-options

signature
	constructors
		IncludeAST: Term * String -> Include

strategies

    /*strc-front-end =
        log-timed(
          strc-get-infile
  		;strc-pack-stratego-trm
  		; tfc1(|"Stratego-Sugar-Cong", "pack")
    		; strc-output-ast
        	; frontend
        | "Front-end succeeded"
        , 1
        )*/
	  
    strc-front-end1(back-end) =
	{| CompilingModules, ImportMap :
      log-timed(
        strc-get-infile ;
	    separate-compilation-front-end(back-end|<get-config <+ !"" > "-p",<get-config;string-ensure-trailing-slash <+ !"./"> "-o")
      | "Front-end succeeded", 1) |}
	
  	strc-front-end2 = 
      //  strc-get-infile
      //  ; strc-pack-stratego-trm
  		tfc1(|"Stratego-Sugar-Cong", "pack")
        ; strc-output-ast
  	  	; frontend
		; strc-optimize



	separate-compilation-front-end(back-end|working-package,working-output)=
		 // create-compilation-units(|working-package, working-output)
		  where(fileName := <\FILE(s) -> s\>)
		  
	// Make a relative file of the given with respect to the include path, then read or create the compilation unit for this file
		; !AbsolutePath(fileName)
		; path-relativize (|
			<pack-include-path;fetch-elem(\IncludeDir(d) -> AbsolutePath(d)\)> )
		; if complete-rebuild 
			then create-compilation-unit(|<id>)
			else read-or-create-compilation-unit(|<id>)
		  end
	// Create the build schedule for the compilation unit and flatten it
		; ![<id>]
		; createBuildSchedule(|<get-schedule-mode>)
		; flattenBuildSchedule
		; log(|Info(), ["Calculated build schedule ", <id>])
	// Compile according to the build schedule, outer map maps over the build tasks in the build schedule
		; map(	not(needsTaskToBeBuild) <+ (task-get-units
				; log(|Info(), ["Compile Build Task " , <id>])
	// Compiles a strongly connected component
				; compile(back-end|working-package,working-output)
	// Update the interface versions of the dependencies 
				; where( map ({unit: 
					  id => unit
					; get-all-module-dependencies
					; map(update-module-dependency-interface(|unit))
					}))
	// Finally write all units
				; map (write-compilation-unit)
			)
		)
		

	
	compile (back-end|working-package,working-output)=
	  if !AbsolutePath(working-output);not(path-is-directory)
	    then log(|Error(), ["Given path is not a directory or does not end with a slash: ", working-output]); fail
	  end
	; id => circular-units
	; if not(complete-rebuild);!circular-units; map(is-persisted; is-consistent-shallow; is-consistent-to-interfaces)
		then 
			  log(|Info(),["Check to compile, is consistent to interfaces, do not compile ", circular-units])
		else
			  map ( {parseResult, work-unit, fileName, moduleName:
				  get-stratego-source-file
				; path-get-full-path => fileName
				; !(IncludePathName(fileName),<pack-include-path>)
				; pack-process-options
				 
				; log(|Info(), ["Parse file:         ", <id>])
				; (separate-compilation-parse-file <+ log(|Error(), ["File not found: ", <id>])) => parseResult
				; where(moduleName:=<\(_,Module(name,_))->name\><id>)
				; log(|Info(), ["Check to compile, need to compile ", moduleName])
				; log(|Info(), ["Compile to package: ", working-package])
				; log(|Info(), ["Place files in:     ", working-output])
				; create-compilation-unit-for-module(|moduleName) => work-unit
				; !(moduleName, work-unit, parseResult, fileName)
				 })
			; compile-asts(back-end|working-package,working-output, circular-units)
		end
			
	compile-asts(back-end|working-package,working-output, circular-units)= {x, xs, other-circular-units :
			  log(|Info(), ["Transforming import"])
			//  where(map(\(moduleName, _, _, _) -> moduleName\) => moduleNames)
			; map( {moduleName, work-unit, ast, fileName, transformed-ast, new-libs :
				  ?(moduleName, work-unit, ast, fileName)
				; !ast
				; (separate-compilation-rewrite-imports(back-end|moduleName, work-unit, working-package,working-output, circular-units)) => (transformed-ast, new-libs)
				; !(moduleName, work-unit, transformed-ast, new-libs, fileName)
				})
			
			// We know, that the list of units to compile at once is not empty, so we can guarantee a first element
			; ?[x|xs]
			; !(xs, x)
			; log(|Info(), ["Combine modules"])
			; foldl( {new-content, new-libs, moduleName, work-unit, merged-name, merged-content, merged-libs, fileName :
				  ?( (_, _, Module(_, new-content), new-libs, _), (moduleName, work-unit, Module(merged-name, merged-content), merged-libs, fileName) )
				; !(moduleName, work-unit, Module(merged-name, <conc> (new-content, merged-content)), <conc> (new-libs, merged-libs), fileName)
			})
			
			; where(!xs;map(Snd) => other-circular-units)

			; log(|Info(), ["Prepare for front end"])
			; {moduleName, merged-ast, new-libs, fileName, work-unit, rtree-file, signature-term:  
			  ?(moduleName, work-unit, merged-ast, new-libs, fileName)
			; save-and-set-options(
				  where(incl := <get-config <+ ![]> "-I")
				; where(libs := <get-config <+ ![]> "-la")
				; where(<set-config>("-I", <conc;make-set> (<map(Third;Snd;!IncludeDir(<id>))> new-libs, incl)))
				; where(<set-config>("-la", <conc;make-set> (libs, <map(Fst)> new-libs)))
				; log(|Notice(), ["Import transformation for ", moduleName])
				; log(|Notice(), ["Use import dirs: " , <get-config> "-I"])
				; log(|Notice(), ["Use libraries:   " , <get-config> "-la"])
			
				; !IncludeAST(merged-ast,fileName)
				; pack-stratego(|<pack-include-path>) => (files, spec)
				; !spec
			
				; log(|Notice(), ["Calling Frontend for ", moduleName])
				; strc-front-end2
			
				;  log(|Info(), ["Calculating transitive externals for ", work-unit])
				; where(
					  calculate-transitive-externals(|work-unit, circular-units) => (rtree-file, signature-term)
					; add-generated-file(|work-unit, AbsolutePath(rtree-file)) 
					; set-interface-hash(|work-unit, signature-term) 
				
				)
				
				; log(|Notice(), ["Calling Backend for ", moduleName])
				; back-end(|circular-units)
				; where(<set-config> ("-la", libs); <set-config> ("-I", incl))
			| working-package,working-output,fileName,moduleName)}
			;{compiled-module-name:
			  !x
			; ?(compiled-module-name, _, _ ,_,_)
			; where(<debug> ["Compiled module name ", compiled-module-name])
			; !xs
			; map( {module-name, module-unit,  file-name:
				  ?(module-name, module-unit, _, _, file-name)
				  ; where(<debug> ["Generate dummy for ", module-name])
				; ![(module-name, module-unit, (file-name, Module(module-name, [Imports([Import(compiled-module-name)])])), file-name)] ; debug
				})
			; map(compile-asts(back-end|working-package,working-output, other-circular-units))
			}
			; !circular-units
			}
	 

	string-char-replace(|c1, c2) = explode-string; map((?c1;!c2)<+id); implode-string
	
	
	to-library-name = separate-compilation-modify-filename(if or(?"char",?"term") then !["_", <id>];concat-strings end ; jify)
	to-java-compilation-folder:   modulename -> <to-library-name;jify> modulename
	to-java-compilation-filename: modulename -> <to-library-name;path-get-filename;jify> modulename
	to-java-compilation-rel-file: modulename -> <concat-strings> [<to-java-compilation-folder> modulename ,"/",<to-java-compilation-filename> modulename]
	to-java-compilation-abs-path (| working-output):modulename -> <concat-strings> [working-output, <to-java-compilation-folder> modulename, "/"]
	to-java-compilation-abs-file (| working-output) =
		  id => modulename 
		; to-java-compilation-filename
		; string-path-append(|<to-java-compilation-abs-path(|working-output)> modulename)

	
	mkdirs = 
	file-exists <+ mkdir <+ where(explode-string;reverse;drop(|1);drop-until(?'/');drop(|1);reverse;implode-string;mkdirs);mkdir
	
	// Cache compilation unit queries by name because reading from clear dep interface involves a consistency check which is slow
	
	create-compilation-unit-for-module(|module-name) = 
		  create-compilation-unit(|<separate-compilation-get-relative-file;where(path-exists)> module-name) => unit
		; where(rules(CompilationUnitCache: module-name -> unit))
	
	read-compilation-unit-for-module(|module-name) = 
		(   !module-name
		  ; CompilationUnitCache) 
		<+
		(   read-compilation-unit(|<separate-compilation-get-relative-file;where(path-exists)> module-name) => unit
		  ; where(rules(CompilationUnitCache: module-name -> unit)) )
	
	separate-compilation-rewrite-imports(back-end|module-name, module-unit, working-package,working-output, circular-units) =
		?(fileName, <id>);
		let
			to-library-import = ?Import(<id>);to-library-name;\name -> [name,"/",<path-get-filename> name]\;concat-strings;!Import(<id>)
			
			import-to-flag = \Import(name) -> 
				(<concat-strings>[working-package, ".", <to-java-compilation-folder;string-char-replace(|'/','.')> name],
				 <concat-strings> [working-output, <to-library-name> name], 
				 (name,Import(<to-java-compilation-rel-file> name)),
				 <separate-compilation-get-file> name )\
			
		
					
			get-unit-add-dependency(|imported-module-name) = 
			{| import-unit:
				  (read-compilation-unit-for-module(|imported-module-name)
				 	<+ create-compilation-unit-for-module(|imported-module-name)) // => import-unit
				; if equal(| module-unit)
					then	  log(|Info(), ["Module imports itself ", module-name])
					else
						  add-module-dependency(|module-unit)
					end
				
			|}
	
		in
		  where(!circular-units;map(get-stratego-source-file) => circular-source-files)
		; log(|Info(), ["Circular source files ", circular-source-files])
		; fold-on-imports(
			import-is-file,
			{ term:
				  id => term
				; get-unit-add-dependency(|<\Import(name) -> name\> <id>)
				; if !(<id;get-stratego-source-file>, circular-source-files); elem 
						then fail // Circular, do not transform
						else !term; to-library-import
				  end}
			, 
			import-to-flag) end
		   
	separate-compilation-get-file : name->
		FILE(<pack-include-path;fetch-elem(\IncludeDir(dir) -><concat-strings; file-exists> [dir,"/", name, ".str"]\)>)
	separate-compilation-get-relative-file : name->
		<pack-include-path;fetch-elem(
			\IncludeDir(dir) -> 
				<where(<concat-strings; file-exists> [dir,"/", name, ".str"])>
					 RelativePath(AbsolutePath(dir), <concat-strings> [name, ".str"])\)>
			
	separate-compilation-modify-filename(s) =
		let
			split-string = reverse;split-at(?'/');tmap(reverse;implode-string)
			in explode-string;fetch(?'/') < split-string;?(name, path);![path, "/", <s> name];concat-strings+ s end
	
	separate-compilation-replace-file = 
		let
			cut-until-slash = explode-string;reverse;drop-until(?'/');reverse;implode-string
			in \(filePath, newName) -> <conc-strings> (<cut-until-slash> filePath, newName)\ end
			

		    pack-include-from-dir-exists(|mod) :
		      IncludeDir(dir) ->  <concat-strings; file-exists> [dir,"/", mod, ".str"]
			  
			  pack-include-from-xtc-exists(|mod) :
			    IncludeXTC() -> <conc-strings; xtc-find-loc> (mod, ".str")

separate-compilation-check-file =
   ?(IncludeFromPath(<id>), includes)
  ; if has-extension(|"str") then
      remove-extension
    end
  ;  ?mod
  ;  <fetch-elem(
         pack-include-from-dir-exists(|mod)
         <+ pack-include-from-xtc-exists(|mod)
       )
     > includes /// separate-compilation-parse-file
	 
	 
  separate-compilation-check-file = 
     let pack-qualify-pathname =
             where(explode-string => cs)
           ; if <pack-is-qualified> cs then
               id
             else
               <implode-string> ['.', '/' | cs]
             end
 
          pack-is-qualified =
            ?['/' | _] + ?['.' | _ ] + ?['\' | _] + [is-alpha, ':', '\' | id]

      in ?(IncludePathName(<pack-qualify-pathname>), _)
       ; if file-exists then
            pack-include-pathname
            ; (id,report-ambs)
              // (id, xtc-transform(!"report-ambs")) // EV
            ; pack-fc(|"Stratego-Sugar-Cong")
          else
		  fail
          end
     end
	
    separate-compilation-parse-file =
       ?(IncludeFromPath(<id>), includes)
      ; if has-extension(|"str") then
          remove-extension
        end
      ;  ?mod
      ;  <fetch-elem(
             pack-include-from-dir(|mod)
             <+ pack-include-from-xtc(|mod)
           )
         > includes
      ; (id, report-ambs)
      ; pack-fc(|"Stratego-Sugar-Cong")
	  
	  separate-compilation-parse-file = 
      let pack-qualify-pathname =
              where(explode-string => cs)
            ; if <pack-is-qualified> cs then
                id
              else
                <implode-string> ['.', '/' | cs]
              end
  
           pack-is-qualified =
             ?['/' | _] + ?['.' | _ ] + ?['\' | _] + [is-alpha, ':', '\' | id]

       in ?(IncludePathName(<pack-qualify-pathname>), _)
        ; if file-exists then
            pack-include-pathname
            ; (id,report-ambs)
              // (id, xtc-transform(!"report-ambs")) // EV
            ; pack-fc(|"Stratego-Sugar-Cong")
          else
		  fail
          end
      end