module stratego/aterm/PrettyPrint
imports
  libstratego-lib
  libstratego-gpp
  stratego/aterm/PrettyPrintRules

strategies
  
  io-pp-aterm =
    io-stream-wrap(
      max-term-size-option + max-depth-option
    , system-usage
    , system-about
    , pp-aterm 
    )

  pp-aterm =
    ?(<read-from-stream>, fout)
    ; if get-max-depth-config => depth then
        at-depth(!depth, !"...")
      end
    ; pp-aterm-stream(|<get-max-term-size-config>, fout)

strategies

  max-depth-option =
     ArgOption("--max-depth"
    , where(string-to-int; set-max-depth-config) ,!
"--max-depth int
                    Set the maximum depth of ATerm"
    )

  max-term-size-option =
    ArgOption("--max-term-size"
    , where(string-to-int; set-max-term-size-config) ,!
"--max-term-size int
                    Set the term size from which the verbose 
                    pretty printing must be used [8]"
    )

/*
 * get, set and pass max-term-size
 */
strategies

  get-max-term-size-config =
    <get-config> "--max-term-size" <+ !8

  set-max-term-size-config =
    <set-config> ("--max-term-size", <id>)

/*
 * get, set and pass max-depth
 */
strategies

  get-max-depth-config =
    <get-config> "--max-depth"

  set-max-depth-config =
    <set-config> ("--max-depth", <id>)

strategies
  
  pp-aterm-stream(|stream) =
    pp-aterm-stream(|8, stream)

  pp-aterm-stream(|maxtermsize, stream) =
    aterm-escape-strings
    ; pp-aterm-box(|maxtermsize)
    ; box2text-stream(|80, stream)
    ; <fputs> ("\n", stream)

strategies 

  /**
   *  Escaping special characters in strings
   *  model after 
   *  'static int symbolTextSize(Symbol sym)'
   *  in aterm.c (aterm-2.3.1)
   * 
   */
  aterm-escape-strings =
    topdown(try(aterm-escape-string))

  aterm-escape-string :
    s1{t*} -> s2{t*}
    where
      !s1
      ; string-as-chars(
          escape-chars(
            Escape-backslash
          + Escape-double-quote
          + Escape-linefeed 
          + \ ['\t' | cs ] -> ['\', 't'  | cs ] \
          + Escape-carriage-return
          )
        )
      ; ?s2
  
  /**
  * Convenience strategy for pretty printing a term
  *
  * @param Int width of the column to use for pretty printing
  * @type Term -> String
  */
  pp-aterm(|width) = aterm-escape-string; pp-aterm-box; box2text-string(|width)
  