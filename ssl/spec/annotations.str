/**
 * Primitives for getting and setting term annotations.
 *
 * The preferred way to access annotations, however,
 * is through the term syntax t1{t2}, which allows matching and
 * building terms with annotations.
 */
module annotations
signature
  constructors
    Anno : a * b -> a

strategies

  /**
   * Alias of get-annotations.
   *
   * @inc test-get-1
   * @inc test-get-2
   * @inc test-get-3
   * @inc test-get-4
   */
  get-annos = 
    get-annotations

  /**
   * Returns the list of annotations of the current term.
   */
  get-annotations = 
    ?t; prim("SSLgetAnnotations", t)

  /**
   * Alias of set-annotations.
   */
  set-annos = 
    set-annotations

  /**
   * Sets the annotations of the given term t to the annotations a.
   */
  set-annotations = 
    ?(t, a); prim("SSLsetAnnotations", t, a)
 
  set-anno = 
    ?(t, a); <set-annos> (t, [a])

  rm-annotations =
    ?t; prim("ATremoveAnnotations", t)

  has-annos =
    ?_{_}

  has-annotation = 
    where(get-annotations; not([]))

  if-annotation(s1, s2) =
    has-annotation < s1 + s2

  strip-annos = 
    bottomup(rm-annotations)

  catch-annos = 
    rec x(has-annotation < !Anno(<rm-annotations; all(x)>, <get-annotations>) + all(x))

  /**
   * Alias of preserve-annos
   */
  preserve-annotation(s) =
    preserve-annos(s)

  preserve-annos(s):
    t{a*} -> r{a*}
      where <s> t => r
