/**
 * The file module provides abstractions over the basic file system
 * operations in the module posix-file.
 */
module file
imports posix-file io list-basic list-misc string

/**
 * Abstractions for streams
 */
strategies

  /**
   * opens a Stream associated to a FileLoc
   *
   * :: FileLoc * String -> Stream
   */
  open-stream = ?(_, _);
    (  Fst; stdio-stream
    <+ (?Path(<id>), id); fopen
    <+ (is-string,   id); fopen
    )

  /**
   * :: FileLoc -> Stream
   */
  stdio-stream = ?stdin() ; stdin-stream
  stdio-stream = ?stdout(); stdout-stream
  stdio-stream = ?stderr(); stderr-stream

/**
 * Old file administration
 */
strategies

  close-file = 
    ?name; prim("SSL_close_file", name)

  open-file =
    ?(name, mode); prim("SSL_open_file", name, mode)

  open-file =
    ?name; not(?(_, _))
    ; obsolete(!"<open-file> file; use <open-file> (file, mode)")
    ; <open-file> (name, "w")

  append-file = 
    <open-file> (<id>, "a")

strategies

  // :: String -> String
  file-exists =
    <access> (<id>, [F_OK()])

  // :: String -> String
  can-read-file =
    <access> (<id>, [R_OK()])

  is-readable = can-read-file

  // :: String -> String
  can-write-file =
    <access> (<id>, [W_OK()])

  is-writable = can-write-file

  // :: String -> String
  can-execute-file =
    <access> (<id>, [X_OK()])

  is-executable = can-exutable-file

  // :: String -> String
  can-create-file =
    where(dirname; can-write-file)

/**
 * Components of a pathname
 */
strategies

  /**
   * returns directory portion of pathname in a POSIX compatible way
   *
   * :: String -> String
   */
  dirname =
    string-as-chars(
      /* (1) Strip trailing slashes */
      try(split-init-last; ?(<id>, '/'))
    ; /* (2) If string consists entirely of slash characters, string shall be
             set to a single slash character.  In this case, skip steps (3)
             through (8). */
    ( ?[]
      < !['/']
      + ( /* (3) If there are any trailing slash characters in string, they
                 shall be removed. */
          init
          /* (4) If there are no slash characters remaining in string, string
                 shall be set to a single period character.  In this case, skip
                 steps (5) through (8).

             (5) If there are any trailing nonslash characters in string,
                 they shall be removed. */

        ; repeat(split-init-last; (id, not(?'/')); Fst)
        ; (?[]
          < !['.']
          + ( /* (7) If there are any trailing slash characters in string, they
                   shall be removed. */
              repeat(split-init-last; ?(<id>, '/'))
            ; (?[] < !['/'] + id))))))

/**
 * Generate a new, not existing file name.
 */
strategies

  new-file =
    rec x(<conc-strings> (<new>(), ".tmp"); try(file-exists; x))

  // (String, a) -> b :: a -> b
  temp-file(s) =
      where(new-file => f)
    ; <finally(s, try(<remove-file> f))> (f, <id>)

  /**
   * Safe, mkstemp based, creation of temporary file
   * 
   * :: _ -> (String, FileDescr)
   */
  new-temp-file =
      <conc-strings> (<temp-dir>, "/StrategoXT")
    ; mkstemp

/**
 * find files in paths
 */
strategies

  find-in-path =
    ?(file, <id>) 
    ; fetch-elem(<concat-strings; file-exists> [<id>,"/",file])

  find-in-path(mkpath) =
    file-exists 
    <+ split(id, mkpath); find-in-path
    <+ <fatal-error> ["no such file: ", <id>]

  find-file(mkpath, ext) =
    guarantee-extension(ext)
    ; find-in-path(mkpath)

  find-file(ext) =
    (guarantee-extension(ext), id)
    ; find-in-path

/**
 * Copyright (C) 1998-2003 Eelco Visser <visser@acm.org>
 * 
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
 * 02111-1307, USA.
 */