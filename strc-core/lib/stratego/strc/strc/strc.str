/** 
 * This module glues together the components that make up the Stratego
 * compiler. It is based on XTC, the transformation tool composition
 * model. 
 */

module strc

imports
  libstratego-xtc 
  libstratego-lib

  stratego/strc/strc/strc-options
  stratego/strc/strc/format-check

strategies

  strc-strc = 
    strc-command-line-options
    ; log-timed(strc-compile | "Compilation succeeded", 1)
    ; <exit> 0
   <+ log(|Error(),["Compilation failed (", <run-time ; real-to-string(|2)>, " secs)"])
    ; <exit> 1

strategies

  strc-compile =
    xtc-input(
      //strc-front-end
      strc-front-end-with-libstrc
      ; strc-optimize
      ; strc-export-external-defs
      ; strc-back-end
      ; strc-c-compile
    )

  strc-front-end =
    log-timed(
      strc-get-infile
      ; strc-pack-stratego			; fc1(|"Stratego-Sugar-Cong", "pack")
      ; strc-output-ast
      ; strc-if-not-lib(strc-add-main)
      ; strc-repair-types			; fc6(|"Stratego-Sugar-Cong", "fe-rep") // local
      ; strc-pre-desugar			; fc2(|"Stratego-Sugar-1", "fe-des1")   // local
      ; strc-combine-sections			; fc3(|"Stratego-Sugar-1", "fe-comb")   // global reorganization
      ; strc-variables-to-constructors		; fc3(|"Stratego-Sugar-1", "fe-vtc")	// global analysis

      ; strc-raise-annotations			; fc2(|"Stratego-Sugar-2", "fe-ra1")    // local
      ; strc-define-lrules			; fc3(|"Stratego-Sugar-2", "fe-dlr")    // local
      ; strc-lift-dynamic-rules								// semi-local
      ; strc-stratego-desugar								// local
      ; strc-raise-annotations			; fc2(|"Stratego-Sugar-3", "fe-ra2")	// local

      ; strc-use-def									// local analysis (earlier)
      ; strc-check-constructors			; fc4(|"Stratego-Sugar-3", "fe-cc")	// global analysis (earlier)

      ; strc-expand-overlays			; fc3(|"Stratego-Sugar-3", "fe-eo")     // global
      ; strc-rules-to-sdefs			; fc3(|"Stratego-Sugar-3", "fe-rts")    // local
      ; strc-desugar-list-matching		; fc2(|"Stratego-Sugar-4", "fe-dlm")    // local
      ; strc-desugar-default-vardec		; fc3(|"Stratego-Sugar-4", "fe-ddv")    // local
      ; strc-introduce-congdefs			; fc3(|"Stratego-Sugar-4", "fe-cdf")    // global
      ; strc-rename-vars			; fc3(|"Stratego-Sugar-4", "fe-rnv")    // local (?)
      ; strc-desugar 				; fc2(|"Stratego-Sugar-5", "fe-des")    // local

      ; strc-if-lib(strc-extract-all, strc-extract)	; fc3(|"Stratego-Sugar-5", "fe-extr")// global
      ; strc-stratego-warnings			; fc5(|"Stratego-Sugar-5", "fe-warn")   // local (earlier)
      ; strc-rename-defs			; fc3(|"Stratego-Sugar-5", "fe-rnd")    // local
      ; strc-simplify1				; fc3(|"Stratego-Sugar-Cong", "fe-sim") // global analysis
      ; strc-define-congruences			; fc1(|"Stratego-Core", "fe-cong")
      ; strc-output-frontend				
    | "Front-end succeeded"
    , 1
    )

  strc-front-end-with-libstrc =
    log-timed(
      strc-get-infile
      ; strc-pack-stratego			; fc1(|"Stratego-Sugar-Cong", "pack")
      ; strc-output-ast
      //; strc-lib-frontend
      ; read-from
      ; frontend
      ; write-to
      ; strc-output-frontend				
    | "Front-end succeeded"
    , 1
    )

  strc-optimize =
    log-timed( 
      dbg(|"starting optimizer");
	// INNERMOST FUSION
        olevel1(
	    try(where(not(<get-config> "--fusion"))
		; strc-fusion
            	; save-as2(!".opt1")
            )
          )
	// INLINING
	// inline small definitions, congruences
        ; olevel2(
	    strc-worker-wrapper
            ; save-as6(!".opt2")
	    ; strc-do-inline
            ; save-as6(!".opt3")
	    ; strc-dead-def-elim
            ; save-as4(!".opt4")
	    ; strc-simplify1
          )
        //; define-congruences
	// CONSTANT PROPAGATION
        //; olevel5( 
	//    const-prop
	//    ; bound-unbound-vars
	//    ; dead-var-elim
	//    ; simplify3
        //    ; save-as4(!".opt4")
        //  )
	// PATTERN MATCH COMPILATION
        ; olevel3( 
	    strc-compile-match
	    ; strc-desugar-case
            ; save-as2(!".opt5")
	  )
	// INLINING 
        // inline local definitions introduced by pattern match compilation
        ; olevel5(
	   strc-do-inline
           ; save-as5(!".opt7")
	   ; strc-dead-def-elim
           ; save-as5(!".opt8")
          )
	// CONSTANT PROPAGATION
        //; olevel4(
	//    simplify2 // lift term arguments from (prim) calls
        //    ; save-as3(!".opt9")
        //    ; const-prop
	//    ; save-as3(!".opt10")
	//    ; bound-unbound-vars
	//    ; save-as3(!".opt11")
	//    ; dead-var-elim
	//    ; simplify3
        //    ; save-as3(!".opt12")
	//  )
    | <concat-strings>["Optimization succeeded -O ",<get-config; int-to-string>"-O"]
    , 1
    )

strategies

  strc-export-external-defs =
    strc-if-lib(where(
      log-timed(
        strc-defs-to-external-defs
	; where(
	    <not(eq)>(<get-config>"baseout",<get-config>"basein")
	    <+ <get-config>"baseout"
	       ; debug(!"name for library same as name for base file: ")
	       ; fail
          )
        ; copy-to(strc-getoutfile(!".rtree") => file1)
	; log(|Info(), ["Abstract syntax in '", file1, "'"])

        ; xtc-transform(!"pp-stratego", !["--abstract" | <pass-verbose> ] )
        ; copy-to(strc-getoutfile(!".str") => file2)
	; log(|Info(), ["Concrete syntax in '", file2, "'"])
      | "Export of externals succeeded"
      , 1
      )
    ))

strategies

  strc-back-end =
    log-timed(
      strc-lift-definitions
      ; fc2(|"Stratego-Core", "be-lift")
      ; strc-canonicalize-ng
      ; fc2(|"Stratego-Core", "be-can")
      ; strc-simplify1
      ; fc3(|"Stratego-Core", "be-sim")
      ; strc-bound-unbound-vars
      ; fc2(|"Stratego-Core", "be-buv")
      ; strc-escaping-variables
      ; fc2(|"Stratego-Core", "be-esc")
      ; strc-s2c-ng
      ; fc-c(|1,"be-c")
      ; strc-ac2abox
      ; strc-abox2text
      ; rename-to(strc-getoutfile(!".c"))
    | "Back-end succeeded"
    , 1 
    )

strategies // Compile generated C code

  strc-c-compile =
     where(<get-config> "-c")
  <+ log-timed(
       strc-c-to-object-code 
       ; strc-link-object-code
     | "C compilation succeeded"
     , 1 
     )

  strc-c-to-object-code :
    FILE(cfile) -> FILE(ofile)
    where log(|Notice(), "Compiling C code")
	; strc-getoutfile(!".lo") => ofile
	; <strc-call-backend>
	    (<xtc-find> "libtool", [
              "--mode=compile", <xtc-find> "cc" | <concat> [<get-config> "-Xcc", ["-c", cfile,"-o", ofile]]])

  strc-link-object-code :
    FILE(ofile) -> FILE(target)
    where
      log(|Notice(), "Linking object code")
      ; (<get-config> "-o" <+ strc-getoutfile(strc-get-final-suffix)) => target
      ; strc-if-lib(strc-link-library(|ofile, target), strc-link-program(|ofile, target))

  strc-link-program(|ofile, target) =
    <strc-call-backend>
      (<xtc-find> "libtool", [
        "--mode=link", <xtc-find> "cc", ofile, "-o", target | <get-config> "-Xlinker"])

/**
 * Linking a library using libtool
 */
strategies

  strc-link-library(|ofile, target) =
      <call>
        (<xtc-find> "libtool", [
          "--mode=link", <xtc-find> "cc", ofile, "-o", target, "-rpath", <strc-libdir> target, "-avoid-version", "-no-undefined" | <get-config> "-Xlinker"])
    ; if <get-config> DisableInstall() then
        log(|Info(), ["Don't forget to install the library using: libtool --mode=install cp ", target, " ", <strc-libinstallpath> target])
      else
        log(|Info(), "Installing library")
        ; <call>
            (<xtc-find> "libtool", ["--mode=install", "cp", target, <strc-libinstallpath> target])
      end
      

  /**
   * Given a target, return the libdir.
   * @type String -> String
   */
  strc-libdir =
    <get-config> LibDir()
    ; abspath

    <+ dirname
       ; abspath

  /**
   * Given a target, return the location for installing the library.
   *
   * @type String -> String
   */
  strc-libinstallpath =
    <conc-strings> (<get-config> LibDir(), "/", <base-filename>)
    ; abspath
    <+ abspath


strategies

  /**
   * FIXME enable the silent backend calls if the STR-853 has been solved.
   */
  strc-call-backend =
    ?(tool,args)
  ; log(|Notice(),<separate-by(|" ") ; concat-strings>["Command:",tool|args])
  ; //if <geq>(<verbosity>,2) then
      call
/*    else
      where(
        xtc-new-file => f
      ; open => fd
      )
    ; ( call(|None,Some(fd),Some(fd))
      ; <close>fd
     <+ <close>fd
      ; <read-text-file>f
      ; log(|Error(),"Command failed :")
      ; debug
      ; fail
      )
    end
*/

strategies

  // building a library

  strc-if-lib(s1, s2) = where(<get-config>"--library") < s1 + s2
  strc-if-lib(s)      = strc-if-lib(s, id)
  strc-if-not-lib(s)  = strc-if-lib(id, s)
  strc-pass-library   = strc-if-lib(!["--library"], ![])

strategies

  log-timed(s|msg,level) =
    if <geq>(<get-config> "--statistics", level) then 
      where(times => start)
    ; s 
    ; where(
        <diff-times>(<times>,start)
      ; <concat-strings>[ <align-left>(' ', msg, 27)
                        , " : [user/system] = ["
                        , <self-children-user-time; ticks-to-seconds ; real-to-string(|2)>
                        , "s/"
                        , <self-children-sys-time; ticks-to-seconds ; real-to-string(|2)>
                        , "s]"
                        ]
      ; log(|Info(),<id>)
      )
    else
      s
    end

  // save intermediate results

  save-as(ext) = 
    where(
      copy-to(strc-getoutfile(<conc-strings> (".sci", <ext>)) => file1)
      ; log(|Info(), ["abstract syntax in '", file1, "'"] )
      ; xtc-transform(!"stratego-parenthesize", pass-verbose)
      ; xtc-transform(!"stratego2abox", pass-verbose)
      ; xtc-transform(!"abox2text", pass-verbose)
      ; copy-to(<conc-strings> (file1, ".txt") => file2)
      ; log(|Info(), ["concrete syntax in '", file2, "'"])
    )

  save-as1(ext) = if-keep1(save-as(ext))
  save-as2(ext) = if-keep2(save-as(ext))
  save-as3(ext) = if-keep3(save-as(ext))
  save-as4(ext) = if-keep4(save-as(ext))
  save-as5(ext) = if-keep5(save-as(ext))
  save-as6(ext) = if-keep6(save-as(ext))
  save-as7(ext) = if-keep7(save-as(ext))
//  save-as8(ext) = if-keep8(save-as(ext))

  strc-output-ast =
    if <get-config> "--ast" then
       copy-to(strc-getoutfile(!".ast") => file)
       ; notice(|<concat-strings>["Abstract syntax saved in file `", file, "'"])
       ; xtc-io-exit
    end

  strc-output-frontend =
    if <get-config> "-F" then
      xtc-io-exit
    end

strategies

  /**
   * Adding main strategy
   * 
   * What happens if the specification already contains a main strategy? 
   */

  strc-add-main =
    try(
      where(
        <get-config> "-m" => m
      ; log(|Notice(),["Main strategy is : ",m])
      )
      ; if-not-lib(
          xtc-io-transform(try(strc-AddMain(|m)))
        )
      )
    ; save-as3(!".with-main")

  strc-AddMain(|main) : 
    Specification(sects) -> 
    Specification([ Strategies([SDef("main", [], Call(SVar(main), []))]) 
                  | sects ])
      where
        <not("main")>main
      ; <try(strc-check-other-main)>sects

  strc-check-other-main =
    fetch-elem( Strategies(fetch-elem(strc-is-main)) + Rules(fetch-elem(strc-is-main)) )
  ; say(!"Adding main strategy even though one is already present!")

  strc-is-main =
    ?SDef("main", [], _)
  + ?RDef("main", [], _)
 
strategies 

  strc-get-infile :
    FILE(file) -> FILE(file)
    where <remove-extension> file => basein
        ; log(|Debug(),["Basename of input file: ",basein])
        ; <set-config> ("basein",  basein)
        ; (<get-config; remove-extension> "-o" <+ !basein; strc-if-lib(<conc-strings> ("lib", <id>))) => baseout
	; log(|Debug(),["Basename of output file: ",baseout])
        ; <set-config> ("baseout", baseout)

  strc-getoutfile(suffix) =
    <conc-strings>(<get-config> "baseout", <suffix>)

  /**
   * @todo Support for $(EXE)?
   */
  strc-get-final-suffix =
    strc-if-lib(!".la", !"")

  strc-dep-name =
    !["--dep", <<get-config> "-o" <+ <get-config> "baseout">]

strategies // COMPILER COMPONENTS

  comp-out(|f) =
    log-timed(xtc-transform(!f, pass-verbose) | <comp-msg>f, 2)

  comp(|f) =
    log-timed(xtc-transform(!f, !["-b" | <pass-verbose> ]) | <comp-msg>f, 2)

  comp(|f,args) =
    log-timed(xtc-transform(!f, <concat>[["-b"], <pass-verbose>, args]) | <comp-msg>f, 2)

  comp-msg = <concat-strings>["  ",<id>]

// FRONTEND

  strc-pack-stratego = 
    comp(|"pack-stratego",<concat> [
          <strc-dep-name>, <strc-get-include-dirs>, <pass-keep>,
	  <get-config < !["--asfix"] + ![]> "--asfix",
	  <get-config < !["--format-check", <int-to-string>] + ![]> "--format-check",
	  <get-config < !["--prefer-str"] + ![]> "--prefer-str"
    ])


  strc-pre-desugar		= comp(|"pre-desugar")
  strc-repair-types		= comp(|"repair-types")
  strc-combine-sections		= comp(|"combine-sections")
  strc-variables-to-constructors= comp(|"variables-to-constructors")
  strc-raise-annotations	= comp(|"raise-annotations")
  strc-define-lrules		= comp(|"define-lrules")
  strc-lift-dynamic-rules	= comp(|"lift-dynamic-rules")
  strc-stratego-desugar		= comp(|"stratego-desugar")
  strc-use-def			= comp(|"use-def", <strc-pass-maybe-unbound-warnings>)
  strc-check-constructors	= comp(|"check-constructors")
  strc-expand-overlays		= comp(|"expand-overlays")
  strc-rules-to-sdefs		= comp(|"rules-to-sdefs")
  strc-desugar-list-matching	= comp(|"desugar-list-matching")
  strc-desugar-default-vardec	= comp(|"desugar-default-vardec")
  strc-introduce-congdefs	= comp(|"introduce-congdefs")
  strc-rename-vars		= comp(|"rename-vars")
  strc-desugar			= comp(|"desugar")
  strc-extract-all		= comp(|"extract-all")
  strc-extract			= comp(|"extract")
  strc-stratego-warnings	= comp(|"stratego-warnings", <pass-warnings>)
  strc-rename-defs		= comp(|"rename-defs")
  strc-simplify1		= comp(|"simplify1")
  strc-define-congruences	= comp(|"define-congruences")

  strc-lib-frontend =
    comp(|"frontend", <concat>[<strc-pass-main>, <pass-warnings>, <strc-pass-library>])

  strc-pass-main =
    !["--main", <get-config> "-m"] <+ ![]

// OPTIMIZER

  strc-fusion			= comp(|"fusion")
  strc-do-inline 		= comp(|"inline")
  strc-dead-def-elim		= comp(|"dead-def-elim", <strc-if-lib(!["--only-local"],![])>)
  strc-compile-match		= comp(|"compile-match")
  strc-desugar-case 		= comp(|"desugar-case")
  strc-const-prop 		= comp(|"const-prop")
  strc-bound-unbound-vars 	= comp(|"bound-unbound-vars")
  strc-dead-var-elim		= comp(|"dead-var-elim")
  strc-worker-wrapper		= comp(|"worker-wrapper")
  strc-simplify2 		= comp(|"simplify2")
  strc-simplify3 		= comp(|"simplify3")
  strc-canonicalize 		= comp(|"canonicalize")
  strc-canonicalize-ng		= comp(|"canonicalize-ng")
  strc-lift-definitions 	= comp(|"lift-definitions")
  strc-bound-unbound-vars 	= comp(|"bound-unbound-vars")
  strc-escaping-variables 	= comp(|"escaping-variables")

// EXPORT

  strc-defs-to-external-defs = comp(|"defs-to-external-defs", ["--no-inlining"])

// BACK-END

  strc-ac2abox = 
    where(
      if <get-config>"--indent-c" then
        !"C-pretty.pp.af"
      else
        !"C.pp.af"
      end => pp-table 
    )
  ; comp(|"ast2abox", ["-p", <xtc-find> pp-table ])
    
  strc-abox2text = comp-out(|"abox2text")

  strc-s2c 	= comp(|"s2c",  
	 		<concat>[
			  <get-config; mapconcat(!["--C-include", <id>])> "--C-include",
			  <strc-pass-library>
			])

  strc-s2c-ng 	= comp(|"s2c-ng",  
	 		<concat>[
			  <get-config; mapconcat(!["--C-include", <id>])> "--C-include",
			  <strc-pass-library>
			])
