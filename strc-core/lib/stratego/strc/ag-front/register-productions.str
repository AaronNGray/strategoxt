module stratego/strc/ag-front/register-production
imports
  libstratego-lib
  libstratego-ag
  Stratego

imports
  stratego/strc/ag-front/register-symbols

rules
  GetOpDeclName: OpDecl(n, _) -> n

  get-opdecl-name = GetOpDeclName

  GetTypeSorts: FunType(a, r) -> (<map(GetTypeSorts)> a, s)
    where <GetTypeSorts> r => ([], s)

  GetTypeSorts: ConstType(s) -> ([], s)

  GetOpDeclSorts: OpDecl(_, t) -> <GetTypeSorts> t
  GetOpDeclSorts: OpDeclInj(t) -> <GetTypeSorts> t

  get-opdecl-sorts =
    GetOpDeclSorts
  ; normalize-sorts-type
  ; (map(sort-2-ref), sort-2-ref)
 <+ err(|"Cannot find the ''type'' of this constructor declaration: ")
  ; fail

  normalize-sorts-type =
    (map( \ ([], s) -> s \ ), id )
 <+ err(|"Cannot normalized the ''type'': ")
  ; fail

strategies
  is-opdecl =
    OpDecl(id, id)
  + OpDeclInj(id)

  implicit-productions =
    dr-all-keys(|"Sort2Ref")
  ; mapconcat({
      ?l@ Sort |[ List(~srt: e) ]|
    ; ![|[ : ~srt: l ]|
      , |[ : ~srt: e * ~srt: l -> ~srt: l ]|
      ]
   <+ ![]
    })

  register-production =
    restore(
      ?opdecl
    ; get-opdecl-sorts => (uses, produce)
    ; new-production(| uses, produce ) => this
    ; if <get-opdecl-name> opdecl => name then
        set-production-name(|name)
      ; rules(ProdName2Ref:+ name -> this)
      ; where(<dbg(|"register production:")> (opdecl, name, this))
      else
        rules(ProdName2Ref:+ "" -> this)
      ; where(<dbg(|"register production:")> (opdecl, this))
      end
    , err(|"Cannot register the following constructor declaration as a production:")
    )

  register-productions = where(
    <union> (<implicit-productions>, <collect(is-opdecl)>)
  ; remove-all(oncetd(not-handle-sort))
  ; map(register-production)
  )

strategies
  match-to-build =
    topdown(try(
      ?As(_, <id>) + ?OpInj(_, [<id>])
    ))
  ; not(oncetd(?Wld()))

signature
  constructors
    RewriteRule: TermType * Strategy * TermType -> RewriteProperty

strategies
  duplicate-production-with-precondition(|s) =
    err(|"duplicate-production-with-precondition: Not implemented yet.")
  ; fail

  // weird |[ t1 where s1 -> t2 where s2 ]|
  // -> |[ \ _ -> t1 where s1 \; compute-ty1; \ t1 -> t2 where s2 \; compute-ty2 ]|
  register-rewrite-production =
    restore(
      ?rewrite-id
    ; map( ?(<get-term-type-symbol>, _) ) => key
    ; dbg(|"Rewrite production key:")
    ; memo-var(id
      , new-production(| <id>, <last> ) => this
      ; set-production-as-rewrite(|[])
      ; rules(ProdName2Ref:+ key -> this)
      ; where(<dbg(|"register rewrite production:")> (key, this))
      , ProdName2Ref
      )
    , err(|"Cannot register the following rewriting declaration as a production:")
    )
