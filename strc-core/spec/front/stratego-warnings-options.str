/**
 * @author  Merijn de Jonge (mdejonge@tue.nl)
 */
module stratego-warnings-options
imports liblib Stratego-Sugar

signature
  constructors
    ObsoleteWarning    : Str -> Warning
    DebugWarning       : Str -> Warning 
    ConstructorWarning : Str -> Warning

overlays

  AllWarnings = [
     ("debug-arguments", "        missing build operator"), 
     ("obsolete-strategy-calls", "obsolete strategies"), 
     ("maybe-unbound-variables", "unbound variables"),
     ("lower-case-constructors", "lower-case constructors")
     ]

strategies

  stratego-warnings-options = 
    ArgOption("--warning" + "-W"
    , switch id
        case "all"  :
          <set-config> ("--warning", <map(Fst)> AllWarnings)

        case explode-string => ['n', 'o', '-' | wchars] :
          <implode-string> wchars => wstring
          ; <get-config> "--warning"
          ; filter(?wstring)
          ; <set-config> ("--warning", <id>)

        case <elem> (<id>,  <map(Fst)> AllWarnings) :
          <extend-config> ("--warning", [<id>])

        otherwise :
          <fatal-error> [<get-config> "program", ": ", <id>, ": no such warning"]
      end
    , <concat-strings>
        [ "-W,--warning C   Report warnings falling in category C. Categories:\n",
       "                      all                      all categories (default) \n",
       "                      no-C                     no warnings in category C\n"
        | <Display-possible-warnings> AllWarnings() ]
    )

  Display-possible-warnings = 
     map( \ (s,txt) -> <concat-strings> ["                      ",s, "  ", txt, "\n"] \ )

  get-warnings =
    <get-config> "--warning" <+ !AllWarnings(); map(Fst)
    
  pass-warnings = 
    get-warnings
    ; mapconcat(!["--warning", <id>])
