/**
 * Desugar list matching.
 */

module desugar-list-matching
imports Stratego-Sugar desugar stratlib
strategies

  desugar-list-matching-io = 
    io-wrap(desugar-list-matching)

strategies

  desugar-list-matching =
    rec dlm(
      alltd(
        (ListVarScope + desugarRule); all(dlm)
        <+ Build(dlm-in-build(dlm))
        <+ Match(dlm-in-match(dlm))
      )
    )

  dlm-in-build(dlm) =
    alltd(
      ListVarAsTail 
      + ListVarInBuild
      + App(dlm, dlm-in-build(dlm))
      + RootApp(dlm)
    )

  dlm-in-match(dlm) =
    alltd(
      ListVarAsTail 
      + App(dlm, dlm-in-match(dlm))
      + RootApp(dlm)
    )

rules

  ListVarScope :
    Scope(xs, s) -> Scope(ys, s)
    where <map(try(?ListVar(<id>)))> xs => ys

  ListVarScope :
    SDefT(f, as1, as2, s) -> SDefT(f, as1, as2', s)
    where <map(try(?ListVar(<id>)))> as2 => as2'

  SingleListVar :
    Var(ListVar(x)) -> Var(x)

  is-nil = 
    ?Term|[ Nil() ]| 
    + ?Term|[ Nil(){^pt*} ]| 
    + ?PreTerm|[ Nil() ]|

  ListVarAsTail : 
    PreTerm|[ Cons(lid, t) ]| -> PreTerm|[ lid ]|
    where <is-nil> t

  ListVarInBuild : 
    PreTerm|[ Cons(lid, t) ]| -> PreTerm|[ <conc>(lid, t) ]|
    where <not(is-nil)> t