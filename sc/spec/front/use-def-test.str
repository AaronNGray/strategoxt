module use-def-test
imports use-def sunit
strategies

  use-def-test =
    test-suite(!"use-def-test",
      test1;
      test2;
      test3;
      test4;
      test5;
      test6;
      test7;
      test8;
      test9;
      test10;
      test11;
      test12
    )

  test1 = 
    apply-and-fail(!"test1"
	, Strategies(defs-use-def)
	, !|[ strategies 
                P() = ?F(p, q); !F(p,H(q,q'))
	        Q() = (?F(p, q) + ?G(p)); !H(p,q)
	        R() = ?F(p, q); !F(p,H(q))
           ]|
	)

  test2 = 
    apply-test(!"test2a"
	, def-use-def
	, !|[ P() = ?F(p, q); !F(p,H(q)) ]|
	);
    apply-and-fail(!"test2b"
	, def-use-def
	, !|[ P() = ?F(p,q); !F(q,G(p,p')) ]|
	)

  test3 = 
    apply-test(!"test3a"
	, def-use-def
	, !|[ P() = ?F(p, q); {q: ?q; !F(p,H(q)) } ]|
	);
    apply-and-fail(!"test3b"
	, def-use-def
	, !|[ P() = ?F(p,q); {q: !F(q,G(p,p')) } ]|
	)

  test4 = 
    apply-and-fail(!"test4a"
	, def-use-def
	, !|[ P() = ?v; !F("SomeVar", <{v: !v; Q()}>) ]|
	);
    apply-test(!"test4b"
	, def-use-def
	, !|[ P() = ?v; !F("SomeVar", <Q()> v) ]|
	);
    apply-and-fail(!"test4c"
	, def-use-def
	, !|[ P() = !F("SomeVar", <{v: !v; Q()}>) ]|
	)

  test5 = 
    apply-test(!"test5a"
	, def-use-def
	, !|[ P() = (?F(p, q) + ?G(p)); !H(p) ]|
	);
    apply-and-fail(!"test5b"
	, def-use-def
	, !|[ P() = (?F(p, q) + ?G(p)); !H(p,q) ]|
	)

  test6 = 
    apply-test(!"test6a"
	, def-use-def
	, !|[ P() = ((Q() <+ R()); ?p); !F(p) ]|
	);
    apply-and-fail(!"test6b"
	, def-use-def
	, !|[ P() = (Q() <+ R(); ?p); !F(p) ]|
	)

  test7 = 
    apply-test(!"test7a"
	, def-use-def
	, !|[ P() = ?F(p,q) < !H(q) + ?G(p); !H(p) ]|
	);
    apply-and-fail(!"test7b"
	, def-use-def
	, !|[ P() = (?F(p,q) < !H(p) + ?G(p)); !H(p,q) ]|
	);
    apply-and-fail(!"test7c"
	, def-use-def
	, !|[ P() = ?F(p,q) < !H(p) + (?G(p); !H(p,q)) ]|
	)

  test8 = 
    apply-test(!"test8a"
	, def-use-def
	, !|[ P() : F(p,q) -> H(q, q') where !p; Q(); ?q' ]|
	);
    apply-and-fail(!"test8b"
	, def-use-def
	, !|[ P() : F(p,q) -> H(q, q') where id ]|
	)


  test9 = 
    apply-test(!"test9a"
	, def-use-def
	, !|[ P() = \ F(p,q) -> H(q, q') where !p; Q(); ?q' \ ]|
	);
    apply-and-fail(!"test9b"
	, def-use-def
	, !|[ P() = \ F(p,q) -> H(q, q') where id \ ]|
	)

  test10 = 
    apply-and-fail(!"test10"
	, Overlays(defs-use-def)
	, !|[ overlays
		F(p, q) = H(G(p, "a"), q)
	        L(p, q) = K(p, "b", q')
           ]|
	)

  test11 = 
    apply-test(!"test11a"
	, def-use-def
	, !|[ P() = ?Cons(X, L)
	            ; {YS: ?Cons(Y, YS)}
	            ; !Cons(Y, Cons(X, L))
	   ]|
	);
    apply-test(!"test11b"
	, Strategies(defs-use-def)
	, !|[ strategies
	        LSort(S) :
	          Cons(X, L) -> Cons(Y, Cons(X, L'))
	          where !L; at-suffix({YS: ?Cons(Y, YS); where(<S()> Tup(X, Y)); !YS}); ?L'
	   ]|
	)

  test12 = 
    apply-test(!"test12a"
	, def-use-def
	, !RDef("test4", [], [[ Foo(?p) --> Foo(!p) where id ]])
	) 
