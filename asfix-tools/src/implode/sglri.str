module sglri
imports
  liblib
  sglri-options
  error-reporting

strategies

  sglri =
    init-sglr-options
    ; xtc-io-wrap(sglri-options,
        parse-and-implode
        <+ <xtc-exit> 1
      )

  sglri-options =
      sglr-parse-table-option
    + sglr-start-symbol-option

    + Option(fail, fail, !"")

    + sglr-filter-option

    + Option(fail, fail, !"")

    + custom-sglr-option
    + custom-implode-option

    + Option(fail, fail, !"")

strategies

  parse-and-implode =
    let try-get-parse-table =
          <get-config> ParseTable() <+ fatal-err(|"No parse table specified")

     in xtc-sglr-strict(|<try-get-parse-table> (),
          <concat> [
              ["-2"]
            , <pass-sglr-start-symbol> ()
            , <pass-custom-sglr-options> ()
            , <pass-sglr-verbose> ()
            ])
    end;

    let pass-binary-output =
          if <get-config> "-b" then
            !["-b"]
          else
            ![]
          end

     in implode-asfix(
          <concat> [
            <pass-custom-implode-options> ()
          , <pass-binary-output> ()
          , <pass-verbose> ()
          ])
    end

strategies

  implode-asfix(args) =
    xtc-transform(!"implode-asfix", args)

  xtc-sglr-strict(|tbl, args) =
    ?input
    ; xtc-transform-always(id, report-sglri-error-summary(|input)
      | "sglr", ["-p", tbl | args ])

  xtc-transform-always(c, f| tool, args) =
    ?FILE(f)
    ; where(<xtc-new-file> f => g)
    ; <conc> (args, ["-i", f, "-o", g])
    ; if xtc-command-silent(|tool) then <c> FILE(g) else <f> FILE(g) end

  xtc-transform-always(c, f | tool, args) =
    ?stdin()
    ; where(xtc-new-file => g)
    ; <conc> (args, ["-o", g])
    ; if xtc-command-silent(|tool) then <c> FILE(g) else <f> FILE(g) end

  xtc-command-silent(|tool) =
    where(<xtc-find-warning> tool => loc)
    ; log(|Debug(),<concat-strings>["Invoking tool ", tool," at location ",loc," with arguments: "],<id>)
    ; where(<call(|None(), None(), Some(<open> "/dev/null"))> (loc, <id>))

  /**
   * Reports an sglri error summary to the stderr.
   * Always fails.   
   *
   * @type  summary -> _
   */
  report-sglri-error-summary(|input) =
    finally(
      read-from
      ; ?summary
      ; <report-error-summary(|summary)> ()
    , fail
    )
