module asfix-anno-comments
imports liblib AsFix

strategies

 main-asfix-anno-comments = 
    io-wrap(comment-sort-option,
      init-comment-sorts
      ; topdown-consnil(try(AnnoComment))
    )

strategies

  AnnoComment :
    [ a1@appl(prod([cf(layout)], cf(opt(layout())), _), args)
    , a2
    | as ]
      ->
    [ a1, a2' | as ]
    where
      <fetch-comment> args => (comment,sort)
      ; <try(oncetd({p:
          has-cons; ?p{a*}; !p{(Comment(sort), comment), a*}
         }))> a2 => a2'

  fetch-comment =
    where(get-comment-sorts => sortnames)
    ; oncetd({sortname:
      ?appl(prod(_, lex(sort(sortname)), _), _)
      ; where(<fetch(?sortname)> sortnames => sort)
      ; yield
      ; !(<id>,sortname) => s
      })
    ; !s

  has-cons =
    where(
    ?appl(prod(_, _, <id>), _)
    ; oncetd(?cons(_))
    )

strategies

  topdown-consnil(s) =
    rec x(s; all-consnil(x))

  all-consnil(s) =
    if is-list then
      [s | id]; [id | s] + []
    else
      all(s)
    end

rules
  Kids  : appl(p, ts) -> <concat> ts
  Kids' : x -> [x]

strategies

  yield =
    rec x((appl(id, map(x)); Kids) <+ Kids')
    ; implode-string

signature
   constructors
     Comment : String -> AnnoKey

strategies
  
  comment-sort-option = 
    ArgOption("--sort"
    , <extend-config> ("comment-sorts", [<id>])
    , !"--sort name      Specify a comment sort. Multiple --sort options are allowed [Comment]"
    )
    
  init-comment-sorts =
    where(<get-config> "comment-sorts" <+ <set-config> ("comment-sorts", ["Comment"]))

  get-comment-sorts =
    <get-config> "comment-sorts"
