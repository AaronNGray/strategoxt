module sglri
imports
  libstratego-sglr
  libxtclib
  sglri-options
  implode-asfix

strategies

  main-sglri =
    xtc-io-wrap(sglri-options,
      if output(parse-and-implode) then
        <xtc-exit> 0
      else
        <xtc-exit> 1
      end
    )

  print-config =
    if get-ambiguity-is-error then
      say(!"ambiguities: off")
    else
      say(!"ambiguities: on")
    end

strategies

  parse-and-implode =
    ?input;
    let try-get-parse-table =
          <get-config> ParseTable()
          ; ReadFromFile
          ; open-parse-table
          <+ fatal-err(|"No parse table specified")

        try-get-start-symbol =
          <get-config> StartSymbol() <+ !None()

     in where(
          try-get-parse-table => tbl
        ; try-get-start-symbol => sort
        )
      ; open-input-stream => stream
      ; ( finally(
            parse-stream-pt(|tbl, sort, <path-of-input> input)
          , <fclose> stream
            ; <close-parse-table> tbl
          ) <+ report-parse-error
        )
      ; implode-asfix-using-config
    end

strategies

  path-of-input =
    ?FILE(<id>)

  path-of-input =
    ?stdin(); !"stdin"

  open-input-stream =
    ?FILE(<id>); <fopen> (<id>, "r")

  open-input-stream =
    ?stdin(); stdin-stream

  report-parse-error =
    finally(
      ?input
      ; get-parse-error => summary
      ; <report-error-summary(|summary)> input
    , fail
    )

strategies

  sglri-options =
      sglr-parse-table-option
    + sglr-start-symbol-option
    + sglr-amb-option
    + Option(fail, fail, !"")

    + sglr-filter-option
    + Option(fail, fail, !"")

    + custom-sglr-option
    + custom-implode-option
    + Option(fail, fail, !"")

    + implode-asfix-options
    + Option(fail, fail, !"")
