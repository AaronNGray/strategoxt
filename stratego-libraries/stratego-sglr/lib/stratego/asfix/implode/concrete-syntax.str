/**
 * Implosion of concrete syntax.
 */
module stratego/asfix/implode/concrete-syntax
imports
  stratego/concrete-syntax/markers

strategies
    
  skip-concrete(impl) =
    ?appl(prod, args)
    ; <get-cons> prod => c
    ; <strsglr-is-quote-cons> c
    ; where(<remove-all(is-ignorable-syntax)> args => [ap])
    ; !c#([ap])
    ; rec skip(
        alltd(
           appl(prod(one(varsym(id)), id, id), id)
           ; impl

        <+ appl(where(get-cons; strsglr-is-antiquote-cons), id)
           ; impl

        <+ annotation(impl, skip)
        )
      )

  /**
   * @todo Reuse existing strategies to get the constructor.
   */
  annotation(impl, skip) =
    ?appl(prod, <id>)
    ; where(<get-cons> prod => "WithAnno")
    ; filter(not(is-ignorable-syntax); not(appl(prod(id, oncetd(lex(id)), id), id)))
    ; ?[x|xs] 
    ; !"WithAnno"#([<skip> x| <impl> xs])

/**
 * Yielding of quotes. Should be applied after implosion.
 */
strategies

  yield-asfix-quotes =
    alltd(strsglr-quote-cong(asfix-yield-quote))

  asfix-yield-quote =
    let yield(|chars) =
            asfix-yield-acc(yield | chars)
          + asfix-yield-quote-acc(yield | chars)

     in appl(!prod([], "yield", no-attrs()), yield(|[]); reverse)
      + "WithAnno"#([asfix-yield-quote | id])
      + strsglr-metavar-cong(id)
      + strsglr-antiquote-cong(yield-asfix-quotes)
    end
    
  /**
   * WithAnno has no meaning in yielded quotes.
   */
  asfix-yield-quote-acc(rec : b * c -> d | chars1) :
    "WithAnno"#([t | _]) -> chars2
    where
      <rec(|chars1)> t => chars2

  asfix-yield-quote-acc(rec : b * c -> d | chars1) :
    amb([x | _]) -> chars2
    where
      <rec(|chars1)> x => chars2

  asfix-yield-quote-acc(rec : b * c -> d |chars1) =
    strsglr-metavar-cong(id)
    ; ![<id> | chars1]

  asfix-yield-quote-acc(rec : b * c -> d |chars1) =
    strsglr-antiquote-cong(yield-asfix-quotes)
    ; ![<id> | chars1]
