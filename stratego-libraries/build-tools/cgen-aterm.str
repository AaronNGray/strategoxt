/**
 * Generates a C program with a function that produces the
 * given ATerm without any dependency on a file in the
 * fule system.
 *
 * @author Martin Bravenboer <martin.bravenboer@gmail.com>
 */
module cgen-aterm
imports libstratego-lib
signature
  constructors
    FunctionName : ConfigKey

strategies

  main-cgen-aterm =
    input-wrap(functionname-option,
      tbl := <write-to-shared-string>
      ; where(print-header(|tbl))
      ; repeat(
          splitmap(print-elem|15)
        ; print-newline
        ; Snd
        <+ map(print-elem); fail
        )
      ; where(print-footer(|tbl))
      )

  functionname-option =
      ArgOption("--name"
      , OptionHandler(<set-config> (FunctionName(), <id>))
      | "--name           Function name for getting the ATerm"
      )

    + OptionCheck(<set-config> ("--help", ()),
        OptionOneCheck("--name")
      | "error: option --name is required"
      )

strategies

  print-header(|tbl) =
    where(
      <get-config> FunctionName()
    ; <fputstrings> (["
#include <aterm1.h>
#include <srts/stratego.h>

static ATerm ", <id>, "_0_0(StrSL sl, ATerm t);
static void init_module_constructors() {}
static void init_module_constant_terms() {}
static void register_strategies() {
  static struct str_funlink fl = { &", <id>, "_0_0, NULL };
  static struct str_closure cl = { &fl , NULL };
  SRTS_register_function((ATerm)ATmakeAppl0(ATmakeSymbol(\"", <id>, "_0_0\", 0, ATtrue)), &cl);
}
static void init_strategies() {}

#include <srts/init-stratego-module.h>

static ATerm ", <id>, "_0_0(StrSL sl, ATerm t) {
  static ATerm result = NULL;
  static char parsetable[] = {
    "], <stdout-stream>)
    )

  print-footer(|tbl) =
    where(<fputstrings> (["
  0 };

  if(result == NULL) {
      result = ATreadFromSharedString(parsetable, ", <length; int-to-string> tbl, ");
    if(result != NULL) {
      ATprotect(&result);
    }
  }
  return result;
}
"], <stdout-stream>)
    )

  print-newline =
    where(<fputs> ("\n", <stdout-stream>))

  print-elem =
    <fputs> (<int-to-string>, <stdout-stream>)
    ; <fputs> (",", <stdout-stream>)

  fputstrings =
    ?(strings, stream)
    ; <map(<fputs> (<id>, stream))> strings
