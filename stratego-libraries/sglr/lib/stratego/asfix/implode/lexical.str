/**
 * Implosion of lexical syntax.
 */
module stratego/asfix/implode/lexical
strategies

  ImplodeLexical =
    ?appl(prod([lex(_)],cf(_),_), _)
    ; asfix-yield-appl

  ImplodeLexical =
    ?appl(prod(_,lex(_),_), _)
    ; asfix-yield-appl

  ImplodeLexical =
    ?appl(prod(_,lit(_),_),_)
    ; !lit(<asfix-yield-appl>)

  ImplodeLexical =
    ImplodeLexicalListVar <+ ImplodeLexicalVar
    
  ImplodeLexicalListVar =
    ?appl(prod(_,varsym(cf(iter-star(_))),_),_)
    ; !meta-listvar(<asfix-yield-appl>)

  ImplodeLexicalListVar =
    ?appl(prod(_,varsym(cf(iter-star-sep(_,_))),_),_)
    ; !meta-listvar(<asfix-yield-appl>)

  ImplodeLexicalListVar =
    ?appl(prod(_,varsym(cf(iter(_))),_),_)
    ; !meta-listvar(<asfix-yield-appl>)

  ImplodeLexicalListVar =
    ?appl(prod(_,varsym(cf(iter-sep(_,_))),_),_)
    ; !meta-listvar(<asfix-yield-appl>)

  ImplodeLexicalVar =
    ?appl(prod(_,varsym(_),_),_)
    ; !meta-var(<asfix-yield-appl>)

strategies

  /**
   * The yield of an AsFixTerm is the string consisting of the
   * characters at the leaves of the tree.1
   */
  asfix-yield-appl =
    let yield(|chars) =
            asfix-yield-acc(yield | chars)

        main-yield =
          yield(|[])
          ; reverse
          
     in ?appl(_, <id>){anno*}
      ; yield(|[])
      ; reverse
      ; implode-string
      ; ?s
      ; !s{anno*}
    end

  asfix-yield-acc(rec : b * c -> d |chars1) :
    appl(_, ts) -> chars2
    where
      <rec(|chars1)> ts => chars2

  asfix-yield-acc(rec : b * c -> d |chars) :
    [] -> chars

  asfix-yield-acc(rec : b * c -> d |chars0) :
    [x | xs] -> chars2
    where
      <rec(|chars0)> x => chars1
    ; <rec(|chars1)> xs => chars2

  asfix-yield-acc(rec : b * c -> d | chars) :
    i -> [i | chars]
    where
      is-int
