\literate[koala-make]

 KoalaTools -- Tool suite for Koala component model

 Author: Merijn de Jonge (mdejonge@cs.uu.nl)

 $Id: bundlegen.str 5017 2004-01-27 11:53:48Z merijn $

\begin{code}
module koala-make
imports lib CompositeSystem

strategies

koala-make = iowrap((id,Koala-make),id)

Koala-make = 
   Composition(?components);
   !components;
   filter(
      \Module(_,Path(PathName(path)),_,_) -> <build-path>path \
   );
   <table-getlist>"koala-make";
   map(createAutomakefile)

build-path =
   ?[path|subpaths];
   !([path], subpaths);
   rec x ({path,subpath,subsubpaths:
      (id, [])
   <+
      ?(path, [subpath]);
      <table-append>("koala-make", path, [subpath])
   <+ 
      ?(path, [subpath|subsubpaths]);
      <table-append>("koala-make", path, [subpath]);
      <x>( <conc>(path,[subpath]), subsubpaths)
   })

createAutomakefile: (path, subpaths) -> ( full-path, Automakefile)
where
   <concat-strings>[<path2string>path, "/", "Makefile.am"] => full-path;
   <concat-strings> [
      "SUBDIRS=",<separate-by;concat-strings>(" ",subpaths),"\n\n",
      "all: install\n"
   ] => Automakefile

path2string: p -> s
   where
   <separate-by;concat-strings>("/", p) => s

\end{code}
% Copyright (C) 2004 Merijn de Jonge <mdejonge@cs.uu.nl>
%
% This program is free software; you can redistribute it and/or modify
% it under the terms of the GNU General Public License as published by
% the Free Software Foundation; either version 2, or (at your option)
% any later version.
%
% This program is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with this program; if not, write to the Free Software
% Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
% 02111-1307, USA.

