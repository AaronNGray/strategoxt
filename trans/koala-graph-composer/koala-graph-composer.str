\literate[koala-graph-composer]

 KoalaTools -- Tool suite for Koala component model

 Author: Merijn de Jonge (mdejonge@cs.uu.nl)

 $Id: bundlegen.str 5017 2004-01-27 11:53:48Z merijn $

\begin{code}
module koala-graph-composer
imports lib CompositeSystem GraphXML

strategies
koala-graph-composer = 
   iowrap(Koala-graph-composer)

short-description(p) =
   !["Usage: ", <p>(), " -i file [options]"]

long-description(p) =
   ![<p>(), " makes a composition of graphs in GXML format according ",
    "to a composition generated by koala-compiler from a Koala ",
    "specification.\n\n",
    "See 'koala-compiler'."]

Koala-graph-composer = 
   Composition(?comps);
   !comps;
   filter( Module(id,id,id,id));
   where(
     map( \Module(Name(name), Path(PathName(path)), Some(Url(url)),_) -> 
        (path,<concat-strings>[url, "/", name, ".gxml"] )\ )
   => graph-files
   );
   map( {edges: \
     Module(Name(name),Path(PathName(path)),_,links) -> edges 
     where
       collect( {n2:\  Wire(Interface(intf),_,Target(PathName(target))) -> 
                     edge1([target(<concat;concat-strings>[path,["_",intf]]),
                            source(<concat;concat-strings>[target,["_",intf]])]) 
                            \ }) => edges 
   \ } ); flatten-list => edges;
   <concat-graph-files>graph-files;
   !GraphXML([],[graph([],<conc>(edges, <id>))])

concat-graph-files = 
   map( {graph:\
   (path, file-name) -> graph 
   where
   !file-name;
   ReadFromFile;
   topdown( try(is-string;
            \ s -> <concat;concat-strings>[path, ["_",s]] \ ) )
   => graph
    \ });
   collect(node1(id)+edge1(id))

\end{code}
% Copyright (C) 2004 Merijn de Jonge <mdejonge@cs.uu.nl>
%
% This program is free software; you can redistribute it and/or modify
% it under the terms of the GNU General Public License as published by
% the Free Software Foundation; either version 2, or (at your option)
% any later version.
%
% This program is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with this program; if not, write to the Free Software
% Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
% 02111-1307, USA.

