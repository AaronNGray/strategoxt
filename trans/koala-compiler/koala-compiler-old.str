\literate[koala-compiler]

 KoalaTools -- Tool suite for Koala component model

 Author: Merijn de Jonge (mdejonge@cs.uu.nl)

 $Id: bundlegen.str 5017 2004-01-27 11:53:48Z merijn $

\begin{code}
module koala-compiler

imports koala-access lib koala-relations CompositeSystem

strategies
koala-compiler =
   iowrap(Koala-compiler)

Koala-compiler =
   Koala-relations;
   where(path-finder => references );
   where(component-finder => ingredients);
   <composer>(ingredients, references)


composer: (ingredients, references) -> Composition(composition)
where
   !ingredients;
   map( {path, cname, varname, location:
      ?(path, cname, varname, location);
      !Component(Name(cname),Name(varname),
        Path(PathName(path)), 
        <[];!None <+ !Some(Url(<un-double-quote>location))>location,
        <collect( {link, name, interface: 
          ?(path, link,name, interface); !Wire(Interface(interface), Name(name),Target(PathName(link)))
         })>references
       )
   }) => composition

path-finder = 
   where(filter(connection(id,id,id)) => wires);
   filter(requiredInterface(id,id,id));
   map(Path-finder(!wires))

// transitively find endpoint of an Interface/Interface  connection
Path-finder(wires): interface -> path
   where
   interface(?source,id,?varname);
   rec x ( {src, target:
      ?src;
      wires;
      fetch-elem(IIConnection(?src, ?target,id));
      <x>target
   <+
      interface(?target, ?interf, id);!(source, target, varname, interf)
   }) => path

   
component-finder =
   filter({path,cname,varname, specials, location:
      ContainedComponent( ?path, ?cname, ?specials, ?varname );
      <oncetd(Filespecial(?location)) <+ ![] => location>specials;
      !(<conc>(path, [varname]), cname, varname, location)})

\end{code}
% Copyright (C) 2004 Merijn de Jonge <mdejonge@cs.uu.nl>
%
% This program is free software; you can redistribute it and/or modify
% it under the terms of the GNU General Public License as published by
% the Free Software Foundation; either version 2, or (at your option)
% any later version.
%
% This program is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with this program; if not, write to the Free Software
% Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
% 02111-1307, USA.

