\literate[koala-pnet-composer]

 KoalaTools -- Tool suite for Koala component model

 Author: Merijn de Jonge (mdejonge@cs.uu.nl)

 $Id: bundlegen.str 5017 2004-01-27 11:53:48Z merijn $

\begin{code}
module koala-pnet-composer
imports lib CompositeSystem  xml-tools pnet-tools

strategies
koala-pnet-composer = 
   iowrap(Koala-pnet-composer)

short-description(p) =
   !["Usage: ", <p>(), " -i file [options]"]

long-description(p) =
   ![<p>(), " makes a composition of Petri Nets in PNML format according ",
    "to a composition generated by koala-compiler from a Koala ",
    "specification.\n\n",
    "See 'koala-compiler'."]

Koala-pnet-composer = 
   Composition(?comps);
   !comps;
   filter( Module(id,id,id,id));
   where(
     map( \Module(Name(name), Path(PathName(path)), Some(Url(url)),_) -> 
        (path,<concat-strings>[url, "/", name, ".pnml.xml.af"] )\ )
   => pnet-files;
   <concat-pnet-files>pnet-files => pnet
   );
   map( {edges: \
     Module(Name(name),Path(PathName(path)),_,links) -> edges 
     where
       collect( {s1,s2,n1,n2:\  
          Wire(Interface(intf),_,Target(PathName(target))) ->
            <build-arc>(n1, n2)
          where
            <concat;concat-strings>[path,["_",intf]] => s1;
            <id-lookup(?s1)>pnet => n1;
            <concat;concat-strings>[target,["_",intf]] => s2;
            <id-lookup(?s2)>pnet => n2


                            \ }) => edges 
   \ } ); flatten-list => edges;
   <concat;build-graph-content>[edges,pnet];
   desugar-fix

concat-pnet-files = 
   map( {pnet:\
   (path, file-name) -> pnet
   where
   !file-name;
   ReadFromFile;
   ensugar-fix;
   topdown(
      try(
         Attribute(QName(None,"id" + "source" + "target"),
            \s  -> <concat;concat-strings>[path,["_",s]] \ )
      ));
   topdown(
      try({s:
         ?%>
         <name>
           <value>
           <% ?s ::cdata %>
           </value>
         </name>
         <% ;
         !%>
         <name>
           <value>
           <% <concat;concat-strings>[path,["_",s]]  ::cdata %>
           </value>
         </name>
         <% 


      }))
   => pnet
    \ });
    collect-graph-content

\end{code}
% Copyright (C) 2004 Merijn de Jonge <mdejonge@cs.uu.nl>
%
% This program is free software; you can redistribute it and/or modify
% it under the terms of the GNU General Public License as published by
% the Free Software Foundation; either version 2, or (at your option)
% any later version.
%
% This program is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with this program; if not, write to the Free Software
% Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
% 02111-1307, USA.

