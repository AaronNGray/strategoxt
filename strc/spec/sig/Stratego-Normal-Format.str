module Stratego-Normal-Format
imports
  Stratego-Amb
  Stratego-MetaTransition
  lib

strategies

  main =
    io-wrap(
      stratego-specification
    ; if <get-config> "amb-error" then
          <exit> 1
      end
    )

  stratego-specification =
    Specification([
      id,
      Strategies(list(strategy-definition))
    ])
    <+ amb-error
    <+ debug(!"warning: not a stratego-specification: ")

  strategy-definition =
      ?SDefT(name, _, _, _)
      ; {| CurrentSDef :
          rules(CurrentSDef : () -> name)
        ; SDefT(
            is-string + Mod(is-string,is-string)
          , list(VarDec(is-string,id))
	  , list(VarDec(is-string,id))
	  , strategy-expression
          )
        |}
    + ExtSDefInl(is-string + Mod(is-string,is-string)
	    , list(VarDec(is-string,id))
	    , list(VarDec(is-string,id))
	    , strategy-expression)
    + ExtSDef(is-string + Mod(is-string,is-string)
	    , list(VarDec(is-string,id))
	    , list(VarDec(is-string,id)))
    <+ amb-error
    <+ debug(!"warning: not a strategy-definition: ")

  strategy-expression =
  rec exp(
	Id
	+ Fail
	+ Not(exp)
	+ Test(exp)
	+ Seq(exp, exp)
	+ Choice(exp, exp)
	+ LChoice(exp, exp)
	+ GuardedLChoice(exp, exp, exp)
	+ GChoice(exp, exp)
	+ LGChoice(exp, exp) 

	+ CallT(SVar(is-string + Mod(is-string,is-string)), 
		list(exp), 
		list(term-expression))    

	+ Rec(is-string, exp)
	+ Path(is-int, exp)
	+ Cong(is-string, list(exp))
	+ One(exp)
	+ Some(exp)
	+ All(exp)
	+ Thread(exp)
	+ Match(term-expression)
	+ Build(term-expression)
	+ Scope(list(is-string), exp)
	+ Where(exp)
	+ PrimT(is-string,list(exp),list(term-expression))

	+ Let(list(SDefT(is-string,
		         list(VarDec(is-string, FunType(list(id),id))),
			 list(VarDec(is-string, ConstType(id))),
			 exp))
	     ,exp)

        <+ amb-error
	<+ debug(!"warning: not a strategy-expression: ")
	)

  term-expression =
  rec trm(
	Wld 
	+ Var(is-string)
	+ Int(is-int)
	+ Real(is-real)
	+ Str(is-string)
	+ BuildDefault(trm)
	+ As(Var(is-string), trm)
	+ Op(is-string, list(trm))
        <+ amb-error
	<+ debug(!"warning: not a term-expression: ")
	)

  amb-error =
    amb(id)
    ; where(
        if <CurrentSDef; is-string> () => sdef then
          say(<concat-strings> ["error: ambiguity in the definition of ", sdef, ". Alternatives: "])
        else
          say(<concat-strings> ["error: ambiguity in specification. Alternatives: "])
        end
      ; ?amb(<id>)
      ; map(debug(!"    - "))
      ; <set-config> ("amb-error", 1)
      )

signature
  constructors
    amb: List(a) -> a