\literate[{\btt Stratego-Canonical-Format}]

\begin{code}
module Stratego-Canonical-Format
imports Stratego-Amb lib automaton Stratego-MetaTransition
signature
  constructors
    MatchOp : String * List(String) -> Term

strategies

  main = io-wrap(stratego-specification)

  stratego-specification =
    Specification([
      id,
      Strategies(list(strategy-definition))
    ])
    <+ log(|Error(),"Not a stratego-specification: ",<id>) 

  strategy-definition =
    SDef(is-string, list(VarDec(is-string,id)), strategy-expression)
    <+ SDefT(is-string, list(VarDec(is-string,id))
		      , list(VarDec(is-string,id)), strategy-expression)
    <+ ExtSDefInl(is-string, list(VarDec(is-string,id))
		      , list(VarDec(is-string,id)), strategy-expression)
    <+ ExtSDef(is-string, list(VarDec(is-string,id))
		        , list(VarDec(is-string,id)))
    <+ log(|Error(),"Not a strategy-definition: ",<id>)

  strategy-expression =
  rec exp(
	Id 
	+ Fail
	+ Not(exp)
	+ Test(exp)
	+ Seq(exp, exp)
	+ Choice(exp, exp)
	+ LChoice(exp, exp)
	+ GuardedLChoice(exp, exp, exp)
	+ GChoice(exp, exp)
	+ LGChoice(exp, exp)
	+ CallT(SVar(is-string),list(CallT(SVar(is-string),[],[])),list(Var(is-string)))
	+ Rec(is-string, exp)
	+ Cong(is-string, list(exp))
	+ One(exp)
	+ Some(exp)
	+ All(exp)
	+ Thread(exp)
	+ Match(match-term-exp)
	+ Build(term-expression)
	+ Scope(list(is-string), exp)
	+ Where(exp)
	+ PrimT(is-string,list(CallT(SVar(is-string),[],[])),list(Var(is-string)))
	+ Let(list(SDef(is-string,list(VarDec(is-string,id)),exp)
		   + SDefT(is-string,list(VarDec(is-string,id))
				    ,list(VarDec(is-string,id)),exp)),exp)

	<+ log(|Error(),"Not a strategy-expression: ",<id>)
	)

  case-alternative(exp) =
        Alt(Fun(is-string,is-int) + Str(is-string) + Int(is-int) + Real(is-real)
           , list(Var(is-string)),exp)
	<+ log(|Error(),"Not a case-alternative: ",<id>)

  match-term-exp =
  rec trm(
	Wld 
	+ Var(is-string)
	+ Int(is-int)
	+ Real(is-real)
	+ Str(is-string)
	+ BuildDefault(trm)
	+ As(Var(is-string), trm)
	+ Op(is-string, list(trm))
	<+ log(|Error(),"Not a match-term-expression: ",<id>)
  )

  term-expression =
  rec trm(
	Wld 
	+ Var(is-string)
	+ Int(is-int)
	+ Real(is-real)
	+ Str(is-string)
	+ BuildDefault(trm)
	+ As(Var(is-string), trm)
	+ Op(is-string, list(trm))
	<+ log(|Error(),"Not a term-expression: ",<id>)
	)
\end{code}
