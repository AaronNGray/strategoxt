module defs-to-external-defs
imports lib Stratego
strategies

  defs-to-external-defs-io =
    io-wrap(dted-options, defs-to-external-defs)

  dted-options =
    Option("--no-inlining",
	where(<set-config>("--no-inlining", ())),
	!"--no-inlining: do not include definition bodies")
  
  defs-to-external-defs =
    Specification([id, Strategies(map(try(DefToExtDefInl <+ DefToExtDef)))])

  DefToExtDef :
    SDefT(f, a1*, a2*, s) -> ExtSDef(<unmangle>f, a1*, a2*)

  DefToExtDefInl :
    SDefT(f, a1*, a2*, s) -> ExtSDefInl(<unmangle>f, a1*, a2*, <topdown(try(UnmangleSVar))> s)
    where <inlinable> s

  inlinable = 
    where(not(<get-config>"--no-inlining"))

  UnmangleSVar :
    SVar(x) -> SVar(<unmangle>x)

  unmangle =
    explode-string
    ; at-suffix(['_' | match-digits(['_' | match-digits([]) ])]; ![])
    ; implode-string
    ; uncify

  match-digits(s) =
    [is-num | match-digits(s) <+ s]



