/**
 * @author  Martin Bravenboer
 */
module err-trace
imports options xtc-lib

signature
  constructors
    ErrTrace : List(TraceItem) -> err-trace
    Current  : a -> TraceItem
    Err      : a -> TraceItem
  
strategies

  err-output-option =
    ArgOption("--err"
    , where(<set-config>("--err-output", <id>))
    , !"--err file       if an error occurs, output error to trace in file"
    )

  /**
   * @type  _ ->? String
   */
  get-err-output-option  = <get-config> "--err-output"

  /**
   * @type _ -> List(String)
   */
  pass-err-output-option =
    !["--err", <get-err-output-option>] <+ ![]

strategies

  /**
   * @type  Error -> ()
   */
  trace-err =
    <push-trace-item> Err(<id>) <+ debug

  trace-current(|t) =
    where(<trace-current> t)

  trace-current =
    <push-trace-item> Current(<id>) <+ id

  push-trace-item =
      ?item
    ; get-err-trace
    ; \ ErrTrace(items) -> ErrTrace([item | items]) \
    ; write-err-trace
    ; !()

  print-err-trace =
      get-err-trace
    ; write-to
    ; xtc-debug(!"err-trace")

strategies

  /**
   * @type  _ -> ErrorTrace
   */
  get-err-trace =
       get-err-output-option
     ; where(file-exists)
     ; ReadFromFile
     ; ?ErrTrace(_)
    <+ !ErrTrace([])

  /**
   * @type  ErrorTrace -> ()
   */
  write-err-trace =
    <WriteToTextFile> ( <get-err-output-option> (), <id>)
    ; !()

