module xml-interpret
imports xtc-lib xml-xtc-tools options regular-xtc-tools parse-xml-info


strategies

  main-xml-interpret =
    xtc-io-wrap(xml-interpret-option, 
      if-verbose2(where(<debug> "  --> Parsing xml input"))
    ; (use-jaxp < parse-jaxp + xtc-parse-xml-info)
    ; if-verbose2(where(<debug> "  --> Interpreting document against schema"))
    ; xtc-xml-info2irhg(|<input-schema-to-rhg-nf>)
    ; if-verbose2(where(<debug> "  --> Rewriting regular hedge grammar instance to regular tree grammar instance"))
    ; xtc-irhg2irtg
    ; if-verbose2(where(<debug> "  --> Imploding regular tree grammar instance"))
    ; xtc-implode-irtg
    )

  xml-interpret-option =
      ArgOption("--dtd"
      , where(<set-config> ("--dtd", FILE(<id>)))
      , !"--dtd file       Interpret against dtd in file")
    + ArgOption("--sig"
      , where(<set-config> ("--sig", FILE(<id>)))
      , !"--sig file       Interpret against Stratego sigature in file")
    + ArgOption("--rhg"
      , where(<set-config> ("--rhg", FILE(<id>)))
      , !"--rhg file       Interpret against rhg in file")
    + ArgOption("--rhg-nf"
      , where(<set-config> ("--rhg-nf", FILE(<id>)))
      , !"--rhg-nf file      Interpret against normalized rhg in file")
    + parse-xml-options



  input-schema-to-rhg-nf =
      (   <get-config> "--rhg-nf"
       <+ <get-config> "--rhg"; xtc-rhg-front-end
       <+ <get-config> "--dtd"; xtc-parse-dtd; xtc-adtd2arhg
       <+ <get-config> "--sig"; <fatal-error> ["** ERROR: signature support is disabled."]    // xtc-str2astratego; xtc-astratego2artg
       <+ <fatal-error> ["** ERROR: no schema specified."]
      )
    ; ?FILE(<id>)