module data2xml-doc
imports xml-doc xml-info2xml-doc
imports liblib

strategies

  main-data2xml-doc =
    io-wrap(explicit-option,
      if <get-config> "--explicit" then
        explicit-data2xml
      else
        if <get-config> "--very-explicit" then
          very-explicit-data2xml
        else
          implicit-data2xml
        end
      end
    )

  explicit-option =
    Option("--explicit"
    , <set-config> ("--explicit", ())
    , "--explicit       Use explicit XML elements for strings, lists, integers, and tuples"
    )
  + Option("--very-explicit"
    , <set-config> ("--very-explicit", ())
    , "--very-explicit  Use explicit XML elements for applications, strings, lists, integers, and tuples"
    )

strategies

  very-explicit-data2xml =
    explicit-data2xml(
      rec x(
         ExplicitExplode-String
      <+ ExplicitExplode-Int
      <+ ExplicitExplode-List
      <+ ExplicitExplode-Tuple
      <+ ExplicitExplode-Appl(x)
      )
    )

  explicit-data2xml =
    explicit-data2xml(
         ExplicitExplode-String
      <+ ExplicitExplode-Int
      <+ ExplicitExplode-List
      <+ ExplicitExplode-Tuple
      <+ Explode-Appl
    )

  implicit-data2xml =
    data2xml(
       Explode-String
    <+ Explode-Int
    <+ Explode-List
    <+ Explode-Tuple
    <+ Explode-Appl
    )

  data2xml =
    implicit-data2xml

  data2xml(s) =
    bottomup(
        try(
            \ None() -> [] \
         <+ ?Some(<id>)
         <+ s
        )
      )
    ; data2xml-finish

  explicit-data2xml(s) =
    bottomup(try(s))
    ; data2xml-finish

  data2xml-finish =
    bottomup(try( \ Conc(l1, l2) -> <conc> (l1, l2) \ ))
    ; !%>
        <?xml version="1.0"?>

        <% id %>
      <%

strategies

  Explode-Appl =
    Explode-EmptyAppl <+ Explode-NotEmptyAppl

  Explode-EmptyAppl :
    s#([]){atts*} -> %>< ~n:s <@ <map(Explode-Attr)> atts* :: * @> /><%

  Explode-NotEmptyAppl :
    s#(children){atts*} ->
      %>< ~n:s <@ !atts2 :: * @> ><% <flatten-list> children :: * %></ ~n:s ><%
      where <map(Explode-Attr)> atts* => atts2

  Explode-Attr :
    (s, v) -> @> ~n:s="<% <string2xml-attr-value> v %>" <@

  Explode-String :
    s -> %><% !s2 :: cdata %><%
      where <is-string; xml-escape> s => s2

  Explode-List :
    xs -> %><% !xs :: * %><% :: *
    where
      <is-list> xs

  Explode-Tuple :
    "" # (xs) -> %><% !xs :: * %><% :: *

  Explode-Int :
    i ->  %><% !s :: cdata %><%
    where
      <is-int> i
      ; <int-to-string> i => s

strategies 

  ExplicitExplode-Appl(x) :
    s#(children){atts*} ->
      %><appl cons="<% !s %>"><% <map(ExplicitExplode-Attr(x))> atts* :: * %> <% <flatten-list> children :: * %></appl><%

  ExplicitExplode-Attr(x) =
    !%><anno><% bottomup(x) %></anno><%

  ExplicitExplode-List =
    !%><list><% Explode-List :: * %></list><%

  ExplicitExplode-Tuple =
    !%><tuple><% Explode-Tuple :: * %></tuple><%

  ExplicitExplode-Int =
    !%><int><% Explode-Int %></int><%

  ExplicitExplode-String =
    !%><string><% Explode-String %></string><%
