module xml-info2data
imports xml-info liblib

strategies

  main-xml-info2data =
    io-wrap(explicit-option,
      if <get-config> "--explicit" then
        explicit-xml-info2data
      else
        implicit-xml-info2data
      end
    )

  explicit-option =
    Option("--explicit"
    , <set-config> ("--explicit", ())
    , "--explicit       Use explicit XML elements for strings, lists, integers, and tuples"
    )

  explicit-xml-info2data =
      ?Document(<id>)
    ; topdown-wannos(repeat(ExplicitImplode <+ Implode))

  xml-info2data =
    implicit-xml-info2data

  implicit-xml-info2data =
      ?Document(<id>)
    ; topdown-wannos(repeat(Implode))

rules

  Implode:
    Element(Name(_, s), [atts*], children) -> s#(children){atts*}

  Implode:
    Text(s) -> s

  Implode:
    Attribute(Name(_, s), value) -> (s, value)

rules

  ExplicitImplode :
    Element(Name(_, "int"), _, [Text(s)]) -> <trim-chars('\n' + '\t' + '\r' + ' '); string-to-int> s

  ExplicitImplode :
    Element(Name(_, "string"), _, [Text(s)]) -> s

  ExplicitImplode :
    Element(Name(_, "tuple"), _, children) -> ""#(children)

  ExplicitImplode :
    Element(Name(_, "list"), _, children) -> children

strategies

  topdown-wannos(s) =
    rec x(
      topdown(s; id{map(x)} )
    )
