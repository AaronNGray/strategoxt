module ppgen
imports xtc-lib stratego-xt-xtc-tools options

signature
  constructors
    PlainText : InputType
    ParseTree : InputType
    AST       : InputType

strategies
  
  io-ppgen=
    option-wrap(ppgen-options, xtc-io(ppgen))

  ppgen = 
      <pre-process-sdf> (<get-input-type>, <id>)
    ; xtc-transform(!"sdf-2-pp-table", pass-verbose)
    ; xtc-transform(!"pp-pp-table", pass-verbose)

  pre-process-sdf : (PlainText(), f) -> <xtc-parse-sdf> f
  pre-process-sdf : (ParseTree(), f) -> <xtc-implode-asfix> f
  pre-process-sdf : (AST()      , f) -> f

  get-input-type =
    <get-config> "--input-type"  <+ !PlainText()

  ppgen-options =
      Option("-A"
      , where(<set-config> ("--input-type", ParseTree()))
      ,	!"-A          Input is parse tree."
      )
    + Option("-a"
      , where(<set-config> ("--input-type", AST()))
      ,	!"-a          Input is abstract syntax tree."
      )
    + io-options

/*
usage:
  ppgen [-A] [-a] [-h] [-i grammar] [-o file] [-v]

where
  -A          Input is parse tree.
  -a          Input is abstract syntax tree.
  -h          Displays usage information.
  -i grammars Specifies SDF module or definition for whihc to generate a
              pretty-print table.
  -o file     Specifies the name of the resulting pretty-print table.
  -S          Run silently.
  -v          Run verbosely.
*/
