/**
 * sdf-desugar implements the following desugarings:
 *
 *   - remove double quotes in String literals: cons, lit
 *   - replace no-attrs by attrs([])
 *
 * @author Martin Bravenboer <martin@cs.uu.nl>
 */
module sdf-desugar
imports Sdf2 libstratego-lib implode-aterm
strategies

  main-sdf-desugar =
    io-wrap(
      topdown(repeat(Desugar); try(DesugarOnce))
    )

  Desugar :
    no-attrs() -> attrs([])

  /**
   * FIXME SDF: qlit is an obsolete SDF constructor name
   */
  Desugar :
    lit(qlit(s)) -> lit(<un-double-quote> s)

  Desugar :
    lit(quoted(s)) -> lit(<un-double-quote> s)

  /**
   * FIXME SDF: uqlit is an obsolete SDF constructor name
   */
  Desugar :
    lit(uqlit(s)) -> lit(s)

  Desugar :
    lit(unquoted(s)) -> lit(s)

  Desugar :
    term(default(t)) -> term(t)

rules

  DesugarOnce =
    term(implode-aterm)
