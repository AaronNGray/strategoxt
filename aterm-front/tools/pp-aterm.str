module pp-aterm
imports
  libstratego-lib
  libstratego-gpp
  pp-aterm-options
  ugly-print

strategies

  io-pp-aterm =
    io-stream-wrap(
      max-term-size-option
      + max-depth-option
      + unescaped-strings-option     
    , system-usage
    , system-about
    , pp-aterm 
    )
    
  pp-aterm =
    ?(<read-from-stream>, fout)
    ; pp-aterm2box
    ; box2text-stream(|80, fout)
    ; <fputs> ("\n", fout)
    
  pp-aterm2box =
      if get-max-depth-config => depth then
        at-depth(!depth, !"...")
      end
    ; if not(get-unescaped-strings-config) then
        escape-strings
      end
    ; ugly-print
    
strategies 

  /**
   *  Escaping special characters in strings
   *  model after 
   *  'static int symbolTextSize(Symbol sym)'
   *  in aterm.c (aterm-2.3.1)
   * 
   */
  escape-strings =
    topdown(try(escape-aterm-strings))

  escape-aterm-strings :
    s1{t*} -> s2{t*}
    where
      !s1
      ; string-as-chars(
          escape-chars(
            Escape-backslash
          + Escape-double-quote
          + Escape-linefeed 
          + \ ['\t' | cs ] -> ['\', 't'  | cs ] \
          + Escape-carriage-return
          )
        )
      ; ?s2
