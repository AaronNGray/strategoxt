/**
 * metavar/1 : meta variable
 * lquote/1  : quotation symbol
 * rquote/1  : quotation symbol
 */
module pp-stratego-latex-alltt
imports options parse-cs-lib asfix

strategies

  io-pp-stratego-latex-alltt =
    xtc-io-wrap(syntax-option, pp-stratego-latex-alltt)

  pp-stratego-latex-alltt =
      pcs-input-file
    ; init-language-binding
    ; pcs-parse-asfix2me
    ; xtc-io-transform(to-latex-alltt)
    ; xtc-asfix2me-yield

  xtc-asfix2me-yield =
    xtc-transform(!"unparsePT")

  to-latex-alltt =
    topdown(try(CheerUp))

strategies

  /**
   * Meta variable
   */
  CheerUp =
    appl(prod([varsym(id)], id, id), map(try(FancyVar)))

  /**
   * Quotation to concrete object syntax
   */
  CheerUp =
      check-cons-of-appl(is-Quote)
    ; appl(id, map(try(FancyQuote)))

  /**
   * Dynamic Rule Scope
   */
  CheerUp =
      check-cons-of-appl(?"DynRuleScope")
    ; appl(id, map(try(FancyDynRuleScope)))

rules

  FancyVar :
    lit(s) -> lit(<concat-strings> ["\\metavar{", s, "}"])

  FancyQuote :
    lit("|[") -> lit("\\lquote{|[}")

  FancyQuote :
    lit("]|") -> lit("\\rquote{]|}")

  FancyDynRuleScope :
    lit("{|") -> lit("\\ldrscope{}")

  FancyDynRuleScope :
    lit("|}") -> lit("\\rdrscope{}")

strategies

  is-Quote =
      "ToTerm"
    + "ToStrategy"
    + "ToBuild"

  is-AntiQuote =
      "FromTerm"
    + "FromApp"
    + "FromStrategy"

  check-cons-of-appl(cons-check) = 
    where(
      ?appl(prod(_, _, attrs(<id>)), _)
    ; fetch-elem(?term(cons(<id>)))
    ; cons-check
    )
