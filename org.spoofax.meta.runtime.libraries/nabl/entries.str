module nabl/entries

imports
  
  nabl/collect
  nabl/utils
  nabl/uri
  index/core
  editor/origins
  
signature

  constructors
    
    Def   : URI                    -> Entry
    Alias : URI * URI              -> Entry
    Use   : URI                    -> Entry
 
rules // Index entries to diff
  
  analyze-diff-entry = ?Def(_)
 
rules
  
  new-def(|partition, uri):
    x -> definition
    with
      definition := <origin-track-forced(!Def(uri))> x;
      <index-add(|partition)> definition;
			<new-alias(|partition, <nabl-nonunique-uri> uri, uri)> x
      
  new-alias(|partition, uri, alias-uri):
    x -> alias
    with
      alias := <origin-track-forced(!Alias(uri, alias-uri))> x;
      if <not(eq)> (uri, alias-uri) then
      	<index-add(|partition)> alias
      end
      
  new-use(|partition, uri):
    x -> use
    with
      use := <origin-track-forced(!Use(uri))> x;
      <index-add(|partition)> use

rules // index uri & value projections
  
  /** @internal */
  nabl-uri-impl:
    Def(uri) -> uri
    
  /** @internal */
  nabl-uri-impl:
    Alias(uri, _) -> uri
    
  /** @internal */  
  nabl-uri-impl:
    Use(uri) -> <nabl-is-uri> uri
        
    
  /** @internal */
  nabl-value-impl:
    Def(value) -> value
    
  /** @internal */
  nabl-value-impl:
    Alias(_, value) -> value

  /** @internal */
  nabl-value-impl:
    Use(value) -> value
    
