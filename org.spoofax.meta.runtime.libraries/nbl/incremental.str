module nbl/incremental

imports
	
	index/core
	index/query
	nbl/collect
	nbl/query
	nbl/entries
	nbl/uri

signature

  constructors
    
    Read    : URI * Int -> Entry
    ReadInv : Int * URI -> Entry
      
rules // Construction
	
	nabl-new-reads(|nr):
		uri -> [Read(uri, nr), ReadInv(nr, uri)]

rules // Index
	
	nabl-entry-olds:
		partition -> <index-get-all-in-partition; filter(nabl-diff-entry)> partition
			
	nabl-entry-clear:
		partition -> <index-clear-partition> partition
      
rules // Entry diff
	
  nabl-entry-diff:
    (old, new) -> (added, removed)
    with
      added   := <diff(nabl-diff-eq)> (new, old);
      removed := <diff(nabl-diff-eq)> (old, new)

  nabl-diff-entry =
    ?Def(_)
    
  nabl-diff-entry =
    ?Prop(_, _, _)
      
  nabl-diff-eq:
    (Def(uri1), Def(uri2)) -> <id>
    where
      <nabl-uri-eq> (uri1, uri2)
      
  nabl-diff-eq:
    (Prop(uri1, kind1, value1), Prop(uri2, kind2, value2)) -> <id>
    where
      <nabl-uri-eq> (uri1, uri2);
      <eq> (kind1, kind2);
      <eq> (value1, value2)

rules // Incremental
	
  nabl-entry-incremental:
    (old, new) -> [added*, removed*]
    with
      (added*, removed*) := <nabl-entry-diff> (old, new)

	nabl-get-reads:
		<with(uri := <nabl-uri> | "Could not extract URI from given term.")> -> [read*, fail-read*]
		with
			read*      := <nabl-get-all-values> Read(uri, ());
			if fail-uri := <nabl-replace-uri-qualifier(|())> uri then
				fail-read* := <nabl-get-all-values> Read(fail-uri, ())
			else
				fail-read* := []
			end
		
	nabl-remove-read:
		nr -> <id>
		with
			uri* := <nabl-get-all-values> ReadInv(nr, ());
			<index-remove-all> ReadInv(nr, ());
			// Use index-remove-one because only Reads with value (nr) should be removed.
			<map(!Read(<id>, nr); index-remove-one)> uri*;
			<filter(!Read(<nabl-replace-uri-qualifier(|())>, nr); index-remove-one)> uri*
      
rules /** @internal URI & value projections */
  
  /** @internal */  
  nabl-uri-impl:
    Read(uri, _) -> uri

  /** @internal */  
  nabl-value-impl:
    Read(_, value) -> value
    
  /** @internal */  
  nabl-uri-impl:
    ReadInv(uri, _) -> uri

  /** @internal */  
  nabl-value-impl:
    ReadInv(_, value) -> value
    