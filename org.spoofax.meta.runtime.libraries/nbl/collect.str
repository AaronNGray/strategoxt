module nbl/collect

imports
	
	task/core
	nbl/tasks
	nbl/entries
	nbl/interface
	nbl/uri
	nbl/utils
	
signature 

	sorts
	
		State DefScope ImplicitDef Use RefScope Range

  constructors // state
  	
  	State : String * Term -> State

	constructors // def

		Current    :            						DefScope
		Subsequent :            						DefScope
		DefScope   : Namespace * Segment -> DefScope

  constructors // implicit def
    
    IDefCandidate : Namespace * Name * Unique * List(Property) -> ImplicitDef
  
  constructors // use
      
		UseCandidate  : Namespace * List(Property) * RefScope * Range        -> Use
		UseCandidate  : Namespace * Name * List(Property) * RefScope * Range -> Use
	
		Current     :                                     							RefScope
		Surrounding :                                  	 							  RefScope
		Context     : String * Namespace * List(Property) * RefScope -> RefScope
		
    All : Range
    One : Range
	
	constructors // import
	  
	  Imported : Namespace                   -> Namespace
	  Import   : Language * Namespace        -> Property
	  Import   : Language * Namespace * Name -> Property
  	Type     : Property
  	
  constructors // property
    
    Prop : Property * Value -> Prop
	
strategies // generic traversal
	
	/**
	 * Main strategy to collect all index elements and task* from an AST.
	 */
	nabl-collect(|lang):
	  ast -> (ast', <iset-elements> elem*, <iset-elements> task*)
	  where
	    new-iset => elem*;
	    new-iset => task*;
	    ast' := <nabl-collect(id, id|lang, elem*, task*, [], [])> ast
	
	/**
	 * Performs the collection in a topdown traversal. 
	 * At each node, it tries various calls to generated strategies, which call back to generic strategies.
	 */
	nabl-collect(sibling-uris, deps'|lang, elem*, task*, uri*, dep*) =
		nabl-state-pop(?state*);
		
		nabl-scope-site(?uri'*|lang, uri*, state*); // scopes of external definitions, e.g. variables in let expressions
		nabl-def-collect(?child-uri*, sibling-uris|lang, elem*, task*, uri'*, state*);
		nabl-import-dependency-annotate;
		nabl-import-dependency-collect(deps'|dep*);
		nabl-state-collect;
		
		preserve-annos(force-origins(id#(nabl-siblings(|lang, elem*, task*, child-uri*, dep*)))); // visit children from left to right
		
		nabl-use-collect(|lang, elem*, task*, uri'*, dep*, state*);
    nabl-import-collect(|lang, elem*, task*, child-uri*, state*);
		nabl-prop-collect(|lang, elem*, task*, uri'*, state*);
    nabl-implicit-def-collect(|lang, elem*, task*, child-uri*, state*)
	
	nabl-siblings(|lang, elem*, task*, uri*, dep*) = 
		[]
	
	nabl-siblings(|lang, elem*, task*, uri*, dep*) =
		[nabl-collect(?sibling-uri*, ?deps'|lang, elem*, task*, uri*, dep*)|nabl-siblings(|lang, elem*, task*, sibling-uri*, deps')]

rules // generic analysis of state
  
  nabl-state-collect = try(nabl-state-site)
  
  nabl-state(pattern|state):
    term -> <map(try(nabl-state-annotate(pattern|state)))> term
      
  nabl-state-annotate(pattern|state) =
    ?pattern; add-annotation(|state)
    
  nabl-state-pop(states'):
  	term{anno*} -> term'
  	where
  		state* := <collect-all(?State(_, _))> anno*;
  		term'  := <remove-annotations(?State(_, _))> term;
  		match(states'|state*)
  		
  nabl-get-state(|name) =
  	fetch-elem(?State(name, _))

rules // generic analysis of scopes
	
	nabl-scope-site(child-uris|lang, uri*, state*):
		name{anno*} -> result
		where
			if (anno1*, anno2*) := <split-fetch(?DefScope(ns, segment) => scope )> anno* then
				segment' := <nabl-external-segment> segment;
				uri'*   := <update-scope-uri(nabl-extend-uri(|segment')|lang)> (ns, uri*);
				// TODO: fix empty annotation list
				result   := <nabl-scope-site(child-uris|lang, uri'*, state*)> name{anno1*, anno2*}
			else
				match(child-uris|uri*);
				result := name{anno*}
			end
	
	nabl-def-scope = !DefScope(<id>, <nabl-anonymous-segment>)
	
	nabl-def-scope(|scope): name{anno*} -> name{scope, anno*}
		
	// call back for generic analysis of anonymous scopes
	nabl-anonymous-scope(child-uris|lang, uri*, ns*) =
		match(child-uris|<extend-scope-uri(|lang, <nabl-anonymous-segment>)> (ns*, uri*))	
	
rules // generic analysis of defs
	
	nabl-def-collect(child-uris, sibling-uris|lang, elem*, task*, uri*, state*) =
		nabl-def-site(child-uris, sibling-uris|lang, elem*, task*, uri*, state*) <+ 
		(
			match(child-uris|uri*);
			match(sibling-uris|uri*)
		)
	
	// call backs to generic analysis
	
	// TODO: can the alias storage be handled by new-def
	// definition site in current scope
	nabl-def(child-uris, sibling-uris|lang, elem*, task*, uri*, ns, unique, defscope, scoped):
		a@name{anno*} -> name{d,anno*}
		where
			defscope := Current();
			// get path for namespace of definition site
			current-uri := <lookup-uri(|lang, ns)> uri*;
			// build path for definition site
			def-uri     := <nabl-extend-uri(|ns, name, unique)> current-uri;
			// build Def entry for the index
			d           := <new-def(|elem*, def-uri)> a;
			// store non-unique alias in the index
			nabl-def-store-alias(|elem*, unique, def-uri, ns, name);
		  // preserve current uri* in subsequent scope
		  match(sibling-uris|uri*);
			// extend uri* in children for scoped namespaces
			match(child-uris|<replace-scope-uri(|def-uri)> (scoped, uri*))
		
	// TODO: make this work with lists of def scopes
	// definition site in subsequent scope and/or external	
	nabl-def(child-uris, sibling-uris|lang, elem*, task*, uri*, ns, unique, defscope, scoped):
		a@name{anno*} -> name{d, anno*}
		where
			if 
				defscope := [Subsequent()]
			then
				segment  := <nabl-subsequent-segment>;
				// get path for namespace of definition site
				current-uri := <lookup-uri(|lang, ns)> uri*
			else
				// TODO: check if we can create the segment here
				[DefScope(ns, segment)] := defscope;
				// get base path for language
				current-uri := <nabl-base-uri> lang
			end;
			// build path for definition site
			def-uri  := <nabl-extend-uri(|segment); nabl-extend-uri(|ns, name, unique)> current-uri;
			// build Def entry 
			d        := <new-def(|elem*, def-uri)> a;
			// store a non-unique alias in the index
			nabl-def-store-alias(|elem*, unique, def-uri, ns, name);
			// TODO: check if this is correct for external definitions
			match(sibling-uris|<extend-scope-uri(|lang, segment)> ([ns], uri*));
		  // preserve current uri* in children
		  match(child-uris|uri*)
		  
	nabl-def-store-alias(|elem*, unique, path, ns, name) =
	  if Unique(_) := unique then
      new-alias(|elem*, <nabl-replace-uri(|ns, name, <nabl-nonunique-qualifier>)> path, path)
    end

rules
  
  nabl-import-dependency-annotate = try(nabl-import-dependency-site)

  nabl-import-dependency:
    name -> <nabl-add-reserved(|reserved)> name
    with
      /* Reserve a task number for use as dependency for the subtree of the import and annotate the name with it. 
      The reserved number is used in nabl-import to create a Noop task that has a dependency on the resolution of the 
      import target. This makes sure that imports are resolved before other resolve tasks. */
      reserved := <reserve-task>
      
  nabl-import-dependency-collect(deps'|dep*):
    name -> name
    with
    	/* Do the actual adding of the dependency here because the reserved annotation may be added to a name that is
      in the subtree of the import name. The dependency should only be active after the annotated term. */
      if Reserved(r) := <nabl-collect-reserved> name then
        match(deps'|[r|dep*])
      else
        match(deps'|dep*)
      end
	
rules // generic analysis of uses
	
	nabl-use-collect(|lang, elem*, task*, uri*, dep*, state*) = 
		try(nabl-use-site(|lang, elem*, task*, uri*, dep*, state*))
	
	// single candidate
	nabl-use(|lang, elem*, task*, uri*, dep*, candidate):
		name -> name'
		where
			<not(is-list)> candidate;
			name' := <nabl-use-candidate(|lang, elem*, task*, uri*, dep*)> (candidate, name)
	
	// list of candidates
	nabl-use(|lang, elem*, task*, uri*, dep*, candidates):
		name -> name'
		where
			<is-list> candidates;
			name' := <foldl(nabl-use-candidate(|lang, elem*, task*, uri*, dep*))> (candidates, name)

		
	// candidate in current scope
	nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
		(UseCandidate(ns, prop*, Current(), range), name{anno*}) -> name{u, anno*}
		where
			uri      := <lookup-uri(|lang, ns)> uri*;
			propdep* := <dependent-tasks> prop*;
			result   := <new-task(|task*, [dep*, propdep*])> Resolve(uri, ns, <nabl-fix-name> name, prop*, range);
		  u        := <new-use(|elem*, result)>

  // named candidate in current scope
  nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
    (UseCandidate(ns, name, prop*, Current(), range), term{anno*}) -> term{u, anno*}
    where
      uri      := <lookup-uri(|lang, ns)> uri*;
      propdep* := <dependent-tasks> prop*;
      namedep* := <dependent-tasks> name;
      result   := <new-task(|task*, [dep*, propdep*, namedep*])> Resolve(uri, ns, <nabl-fix-name> name, prop*, range);
      u        := <new-use(|elem*, result)>
	
	
	// candidate in another scope (result of resolution)	  
	nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
		(c@UseCandidate(ns, prop*, target-uri*, range), name{anno*}) -> name{u, anno*}
		where
		  <?Result(_) <+ map(?Result(_))> target-uri*;
		  propdep* := <dependent-tasks> prop*;
		  uridep*  := <dependent-tasks> target-uri*;
			result   := <new-task(|task*, [dep*, propdep*, uridep*])> Resolve(target-uri*, ns, <nabl-fix-name> name, prop*, range);
		  u        := <new-use(|elem*, result)>
	
		  
	// candidate in context
  nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
    (UseCandidate(ns, prop*, Context(name', ns', props', refscope'), range), name{anno*}) -> name{u, anno*}
    where 
      _{uri}   := <nabl-use-candidate(|lang, elem*, task*, uri*, dep*)> (UseCandidate(ns', props', refscope', range), name'{});
      propdep* := <dependent-tasks> prop*;
      result   := <new-task(|task*, [dep*, propdep*])> Resolve(uri, ns, <nabl-fix-name> name, prop*, range);
      u        := <new-use(|elem*, result)>

rules // generic analysis of imports
  
  nabl-import-collect(|lang, elem*, task*, uri*, state*) = 
  	try(nabl-import-site(|lang, elem*, task*, uri*, state*))
  
  nabl-import(|lang, elem*, task*, uri*, imports):
    name -> name''
    where
    	Use(use) := <nabl-collect-use> name;
      name'    := <foldl(nabl-import-candidate(|lang, elem*, task*, uri*, use))> (imports, name);
      if Reserved(r) := <nabl-collect-reserved> name then
      	Result(dep) := use;
      	name''      := <remove-annotations(?Reserved(r))> name';
      	<new-task(|task*, [dep], r)> Noop()
      else
      	name'' := name'
      end
        
  nabl-import-candidate(|lang, elem*, task*, uri*, use):
    (import, name) -> name
    where
      ns       := <nabl-import-namespace> import;
      uri      := <lookup-uri(|lang, ns)> uri*;
      new-prop(|elem*, uri, import, use)
      
  nabl-import-namespace:
    Import(_, Imported(namespace)) -> namespace
      
  nabl-import-namespace:
    Import(_, namespace) -> namespace
    where
      not(Imported(_) := namespace)
      
  nabl-import-namespace:
    Import(_, Imported(namespace), _) -> namespace
      
  nabl-import-namespace:
    Import(_, namespace, _) -> namespace
    where
      not(Imported(_) := namespace)

rules // generic analysis of properties
  
  nabl-prop-collect(|lang, elem*, task*, uri*, state*) = 
  	try(nabl-prop-site(|lang, elem*, task*, uri*, state*))
  
	// call back to generic analysis
	nabl-props(|lang, elem*, task*, prop*):
	  name -> name'
	  where
	    Def(uri) := <nabl-collect-def> name;
		  name'    := <foldl(extend-prop(|elem*, uri))> (prop*, name)
		
	extend-prop(|elem*, uri):
		(Prop(type, result), name) -> name
		where
		  prop := <new-prop(|elem*, uri, type, result)> name

rules // generic analysis of implicit defs

  nabl-implicit-def-collect(|lang, elem*, task*, uri*, state*) = 
  	try(nabl-implicit-def-site(|lang, elem*, task*, uri*, state*))
 
  nabl-implicit-def(|lang, elem*, task*, uri*, candidates):
    name -> name
    where
      <map(nabl-implicit-def-candidate(|lang, elem*, task*, uri*))> candidates
      
  nabl-implicit-def-candidate(|lang, elem*, task*, uri*):
    IDefCandidate(ns, name, unique, prop*) -> <id>
    where
      // get path for namespace of definition site
      current-uri := <lookup-uri(|lang, ns)> uri*;
      // build path for definition site
      def-uri     := <nabl-extend-uri(|ns, name, unique)> current-uri;
      // build Def entry for the index
      new-def(|elem*, def-uri);
      // store non-unique alias in the index
      nabl-def-store-alias(|elem*, unique, def-uri, ns, name);
      // build Prop entries for the index
      <map(\Prop(type, result) -> <new-prop(|elem*, def-uri, type, result)>\)> prop*

rules // name retrieval
  
  nabl-name = is-string
  
  nabl-name-apply(s) = is-string; s
    
  nabl-fix-name = try(fix-completion-name); strip-annos
  
rules // URI retrieval
  
  nabl-collect-def =
    nabl-name;
    get-annos;
    collect-one(?Def(_))
    
  nabl-collect-all-def =
    nabl-name;
    get-annos;
    collect-all(?Def(_))
    
  nabl-collect-use =
    nabl-name;
    get-annos;
    collect-one(?Use(_))
    
  nabl-collect-all-use =
    nabl-name;
    get-annos;
    collect-all(?Use(_))
    
  nabl-add-reserved(|reserved) =
    nabl-name-apply(add-annotation(|reserved))
    
  nabl-collect-reserved =
    nabl-name;
    get-annos;
    collect-one(?Reserved(_))

rules // helpers
	
	replace-scope-uri(|path)         = foldl(update-scope-uri(!path|<nabl-uri-language> path))
	
	extend-scope-uri(|lang, segment) = foldl(update-scope-uri(nabl-extend-uri(|segment)|lang))
	
	update-scope-uri(update|lang):
		(key, uri*) -> result
		where
		  result := <fetch((?key, update))> uri* 
		<+ 
		  result := [(key, <nabl-base-uri; update> lang)|uri*]
			  
	lookup-uri(|lang, ns):
	  uri* -> <<lookup> (ns, uri*) <+ <nabl-base-uri> lang>
	  
