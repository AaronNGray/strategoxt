module nbl/collect

imports
	
	task/core
	nbl/tasks
	nbl/entries
	nbl/interface
	nbl/uri
	nbl/utils
	
signature 

	sorts
	
		State DefScope ImplicitDef Use RefScope Range

  constructors // state
  	
  	State : String * Term -> State

	constructors // def

		Current    :            						DefScope
		Subsequent :            						DefScope
		DefScope   : Namespace * Segment -> DefScope

  constructors // implicit def
    
    IDefCandidate : Namespace * Name * Unique * List(Property) -> ImplicitDef
  
  constructors // use
      
		UseCandidate  : Namespace * List(Property) * RefScope * Range        -> Use
		UseCandidate  : Namespace * Name * List(Property) * RefScope * Range -> Use
	
		Current     :                                     							RefScope
		Surrounding :                                  	 							  RefScope
		Context     : String * Namespace * List(Property) * RefScope -> RefScope
		
    All : Range
    One : Range
	
	constructors // import
	  
	  Imported : Namespace                   -> Namespace
	  Import   : Language * Namespace        -> Property
	  Import   : Language * Namespace * Name -> Property
  	Type     : Property
  	
  constructors // property
    
    Prop : Property * Value -> Prop
    
  constructors // partition
  
  	CollectPartition : Partition
	
strategies // generic traversal
	
	/**
	 * Main strategy to collect all index elements and task* from an AST.
	 */
	nabl-collect(|partition, lang):
	  ast -> (ast', <iset-elements> elem*, <iset-elements> task*)
	  where
	    new-iset      => elem*;
	    new-iset      => task*;
	    new-hashtable => unique*;
	    ast' := <nabl-collect(id|lang, partition, unique*, elem*, task*, [])> ast
	  
	/**
	 * Performs the collection in a topdown traversal. 
	 * At each node, it tries various calls to generated strategies, which call back to generic strategies.
	 */
	nabl-collect(sibling-uris|lang, partition, unique*, elem*, task*, uri*) =
		nabl-state-pop(?state*);
		nabl-scope-site(?uri'*|lang, uri*); // scopes of external definitions, e.g. variables in let expressions
		
		(
		   nabl-def-site(?child-uri*, sibling-uris|lang, partition, unique*, elem*, uri'*, state*) 
    <+ match(sibling-uris|uri*)
    ;  nabl-anonymous-scope-site(?child-uri*|lang, partition, unique*, uri'*, state*)
    <+ match(?child-uri*|uri'*)
    );
    
		try(nabl-state-site);
		
		preserve-annos(force-origins(id#(nabl-siblings(|lang, partition, unique*, elem*, task*, child-uri*)))); // visit children from left to right
		
		try(nabl-use-site(|lang, elem*, task*, uri'*, state*));
    try(nabl-import-site(|lang, elem*, task*, child-uri*, state*));
    try(nabl-prop-site(|elem*, task*, state*));
    try(nabl-implicit-def-site(|lang, partition, unique*, elem*, child-uri*, state*))
	
	nabl-siblings(|lang, partition, unique*, elem*, task*, uri*) = 
		[] + 
		[ nabl-collect(?sibling-uri*|lang, partition, unique*, elem*, task*, uri*)
		| nabl-siblings(|lang, partition, unique*, elem*, task*, sibling-uri*) ]

rules // generic analysis of state
  
  nabl-state(pattern|state) =
    map(try(nabl-state-annotate(pattern|state)))
      
  nabl-state-annotate(pattern|state) =
    ?pattern; add-annotation(|state)
    
  nabl-state-pop(states'):
  	term{anno*} -> term'
  	where
  		state* := <collect-all(?State(_, _))> anno*;
  		term'  := <remove-annotations(?State(_, _))> term;
  		match(states'|state*)
  		
  nabl-get-state(|name) =
  	fetch-elem(?State(name, _))

rules // generic analysis of scopes
	
	nabl-scope-site(adapted-uris|lang, uri*) =
	  with (
	    get-annotations;
	    collect-all(?DefScope(_, _));
	    foldr(!uri*, nabl-def-scope-site(|lang));
	    match(adapted-uris|<id>)
	  );
	  remove-annotations(?DefScope(_, _))
	  
	nabl-def-scope-site(|lang):
	  (DefScope(ns, uri), uri*) -> uri'*
	  where
      segment := <nabl-uri-path; Hd; nabl-external-segment> uri;
      uri'*   := <update-scope-uri(nabl-extend-uri(|segment)|lang)> (ns, uri*)
      
	nabl-construct-def-scope(|lang, partition, unique*) = 
		!DefScope(<id>, <nabl-base-uri; nabl-extend-uri-anonymous(|unique*, partition)> lang)
	
	nabl-def-scope(|scope) = add-annotation(|scope)
		
	// call back for generic analysis of anonymous scopes
	nabl-anonymous-scope(child-uris|lang, partition, unique*, uri*, ns*) =
		match(child-uris|<extend-scope-uri(nabl-extend-uri-anonymous(|unique*, partition)|lang)> (ns*, uri*))	
	
rules // generic analysis of defs
    	
	// call back to generic analysis
	nabl-def(child-uris, sibling-uris|lang, partition, unique*, elem*, uri*, ns, unique, defscope, scoped):
		a@name{anno*} -> name{d,anno*}
		where
			// get URI for namespace of definition site
			current-uri := <lookup-uri(|lang, ns)> uri*;
			switch !defscope
			// in each case we 
			// 1. build URI of the definition site
			// 2. update URIs for children (vertical scope)
			// 3. update URIs for right siblings (horizontal scope)
			case ?Current():
				// 1. extend current URI with ID segment
				// 2. extend URIs of scoped namespaces
				// 3. preserve
				def-uri := <nabl-extend-uri(|unique*, partition, ns, name, unique)> current-uri;
				match(child-uris|<replace-scope-uri(|def-uri)> (scoped, uri*));
			  match(sibling-uris|uri*)
			case ?[Subsequent()]:
				// 1. extend current URI with Subsequent and ID segment
				// 2. preserve
				// 3. extend URI for namespace of definition site with Subsequent segment
				subs-uri := <nabl-extend-uri-subsequent(|unique*, partition)> current-uri;
				def-uri  := <nabl-extend-uri(|unique*, partition, ns, name, unique)> subs-uri;
	  		match(child-uris|uri*);
				match(sibling-uris|<replace-scope-uri(|subs-uri)> ([ns], uri*))
	  	case ?[DefScope(ns, uri)]:
	  		// 1. extend base URI with External and ID segment
				// 2. preserve
				// 3. preserve
				def-uri := <nabl-extend-uri(|unique*, partition, ns, name, unique)> uri;
				match(child-uris|uri*);
				match(sibling-uris|uri*)
			case ?[Subsequent(), DefScope(ns, uri)]:
				// 1a. extend base URI with External and ID segment
				// 1b. extend current URI with Subsequent and ID segment
				// 1c. introduce alias from 1b to 1a
				// 2. preserve
				// 3. extend URI for namespace of definition site with Subsequent segment
				def-uri   := <nabl-extend-uri(|unique*, partition, ns, name, unique)> uri;
				subs-uri  := <nabl-extend-uri-subsequent(|unique*, partition)> current-uri;
        alias-uri := <nabl-extend-uri(|unique*, partition, ns, name, unique)> subs-uri;
	  		<new-alias(|elem*, def-uri, alias-uri)> a;
	  		match(child-uris|uri*);
				match(sibling-uris|<replace-scope-uri(|subs-uri)> ([ns], uri*))
			end;
			// add Def entry to the index
			d := <new-def(|elem*, def-uri)> a
	
rules // generic analysis of uses
	
	// single candidate
	nabl-use(|lang, elem*, task*, uri*, candidate) =
		where (<not(is-list)> candidate);
		nabl-use(|lang, elem*, task*, uri*, [candidate])
	
	// list of candidates
	nabl-use(|lang, elem*, task*, uri*, candidates):
		name{anno*} -> name{u, anno*}
		where
			subtask* := <map(nabl-use-candidate(|lang, elem*, task*, uri*, <nabl-fix-name> name)); unions> candidates;
			result   := <new-task(|task*)> Choice(subtask*);
		  u        := <new-use(|elem*, result)>
		  	
	// candidate in current scope
	nabl-use-candidate(|lang, elem*, task*, uri*, name):
		UseCandidate(ns, prop*, Current(), range) -> subtask*
		where
			uri      := <lookup-uri(|lang, ns)> uri*;
			subtask* := <nabl-use-subtasks(|task*, ns, name)> uri
			
	// TODO: take range into account.
	// TODO: other cases.
	// TODO: create reads for mapping uris to tasks.
	nabl-use-subtasks(|task*, ns, name): 
		uri  -> [subtask1, subtask2, subtask*|subtasks]
		where
			subtask1 := <new-task(|task*)> Resolve(uri, ns, name);
			subtask2 := <new-task(|task*)> ResolveImport(uri, ns, name);
			if ExternalDef(x) := <nabl-uri-path; Hd> uri then
        ext-uri  := <nabl-uri-language; nabl-base-uri; nabl-extend-uri(|Anonymous(x))> uri;
        subtask* := [<new-task(|task*)> Resolve(ext-uri, ns, name)]
      else
      	subtask* := []
      end;
			subtasks := <nabl-uri-parent < nabl-use-subtasks(|task*, ns, name) + ![]> uri

	// candidate in another scope (result of resolution)	  
	// nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
	// 	(UseCandidate(ns, prop*, target-uri*, range), name{anno*}) -> name{u, anno*}
	// 	where
	// 	  <?Result(_) <+ map(?Result(_))> target-uri*;
	// 	  pdep*    := <dependent-tasks> prop*;
	// 		subtask* := <nabl-use-subtasks(|task*, pdep*, ns, <nabl-fix-name> name)> target-uri*;
	// 		taskdep* := <dependent-tasks> subtask*;
	// 		result   := <new-task(|task*, [taskdep*, pdep*])> Choice(subtask*);
	// 	  u        := <new-use(|elem*, result)>	
		  
	// candidate in context
	//  nabl-use-candidate(|lang, elem*, task*, uri*, dep*):
	//    (UseCandidate(ns, prop*, Context(name', ns', props', refscope'), range), name{anno*}) -> name{u, anno*}
	//    where 
	//      _{uri}   := <nabl-use-candidate(|lang, elem*, task*, uri*, dep*)> (UseCandidate(ns', props', refscope', range), name'{});
	//      propdep* := <dependent-tasks> prop*;
	//      result   := <new-task(|task*, [dep*, propdep*])> Resolve(uri, ns, <nabl-fix-name> name, prop*, range);
	//      u        := <new-use(|elem*, result)>
    
rules // generic analysis of imports
  
  nabl-import(|lang, elem*, task*, uri*, imports):
    name -> name'
    where
    	Use(use) := <nabl-collect-use> name;
      name'    := <foldl(nabl-import-candidate(|lang, elem*, uri*, use))> (imports, name)
        
  nabl-import-candidate(|lang, elem*, uri*, use):
    (import, name) -> name
    where
      ns  := <nabl-import-namespace> import;
      uri := <lookup-uri(|lang, ns)> uri*;
      new-prop(|elem*, uri, import, use)
      
  nabl-import-namespace =
       ?Import(_, Imported(<id>)) 
    <+ ?Import(_, <id>)
    <+ ?Import(_, Imported(<id>), _)
    <+ ?Import(_, <id>, _) 

rules // generic analysis of properties
  
  // call back to generic analysis
	nabl-props(|elem*, prop*):
	  name -> name'
	  where
	    Def(uri) := <nabl-collect-def> name;
		  name'    := <foldl(extend-prop(|elem*, uri))> (prop*, name)
		
	extend-prop(|elem*, uri):
		(Prop(type, result), name) -> name
		where
		  prop := <new-prop(|elem*, uri, type, result)> name

rules // generic analysis of implicit defs
 
  nabl-implicit-def(|lang, partition, unique*, elem*, uri*, candidates):
    name -> name
    where
      <map(nabl-implicit-def-candidate(|lang, partition, unique*, elem*, uri*))> candidates
      
  nabl-implicit-def-candidate(|lang, partition, unique*, elem*, uri*):
    IDefCandidate(ns, name, unique, prop*) -> <id>
    where
      // get path for namespace of definition site
      current-uri := <lookup-uri(|lang, ns)> uri*;
      // build path for definition site
      def-uri     := <nabl-extend-uri(|unique*, partition, ns, name, unique)> current-uri;
      // build Def entry for the index
      new-def(|elem*, def-uri);
      // build Prop entries for the index
      <map(\Prop(type, result) -> <new-prop(|elem*, def-uri, type, result)>\)> prop*

rules // name retrieval
  
  nabl-name = is-string
  
  nabl-name-apply(s) = is-string; s
    
  nabl-fix-name = try(fix-completion-name); strip-annos
  
rules // URI retrieval
  
  nabl-collect-def =
    nabl-name;
    get-annos;
    collect-one(?Def(_))
    
  nabl-collect-all-def =
    nabl-name;
    get-annos;
    collect-all(?Def(_))
    
  nabl-collect-use =
    nabl-name;
    get-annos;
    collect-one(?Use(_))
    
  nabl-collect-all-use =
    nabl-name;
    get-annos;
    collect-all(?Use(_))

rules // helpers
	
	replace-scope-uri(|path) = foldl(update-scope-uri(!path|<nabl-uri-language> path))
  extend-scope-uri(s|lang) = foldl(update-scope-uri(s|lang))
  
	update-scope-uri(update|lang):
		(key, uri*) -> result
		where
		  result := <fetch((?key, update))> uri* 
		<+ 
		  result := [(key, <nabl-base-uri; update> lang)|uri*]
			  
	lookup-uri(|lang, ns):
	  uri* -> <<lookup> (ns, uri*) <+ <nabl-base-uri> lang>
	  
	collect-partition:
		(partition, _) -> (partition, CollectPartition())
		
	collect-partition:
		partition -> (partition, CollectPartition())
		where
			<is-string> partition