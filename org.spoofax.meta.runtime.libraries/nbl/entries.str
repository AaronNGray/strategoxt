module entries

imports
  
  nbl/collect
  nbl/utils
  nbl/uri
  
signature

  constructors
    
    Def   : URI                    -> Entry
    Alias : URI * URI              -> Entry
    Use   : URI                    -> Entry
    Prop  : URI * Property * Value -> Entry
  
rules
  
  new-def(|elems, uri):
    x -> definition
    where
      definition := <force-origins(!Def(uri))> x;
      <iset-add(|definition)> elems;
			<new-alias(|elems, <nabl-nonunique-uri> uri, uri)> x
      
  new-alias(|elems, uri, alias):
    x -> definition
    where
      definition := <force-origins(!Alias(uri, alias))> x;
      if <not(eq)> (uri, alias) then
      	<iset-add(|definition)> elems
      end
      
  new-use(|elems, uri):
    x -> use
    where
      use := <force-origins(!Use(uri))> x;
      <iset-add(|use)> elems

  new-prop(|elems, uri, kind, val):
    x -> prop
    where
      prop := <force-origins(!Prop(uri, kind, val))> x;
      <iset-add(|prop)> elems
      
rules // index uri & value projections
  
  /** @internal */
  nabl-uri-impl:
    Def(uri) -> uri
    
  /** @internal */
  nabl-uri-impl:
    Alias(uri, _) -> uri
    
  /** @internal */  
  nabl-uri-impl:
    Use(uri) -> uri
    
  /** @internal */  
  nabl-uri-impl:
    Prop(uri, _, _) -> uri
    
    
  /** @internal */
  nabl-value-impl:
    Def(value) -> value
    
  /** @internal */
  nabl-value-impl:
    Alias(_, value) -> value

  /** @internal */
  nabl-value-impl:
    Use(value) -> value
    
  /** @internal */
  nabl-value-impl:
    Prop(_, _, value) -> value
