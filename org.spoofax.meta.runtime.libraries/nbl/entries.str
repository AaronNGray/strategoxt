module entries

imports
  
  nbl/collect
  nbl/utils
  nbl/uri
  index/core
  
signature

  constructors
    
    Def   : URI                    -> Entry
    Alias : URI * URI              -> Entry
    Use   : URI                    -> Entry
    Prop  : URI * Property * Value -> Entry
  
rules
  
  new-def(|partition, uri):
    x -> definition
    where
      definition := <force-origins(!Def(uri))> x;
      <index-add(|partition)> definition;
			<new-alias(|partition, <nabl-nonunique-uri> uri, uri)> x
      
  new-alias(|partition, uri, alias):
    x -> alias
    where
      alias := <force-origins(!Alias(uri, alias))> x;
      if <not(eq)> (uri, alias) then
      	<index-add(|partition)> alias
      end
      
  new-use(|partition, uri):
    x -> use
    where
      use := <force-origins(!Use(uri))> x;
      <index-add(|partition)> use

  new-prop(|partition, uri, kind, val):
    x -> prop
    where
      prop := <force-origins(!Prop(uri, kind, val))> x;
      <index-add(|partition)> prop
      
rules // index uri & value projections
  
  /** @internal */
  nabl-uri-impl:
    Def(uri) -> uri
    
  /** @internal */
  nabl-uri-impl:
    Alias(uri, _) -> uri
    
  /** @internal */  
  nabl-uri-impl:
    Use(uri) -> uri
    
  /** @internal */  
  nabl-uri-impl:
    Prop(uri, _, _) -> uri
    
    
  /** @internal */
  nabl-value-impl:
    Def(value) -> value
    
  /** @internal */
  nabl-value-impl:
    Alias(_, value) -> value

  /** @internal */
  nabl-value-impl:
    Use(value) -> value
    
  /** @internal */
  nabl-value-impl:
    Prop(_, _, value) -> value
