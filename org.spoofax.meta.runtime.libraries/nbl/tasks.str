module nbl/tasks

imports 
  
  task/core
  nbl/lookup
  nbl/collect
  nbl/query
  nbl/interface
  nbl/uri
  nbl/utils
  nbl/entries
  
signature
  
  constructors
    
    Resolve        : Scope * Namespace * Name    -> Instruction
    ResolveImport  : Scope * Namespace * Name    -> Instruction
    PropConstraint : List(Prop) * List(Result)   -> Instruction
    PropCalc       : Property * Term             -> Instruction
        
rules // Resolve
  
  perform-task(|nr):
  	Resolve(scope*@<with(is-list|"Expected a list of scopes.")>, ns, name) -> result
  	where
  		uri*  := <filter(nabl-uri)> scope*;
  		name' := <try-remove-list; strip-annos> name; // Name could be a list, and annotated.
  		def*  := <nabl-lookup(|ns, name')> uri*;
  		<map(nabl-extend-uri(|ns, name', ()); task-add-read(|nr))> uri*;
  		switch !def*
        case ?[]:
        	fail
        otherwise: 
        	<try(nabl-disambiguate(|ns))> def*
			end => result

	perform-task(|nr):
		ResolveImport(scope*@<with(is-list|"Expected a list of scopes.")>, ns, name) -> result
		where
			uri* := <filter(nabl-uri)> scope*;
			name' := <try-remove-list> name;
			import* := <nabl-lookup-import(|ns, name')> uri*;
			<map(try(nabl-replace-uri-qualifier(|())); task-add-read(|nr))> uri*;
      if <task-has-dependencies> import* then
      	result := <task-collect-dependencies> import*
      else
	      def* := <nabl-lookup(|ns, name')> import*;
	      <map(nabl-uri; nabl-extend-uri(|ns, name', ()); task-add-read(|nr))> import*;
	  		switch !def*
	        case ?[]:
	        	fail
	        otherwise:
	        	<try(nabl-disambiguate(|ns))> def*
				end => result
      end
	  
  perform-task(|nr):
  	PropConstraint(prop*, res*) -> result
  	where
  		filtered := <concat; filter(nabl-resolve-constraint(|prop*))> res*;
      switch !filtered
      	case task-collect-dependencies => d: !d
      	otherwise: !filtered
      end => result
  		
  nabl-resolve-constraint(|prop*):
    entry -> entry'
    where
      filtered := <filter(nabl-resolve-constraint-one(|entry))> prop*;
      switch !filtered
      	case ?[]: fail
      	case task-collect-dependencies => d: !d
      	otherwise: !entry
      end => entry'
      
  // TODO: check dependencies (3rd subterm) as well.
  nabl-resolve-constraint-one(|entry):
    Prop(type, expectedValue, _) -> entry'
    where
    	uri   := <nabl-uri> entry;
      value := <nabl-get-property(|type)> uri;
      if value' := <insert-results> value then
        expectedValue' := <insert-results> expectedValue;
        <eq> (expectedValue', value');
        entry' := entry
      else
        entry' := <task-create-dependencies> value
      end

  perform-task(|nr): 
  	PropCalc(prop, term) -> result'
  	where
  		result := <calc-property> (prop, term);
  		switch !result
  			case task-collect-dependencies => d : !d
      	case not(is-list)                   : ![result]
      	otherwise                           : !result
			end => result'
