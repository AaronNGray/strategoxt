module nbl/check

imports
	
  nbl/resolve
  nbl/collect
  nbl/query
  nbl/tasks
  nbl/entries
  nbl/uri
  task/core
  task/messages
	
signature

	constructors
		
		NoUnresolvedError : NoUnresolvedError
	
rules // Error checking tasks
	
	nabl-unresolved-task(|partition, task):
		name -> name
		with
		  if <not(has-annos; get-annos; collect-one(?NoUnresolvedError()))> name then
		  	<nabl-fix-name; task-create-error-on-failure(|partition, task, "Unresolved reference")> name
		  end
		  
  nabl-duplicate-task(|partition, uri, ns, unique):
		name -> name
		with
			if Unique() := unique then 
				resolve := <new-task(|partition)> ResolveDefs(uri, ns, name);
				<task-create-error-on-multiple(|partition, resolve, "Duplicate definition")> name
			end

rules // Legacy
 
  nabl-is-unresolved =
    ?node;
    has-annos;
    get-annos;
    where(nabl-has-reference);
    not(nabl-collect-one-resolved-def)
  
  nabl-get-ambiguities =
    nabl-collect-all-resolved-defs;
    mapconcat(nabl-get-all-aliases);
    filter(nabl-uri; nabl-uri-parent);
    make-set;
    where(<gt> (<length>, 1))
  
  nabl-get-duplicate-definitions =
    has-annos;
    get-annos;
    collect-one(?Def(_));
    nabl-get-all-definitions;
    make-set;
    where(<gt> (<length>, 1))
