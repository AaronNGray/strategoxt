module analysis/core

imports
	
  nbl/collect
  nbl/entries
  nbl/uri
  nbl/utils
  index/core
  task/core
  task/messages
  
signature

  constructors
  
    File : Path * AST -> File
    Result : Partition * AST * AST * List(URI) * List(Term) * List(Term) * List(Term) -> Result
    // Result(partition, initial-ast, analyzed-ast, changed-read*, error*, warning*, note*)
    
rules // Analysis
	
	analyze-collect(|language, project-path):
    File(partition, initial-ast) -> Result(partition, initial-ast, analyzed-ast, changed-read*, error*, warning*, note*)
    with
    	measure-time(
	    	measure-time(
	    		index-setup(|language, [project-path], partition);
			    index-start-collection(|partition);
	    		task-setup(|project-path);
	    		task-start-collection(|partition)
	      	, id | "analyze-collect-setup"
	      ); measure-time(
	      	analyzed-ast := <nabl-collect(|partition, Language(language))> initial-ast
	    	  , id | "analyze-collect-collect" 
	  	  ); measure-time(
	  	  	index-stop-collection => (removed-entry*, added-entry*);
	      	task-stop-collection(|partition);
	      	changed-read* := <filter(analyze-diff-entry; nabl-uri; try(nabl-replace-uri-qualifier(|())))> [removed-entry*, added-entry*]
	      	, id | "analyze-collect-diff" 
	  	  );  measure-time(
	  	  	(error*, warning*, note*) := <analyze-messages(|partition)>
	  	  	, id | "analyze-collect-messages"
	  	  )
  	  , id | "analyze-collect-total")
	
	analyze-perform-all:
		result* -> (unevaluated-task*, evaluated-count)
		with
			measure-time(mapconcat(analyze-result-reads); make-set => changed-read*, id | "analyze-task-evaluate-concat-reads");
      measure-time(task-evaluate(|changed-read*) => (unevaluated-task*, evaluated-count), id | "analyze-task-evaluate")
  
  // TODO: origin tracking    
  analyze-messages(|partition):
  	_ -> (error*, warning*, note*)
  	with
  		message* := <task-get-messages(|partition)>;
  		error*   := <filter(?Error(_, _); analyze-message-to-tuple)> message*;
  		warning* := <filter(?Warning(_, _); analyze-message-to-tuple)> message*;
  		note*    := <filter(?Note(_, _); analyze-message-to-tuple)> message*
  		
	analyze-message-to-tuple:
		message -> (message, <task-message> message)

rules // Index entries to diff
	
  analyze-diff-entry =
    ?Def(_)
    
  analyze-diff-entry =
    ?Prop(_, _, _)
      
rules // Projections
	
	analyze-result-reads:
		Result(_, _, _, changed-read*, _, _, _) -> changed-read*
		
	analyze-result-analyzed-ast:
		Result(_, _, analyzed-ast, _, _, _, _) -> analyzed-ast