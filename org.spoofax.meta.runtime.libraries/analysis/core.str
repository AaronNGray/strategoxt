module analysis/core

imports
	
  nbl/collect
  nbl/entries
  nbl/uri
  nbl/utils
  index/core
  task/core
  task/messages
  
signature

  constructors
  
    File : Path * AST -> File
    Result : Partition * AST * AST * List(URI) * List(Term) * List(Term) * List(Term) -> Result
    // Result(partition, initial-ast, analyzed-ast, changed-read*, error*, warning*, note*)
    
rules // Analysis
	
	analyze-collect(|language, project-path):
    File(partition, initial-ast) -> Result(partition, initial-ast, analyzed-ast, changed-read*, [], [], [])
    with
    	measure-time(
    		index-setup(|language, [project-path], partition);
		    index-start-collection(|partition);
    		task-setup(|project-path);
    		task-start-collection(|partition)
      	, id | "analyze-collect-setup"
      ); measure-time(
      	analyzed-ast := <nabl-collect(|partition, Language(language))> initial-ast
    	  , id | "analyze-collect-collect" 
  	  ); measure-time(
  	  	index-stop-collection => (removed-entry*, added-entry*);
      	task-stop-collection(|partition);
      	changed-read* := <filter(analyze-diff-entry; nabl-uri; try(nabl-replace-uri-qualifier(|())))> [removed-entry*, added-entry*]
      	, id | "analyze-collect-diff" 
  	  )
	
	analyze-perform-all:
		result* -> (error*, evaluated-count)
		with
			measure-time(mapconcat(analyze-result-reads); make-set => changed-read*, id | "analyze-perform-concat-reads");
      measure-time(task-evaluate(|changed-read*) => (error*, evaluated-count), id | "analyze-perform-evaluate")
  
  // TODO: origin tracking    
  analyze-messages(|partition):
  	_ -> (error*, warning*, note*)
  	with
  		message* := <task-get-messages(|partition)>;
  		error*   := <filter(\Error(m) -> (m, m)\)> message*;
  		warning* := <filter(\Warning(m) -> (m, m)\)> message*;
  		note*    := <filter(\Note(m) -> (m, m)\)> message*

rules // Index entries to diff
	
  analyze-diff-entry =
    ?Def(_)
    
  analyze-diff-entry =
    ?Prop(_, _, _)
      
rules // Projections
	
	analyze-result-reads:
		Result(_, _, _, changed-read*, _, _, _) -> changed-read*
		
	analyze-result-analyzed-ast:
		Result(_, _, analyzed-ast, _, _, _, _) -> analyzed-ast