module analysis/core

imports
	
  analysis/interface
  index/core
  nabl/collect
  nabl/incremental
  nabl/entries
  nabl/uri
  nabl/utils
  task/core
  task/messages
  editor/origins
  
signature

  constructors
  
    File        : Path * AST -> File
    
    // Result(partition, initial-ast, analyzed-ast, changed-read*, error*, warning*, note*)
    Result      : Partition * AST * AST * List(URI) * List(Term) * List(Term) * List(Term) -> Result
    
    // DebugResult(removed-entry*, added-entry*, removed-task*, added-task*, invalidated-task*)
    DebugResult : List(Entry) * List(Entry) * List(TaskID) * List(TaskID) * List(TaskID) -> Result
    
rules // Analysis
	
	analyze-collect(|language, project-path) = analyze-collect(id|language, project-path)
	analyze-collect(try-recover|language, project-path):
    File(partition, initial-ast) -> ( 
    	Result(partition, initial-ast, analyzed-ast, changed-read*, [], [], [])
    , DebugResult(removed-entry*, added-entry*, removed-task*, added-task*, invalidated-task*)
  	)
    with
    	measure-time(
	    	measure-time(
	    		index-setup(|language, project-path);
	    		try(try-recover; index-recover);
			    index-start-collection(|partition);
	    		task-setup(|project-path);
	    		try(try-recover; task-recover);
	    		task-start-collection(|partition)
	      	, id | "analyze-collect-setup"
	      ); measure-time(
	      	analyzed-ast := <nabl-collect(|partition, Language(language))> initial-ast
	    	  , id | "analyze-collect-collect" 
	  	  ); measure-time(
	  	  	index-stop-collection => (removed-entry*, added-entry*);
	      	task-stop-collection(|partition) => (removed-task*, added-task*);
	      	changed-read* := <nabl-get-changed-reads> [removed-entry*, added-entry*]
	      	, id | "analyze-collect-diff" 
	  	  ); measure-time(
	  	  	task-invalidate-task-reads(|changed-read*) => invalidated-task*
	  	  	, id | "analyze-collect-invalidate"
	  	  )
  	  , id | "analyze-collect-total")
	
	analyze-perform-all:
		result* -> (result'*, evaluated-task*, unevaluated-task*)
		with
			measure-time(
				measure-time(
	      	task-evaluate-scheduled => (evaluated-task*, unevaluated-task*)
	      	, id | "analyze-task-evaluate"
	      ); measure-time(
	  	  	result'* := <map(analyze-messages)> result*
	  	  	, id | "analyze-task-messages"
	  	  )
  	, id | "analyze-task-total")
  	  
  analyze-messages:
  	(Result(partition, initial-ast, analyzed-ast, changed-read*, _, _, _), debug-result) -> 
  	(Result(partition, initial-ast, analyzed-ast, changed-read*, error*, warning*, note*), debug-result)
  	with
			(error*, warning*, note*) := <analyze-get-messages> partition

	analyze-get-messages:
		partition -> (error*, warning*, note*)
		with
  		message* := <task-get-messages(|partition)>;
  		error*   := <filter(?Error(_, _, _); analyze-message-to-tuple)> message*;
  		warning* := <filter(?Warning(_, _, _); analyze-message-to-tuple)> message*;
  		note*    := <filter(?Note(_, _, _); analyze-message-to-tuple)> message*
  		
	analyze-message-to-tuple:
		message -> (<origin-location-offset-set(|origin)> term, msg)
		with
			origin := <task-message-origin> message;
			term   := <task-message-term> message;
			msg    := <task-message> message
      
rules // Projections
	
	analyze-result-reads:
		Result(_, _, _, changed-read*, _, _, _) -> changed-read*
		
	analyze-result-analyzed-ast:
		Result(_, _, analyzed-ast, _, _, _, _) -> analyzed-ast

	analysis-partition(|path, project-path) = !$[[project-path]/[path]]
