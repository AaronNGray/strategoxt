module task/incremental

imports 
  
  task/core
  task/store
  index/core
  index/query
  
rules // Diff
	
	task-diff:
		(old, new) -> (added, removed)
		where
      added   := <diff(task-eq)> (new, old);
      removed := <diff(task-eq)> (old, new)
      
  task-diff-entry =
  	?Task(_, _)
      
rules // Incremental
	
	task-olds:
		partition -> <task-partition; index-get-all-in-partition; filter(task-diff-entry)> partition
			
	task-clear:
		partition -> <task-partition; index-clear-partition> partition
	
  task-incremental:
  	(old, new) -> [added*, depTask*]
  	with
  		(added*, removed*) := <task-diff> (old, new);
  		<map(task-remove-result)> removed*;
  		depTask* := <mapconcat(task-nr; task-dependent); map(debug(!"Dependent task: "))> [added*, removed*]
  		
	task-remove-result:
		nr -> <index-remove-all> DoneTask(nr, ())
		
	task-dependent:
		nr -> task*
		with
			nr*   := <task-dep-template; index-get-all; map(task-dep-dep)> nr;
			task* := <map(task-reconstruct)> nr*

rules // Equality
	
	task-eq:
		(t1, t2) -> <id>
		where
			<eq> (<task-nr> t1, <task-nr> t2)
		
rules // Projections
	
	task-nr:
		Task(nr, _, _, _) -> nr
		
	task-nr:
		Task(nr, _) -> nr
	
	task-dep-dep:
		TaskDep(_, d) -> d
		
	task-template:
		nr -> Task(nr, ())
		
	task-dep-template:
		nr -> TaskDep(nr, ())

rules // Construction
		
	task-reconstruct:
		nr -> Task(nr, d*, i, p)
		with
			(p, Task(_, i)) := <task-template; index-get-all-with-partitions; Hd> nr;
			d*             := <task-dep-template; index-get-all; map(task-dep-dep)> nr
