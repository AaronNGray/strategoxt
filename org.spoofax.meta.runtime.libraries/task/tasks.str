module task/tasks

imports
	
	task/core
  task/messages
	task/interface
  
signature

	constructors
		
    Id       : Term              -> Instruction
    Not      : Result            -> Instruction
    Combine  : List(Term)        -> Instruction
    Collect  : List(Term)        -> Instruction
    Choice   : List(List(Term))  -> Instruction
    Sequence : List(Term)        -> Instruction
    Rewrite  : ID * Term         -> Instruction
    Index    : Int * List(Term)  -> Instruction
    Length   : List(Term)        -> Instruction
rules
	
	perform-cyclic-task(|nr) = fail
	
	perform-task(|nr):
		(instruction, "cyclic") -> <perform-cyclic-task(|nr) <+ perform-task(|nr)> instruction

rules
	
	task-is-combinator = ?Choice(_)
  perform-task(|nr)  = ?Choice(<fetch-elem(not(?[]))>)

  /**
  * Create a choice task
  * @type List(task) -> Result(Int)
  */
  task-create-or(|ctx) = <new-task(|ctx)> Choice(<id>)
  
  /**
  * Create a choice task between two tasks, where t1 and t2 take a ctx and produce a task.
  * @type term -> Result(Int)
  */
  task-create-or(t1, t2|ctx):
    term -> <task-create-or(|ctx)> [<t1(|ctx)> term, <t2(|ctx)> term]
  
rules
	
	task-is-combinator = ?Sequence(_)
  perform-task(|nr)  = ?Sequence(<not(fetch-elem(?[])); last>)
  
  /**
  * Create a sequence task
  * @type List(task) -> Result(Int)
  */
  task-create-and(|ctx) = <new-task(|ctx)> Sequence(<id>)

  /**
  * Create a sequence task between two tasks, where t1 and t2 take a ctx and produce a task.
  * @type term -> Result(Int)
  */
  task-create-and(t1, t2|ctx):
    term -> <new-task(|ctx)> Sequence([task1, task2])
    with
      task1 := <t1(|ctx)> term;
      task2 := <t2(|ctx)> term
  
rules
  
  perform-task(|nr) = ?Id(<MkSingleton>)

rules
  
  /**
  * Negation on the success of a task
  * @type: Not(result) -> !Success(result)
  */
  perform-task(|nr):
    Not(result) -> ()
    where
      <?[]> result

  task-is-combinator = ?Not(_)
  
  /**
  * Create negation task
  */
  task-create-not(|ctx) = <new-task(|ctx)> Not(<id>)
  
  /**
  * Create negation task, where t takes a ctx argument and produces the task to be negated 
  */
  task-create-not(t|ctx) = t(|ctx); task-create-not(|ctx)
  
rules
       
  task-is-combinator = ?Combine(_)
  perform-task(|nr)  = ?Combine(<map(is-list <+ MkSingleton)>); concat

rules
       
  task-is-combinator = ?Collect(_)
  perform-task(|nr)  = ?Collect(<map(is-list <+ MkSingleton)>); concat; MkSingleton
  
rules
      	
  perform-task(|nr) = ?Rewrite(key, term); <task-rewrite; MkSingleton> (key, term)
  
rules
  
  perform-task(|nr) = ?Index(i, <index(|i)>);  MkSingleton
  perform-task(|nr) = ?Length(<length>);  MkSingleton
  
