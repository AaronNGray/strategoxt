module task/core

imports 
  
  task/interface
  index/core
  
signature

  sorts
  
    Task Result Instruction
  
  constructors
    
    Task     : Int * List(Int) * Instruction -> Task
    Result   : Int                           -> Result
    DoneTask : Int * Term                    -> DoneTask
    
    Done       : Term                        -> Result
    Fail       :                                Result
    Dependency : List(Int)                   -> Result

rules // Creation
  
  new-task(|task*) = new-task(|task*, <dependent-tasks>)
  new-task(|task*, deps) = new-task(|task*, deps, <address>) 
  new-task(|task*, deps, name):
    instruction -> Result(name)
    with
      task := Task(name, deps, instruction);
      <iset-add(|task)> task*
      
  dependent-tasks = collect-all(dependent-task, union); flatten-list; make-set
  dependent-task = ?Result(<id>) 
  dependent-task =
    has-annos; get-annos; dependent-tasks
  
rules // Execution
  
  perform-tasks(|partition) = repeat(perform-task(|partition)); map(debug(!"ERROR: "))
    
  perform-task(|partition) =
    split-fetch-keep(perform-single-task) => (t1*, (n, res, i), t2*);
    switch !res
      case ?Done(tr):
        <index-add(|partition)> DoneTask(n, tr);
        <map(try(Task(id, effectively(remove-all(?n)), try(alltd(insert-result(|n, tr))))))> [t1*, t2*]
      case ?Fail():
      	<map(try(Task(id, effectively(remove-all(?n)), try(alltd(insert-result(|n, Fail()))))))> [t1*, t2*]
      case ?Dependency(d*):
      	![t1*, t2*, Task(n, d*, i)]
      end

  perform-single-task:
    Task(n, [], i) -> (n, <perform-task> i, i)
    
rules // Utility
  
  effectively(s) = ?t; s; not(?t)  
     
  insert-result(|n, r): Result(n) -> r 
    
  insert-result(|n, r) = 
    has-annos; 
    !(<id>, <get-annos; map(try(insert-result(|n, r)))>); 
    set-annos
