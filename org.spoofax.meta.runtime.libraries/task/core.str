module task/core

imports 
  
  task/interface
  index/core
  
signature

  sorts
  
    Task Result Instruction
  
  constructors
    
    Task     : List(String) * List(String) * Instruction -> Task
    Result   : String                                    -> Result
    Reserved : String                                    -> Result
    DoneTask : String * Term                             -> DoneTask
    FailTask : String * Term                             -> FailTask
    
    Done     :                                              Result
    Done     : Term                                      -> Result
    Fail     : Term                                      -> Result

rules // Creation
  
  new-task(|task*) = new-task(|task*, <dependent-tasks>)
  new-task(|task*, deps) = new-task(|task*, deps, <address>) 
  new-task(|task*, deps, name):
    instruction -> Result(name)
    with
      task := Task(name, deps, instruction);
      <iset-add(|task)> task*
  
  reserve-task =
    !Reserved(<newname> "")
      
  dependent-tasks = collect-all(dependent-task, union); flatten-list; make-set
  dependent-task = ?Result(<id>) 
  dependent-task =
    has-annos; get-annos; dependent-tasks
  
rules // Execution
  
  perform-tasks(|partition) = repeat(perform-task(|partition)); map(debug(!"ERROR: "))
    
  perform-task(|partition) =
    split-fetch-keep(perform-single-task) => (t1*, (n, res), t2*);
    switch !res
      case ?Done(tr):
        <index-add(|partition)> DoneTask(n, tr)
      case ?Done():
        tr := ()
      case ?Fail(tr):
        <index-add(|partition)> FailTask(n, tr)
      end;
    <map(try(Task(id, effectively(remove-all(?n)), try(alltd(insert-result(|n, tr))))))> [t1*, t2*]
   
  perform-single-task:
    Task(n, [], t) -> (n, <perform-task> t)
    
rules // Utility
  
  effectively(s) = ?t; s; not(?t)  
     
  insert-result(|n, r): Result(n) -> r 
    
  insert-result(|n, r) = 
    has-annos; 
    !(<id>, <get-annos; map(try(insert-result(|n, r)))>); 
    set-annos
