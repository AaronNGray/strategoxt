/**
 * Unpack-sdf extracts the modules from an SDF definition and writes them to separate files.
 */
module unpack-sdf
imports lib xtc-lib stratego-xt-xtc-tools Sdf2-Syntax

strategies

  io-unpack-sdf = io-wrap(unpack-sdf-options, unpack-sdf)

  unpack-sdf-options =
    ArgOption("-d", where(<set-config>("-d", <id>)), !"-d dir    Destination directory")  

  unpack-sdf =
    xtc-temp-files(
      !FILE(<get-config>"-i")
    ; xtc-parse-sdf
    ; read-from
    )
  ; split-sdf-definition

  split-sdf-definition =
    "\"sdf-2.1\""#([Definition(map(save-sdf-module-to-file))])

  save-sdf-module-to-file =
    where( get-module-name => basename )
  ; xtc-temp-files(
      write-to
    ; xtc-transform(!"pp-sdf")
    ; where( 
        <concat-strings>[ <get-config<+!".">"-d", "/", basename, ".sdf"] => file
      ; dirname 
      ; mkdir
      )
    ; copy-to(!file)
    )

  get-module-name =
    ?Module(<id>,_,_)

  mkdir = 
    where( readdir <+ <call>("mkdir",[<id>]) )
