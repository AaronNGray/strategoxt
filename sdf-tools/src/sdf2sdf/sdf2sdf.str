module sdf2sdf
imports xtc-lib stratego-xt-xtc-tools

/**
 * sdf2sdf is a tool for applying transformations to SDF
 * definitions. By means of command line options the application
 * of several transformations can be applied. Without any options
 * the tool acts as a pretty-printer.
 *
 * This tool combines several tools working on SDF abstract syntax and
 * provides a parsing front-end and pretty-printing back-end.
 *
 * Usage:
 *  sdf2sdf -i file -o file [--pack-sdf] [--sdfcons] [--sdf2asdf] [--sdf-bracket]
 *
 * Authors:
 *    Merijn de Jonge <mdejonge@cwi.nl>
 *    Eelco Visser <visser@acm.org>
 *    Joost Visser <jvisser@cwi.nl>
 *    Martin Bravenboer <martin@mbravenboer.org>
 */
strategies

  sdf2sdf =
    xtc-io-wrap(sdf2sdf-options,
      (do-pack-sdf < xtc-pack-sdf(sdf-includes) + xtc-parse-sdf)
    ; execute-specified-tools 
    ; xtc-pp-sdf
    )

  execute-specified-tools =
    <foldl(\ (tool, file) -> <xtc-transform(!tool)> file \)> (<specified-tools> (), <id>)

strategies // for options

  sdf2sdf-options = 
      Option("--pack-sdf"
      , where(<set-config>("--pack-sdf", "dummy"))
      , !"--pack-sdf       Pack sdf modules before applying other tools"
      )
    + ArgOption("-I" + "--Include"
      , where(<extend-config>("-SDFI", [<id>]))
      , !"-I d | --Include d\n                    Include modules from directory d"
      )
    + Option("--sdfcons"
      , where(<extend-config>("--tools", ["sdf-cons"]))
      , !"--sdfcons        Apply sdf2cons"
      )
    + Option("--sdf2asdf"
      , where(<extend-config>("--tools", ["sdf2asdf"]))
      , !"--sdf2asdf       Apply sdf2asdf"
      )
    + Option("--sdf-bracket"
      , where(<extend-config>("--tools", ["sdf-bracket"]))
      , !"--sdf-bracket    Apply sdf-bracket"
      )

  sdf-includes =
    <get-config> "-SDFI" <+ ![]

  specified-tools =
    <get-config> "--tools" <+ ![]

  do-pack-sdf =
    where(<get-config> "--pack-sdf")
