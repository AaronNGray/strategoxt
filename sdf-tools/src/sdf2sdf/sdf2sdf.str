/**
 * sdf2sdf is a tool for applying transformations to SDF
 * definitions. By means of command line options the application
 * of several transformations can be applied. Without any options
 * the tool acts as a pretty-printer.
 *
 * This tool combines several tools working on SDF abstract syntax and
 * provides a parsing front-end and pretty-printing back-end.
 *
 * Usage:
 *  sdf2sdf -i file -o file [--pack-sdf] [--sdfcons] [--sdf2asdf] [--sdf-bracket]
 *
 * Authors:
 *    Merijn de Jonge <mdejonge@cwi.nl>
 *    Eelco Visser <visser@acm.org>
 *    Joost Visser <jvisser@cwi.nl>
 *    Martin Bravenboer <martin@cs.uu.nl>
 */
module sdf2sdf
imports liblib sdf-xtc-tools
strategies

  sdf2sdf =
    xtc-io-wrap(sdf2sdf-options,
      if do-pack-sdf then
        xtc-pack-sdf2(sdf-includes)
      else id end
    ; xtc-parse-sdf2
    ; execute-specified-tools 
    ; xtc-pp-sdf2
    )

  xtc-pack-sdf2(includes) =
      xtc-transform(!"pack-sdf",
        <conc> (<pass-verbose> (), <includes; <add-arg-flags> ("-I", <id>)> ())
      )

  execute-specified-tools =
    !(<specified-tools>, <id>)
    ; foldl(\ (tool, file) -> <xtc-transform(!tool)> file \ )

strategies // for options

  sdf2sdf-options = 
      Option("--pack-sdf"
      , where(<set-config>("--pack-sdf", "dummy"))
      , !"--pack-sdf       Pack sdf modules before applying other tools"
      )
    + ArgOption("-I" + "--Include"
      , where(<extend-config>("-SDFI", [<id>]))
      , !"-I d | --Include d\n                    Include modules from directory d"
      )
    + Option("--sdfcons"
      , where(<extend-config>("--tools", ["sdf-cons"]))
      , !"--sdfcons        Apply sdf2cons"
      )
    + Option("--sdf2asdf"
      , where(<extend-config>("--tools", ["sdf2asdf"]))
      , !"--sdf2asdf       Apply sdf2asdf"
      )
    + Option("--sdf-bracket"
      , where(<extend-config>("--tools", ["sdf-bracket"]))
      , !"--sdf-bracket    Apply sdf-bracket"
      )

  sdf-includes =
    <get-config> "-SDFI" <+ ![]

  specified-tools =
    <get-config; reverse> "--tools" <+ ![]

  do-pack-sdf =
    where(<get-config> "--pack-sdf")
