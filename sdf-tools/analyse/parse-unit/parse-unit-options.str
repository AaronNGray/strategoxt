/**
 * Options for parse-unit
 *
 * @author Martin Bravenboer <martin@cs.uu.nl>
 */
module parse-unit-options
imports 
  tool-doc

signature
  constructors
    CustomSglrOptions    : ConfigKey
    ParseTable           : ConfigKey

strategies

  parse-unit-usage =
    <tool-doc>
      [ Usage("parse-unit -p <file.tbl> -i <file.testsuite>")
      , Summary(
         "Performs the test cases in a testsuite for an SDF syntax definition")
      , OptionUsage()
      , AutoReportBugs()
      ]

  parse-unit-about =
    <tool-doc>
      [ AutoProgram()
      , Author(Person("Martin Bravenboer", "martin@cs.uu.nl"))
      , GNU_LGPL("2003-2005", "Eelco Visser <visser@acm.org>")
      , Config([
          DefaultXTCRepository()
        , CurrentXTCRepository()
        ])
      ]

strategies

  parse-unit-options =
       sglr-parse-table-option
    <+ sglr-filter-option
    <+ single-option
    <+ single-asfix2-option
    <+ single-ast-option

  init-parse-unit-options =
    where(<set-verbosity> 1)
    ; init-sglr-options

  init-sglr-options =
    where(
      <set-config> (CustomSglrOptions(), ["-fi", "-fe"])
    )

strategies

  pass-custom-sglr-options =
    <get-config> CustomSglrOptions() <+ ![]

  apply-to-custom-sglr-options(s) = 
    where(
      <get-config> CustomSglrOptions()
    ; s
    ; <set-config> (CustomSglrOptions(), <id>)
    )

/**
 * Parse table
 */
strategies

  sglr-parse-table-option =
    ArgOption("-p"
    , where(<set-config> (ParseTable(), <id>))
    , !"-p <file.tbl>         Use parse table <file.tbl> (required)"
    )

  get-sglr-parse-table =
    <get-config> ParseTable()

/**
 * Filters
 */
strategies

  sglr-filter-option =
      ArgOption("--heuristic-filters"
      , switch id
          case "off" : disable-heuristic-filters
          case "on"  : enable-heuristic-filters
          otherwise  : fatal-err(|"Invalid value for option --heurstic-filters: use on or off")
        end
      , !"--heuristic-filters <on|off>  Enable heuristic disambiguation filters (default: off)"
      )

    + Option("--no-heuristic-filters"
      , disable-heuristic-filters
      , !"--no-heuristic-filters        Disable heuristic disambiguation filters (deprecated option)"
      )

  enable-heuristic-filters =
    apply-to-custom-sglr-options(
      remove-all("-fi" + "-fe")
    )

  disable-heuristic-filters =
    apply-to-custom-sglr-options(    
      if not(fetch(?"-fi")) then
        !["-fi" | <id>]
      end
    ; if not(fetch(?"-fe")) then
        !["-fe" | <id>]
      end
    )

  remove-all(s) =
    filter(not(s))

strategies

  single-option =
    ArgOption("--single"
    , where(<set-config> ("--single", <string-to-int>))
    , !"--single <nr>     Parse test <nr> and output the result to the output"
    )

  get-single-test =
    <get-config> "--single"

  get-single-test-output =
    <get-config> "--single-output"

  is-single-test-output-asfix2 =
    get-single-test-output => "asfix2"

  single-asfix2-option =
    Option("--asfix2"
    , where(<set-config> ("--single-output", "asfix2"))
    , !"--asfix2          Parse to AsFix2 (only applicable when --single is used)"
    )

  single-ast-option =
    Option("--ast"
    , where(<set-config> ("--single-output", "ast"))
    , !"--ast             Parse to AST (only applicable when --single is used)"
    )

  abs-test-file =
    <conc-strings> (<get-relative-prefix> "-i", <id>)

  get-relative-prefix =
       get-config; get-path
    <+ <conc-strings> (<getcwd>, "/")
