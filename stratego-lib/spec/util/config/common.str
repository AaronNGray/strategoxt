/**
 * Get and set values in the configuration table.
 */
module util/config/common
imports 
  collection/hash-table/common

strategies

  get-config = 
    <table-get> ("config", <id>)

  /**
   * Get configurations of keys for which pred succeeds
   */
  get-configs(pred) =
    <table-getlist> "config"; filter((pred,id); Snd) 

  /**
   * get configuration keys for which pred succeeds
   */
  get-config-keys(pred) =
    <table-getlist> "config"; filter((pred,id); Fst) 

strategies

  set-config = 
    ?(key, val)
    ; <table-put> ("config", key, val)

  rm-config = 
    ?key
    ; <table-remove> ("config", key)

  toggle-config = 
    ?(key, val)
    ; (<get-config> key; <table-remove> ("config", key)
       <+ <set-config> (key, val))

  extend-config =
    ?(key, val)
    ; <table-put> ("config", key, <conc> (val, <get-config <+ ![]> key))

  post-extend-config =
    ?(key, val)
    ; <table-put> ("config", key, <conc> (<get-config <+ ![]> key, val))
