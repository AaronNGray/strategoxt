/**
 * Every element of a list is associated with in an integer index.
 * The first element of a list has index 1.
 */
module collection/list/index
imports 
  collection/list/cons 
  strategy/traversal/simple

strategies

 /**
  * Get the n-th element of a list.
  *
  * The first element has index 1.
  *
  * @type Int * List(a) -> a
  * @inc test-index
  */
  index =
    (inc, id); at-index(?t); !t

strategies

 /**
  * Get index of element in list.
  *
  * @type a * List(a) -> Int
  */
  get-index = 
    Gind0 ; rec x(Gind1 <+ Gind2 ; x)

      Gind0  : (x, ys) -> (1, x, ys)
      Gind1  : (n, x, [x | xs]) -> n
      Gind2  : (n, y, [x | xs]) -> (<add> (n, 1), y, xs)

 /**
  * @obsolete use get-index
  */
  get_index = 
    obsolete(!"get_index/0; use get-index/0");
    get-index

  get-index0(s) = at-suffix([s | id]; ![]); length

strategies

 /**
  * Change element in list.
  * The first element has index 0.
  *
  * @type Int * a * List(a) -> List(a)
  * @inc  test-set-index
  */
  set-index =
    ?(i, x, xs)
    ; <at-index(!x)> (i, xs)

 /**
  * Apply s at the specified index
  * The first element has index 0.
  *
  * @type (Int, List(a)) -> List(a)
  */
  at-index(s) =
    ?(i, <id>);

    let next(|j) =
          if !j => i then
            [s | id]
          else
            [id | next(|<inc> j)]
          end
     in next(|0)
    end

strategies

 /**
  * Insert element in list.
  *
  * @type Int * a * List(a) -> List(a)
  * @inc test-insert
  */
  insert =
    Ins0; rec x(Ins1 <+ Ins2(x))

        Ins0:    (i, x, ys) -> (0, i, x, ys)
        Ins1:    (i, i, x, xs) -> [x | xs]
        Ins2(r): (n, i, x, [y | ys]) -> [y | <r>(<add> (n, 1), i, x, ys)]

 /**
  * Apply strategies that require some knowledge of the index of an element 
  * to the elements of the list.
  *
  * @type Int * a -> b :: List(a) -> List(b)
  * @inc  test4a
  * @inc  test4b
  */
  map-with-index(s) = 
      !(1, <id>)
    ; rec x(
        \ (_, [])       -> [] \
      + \ (i, [e | es]) -> [<s> (i, e) | <x> (<inc> i, es)] \
      )

  // :: List(a) -> List(Int * a)
 /**
  * @inc test5
  */
  add-indices = 
    map-with-index(id)
