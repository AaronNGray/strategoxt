/**
 * Creates an xtar.
 *
 * @since 0.13
 */
module xtar
imports
  libstratego-xtc
  pack-sdf-options
  tool-doc

strategies

  /**
   * Entry point
   */
  main-xtar =
    option-wrap(xtar-options,  xtar-usage, xtar-about, id, 
      xtar-init-options
    ; xtc-io(
        create-xtar
      <+ say(!"sorry, could not create xtar")
         ; <xtc-exit> 1
      )
    )

  /**
   * @todo  must the temp dir be removed? Could xtc temp files handle that?
   *
   * @type  FILE -> FILE
   */
  create-xtar =
    where(new-temp-dir => dir)
    ; ?mod
    ; <create-syntax-def(|dir)> mod => def
    ; <create-parse-table(|dir)> def => tbl
    ; <create-pp-table(|dir)> def => pp
    ; <create-pp-table-parsed(|dir)> pp => app
    ; <create-rtg(|dir)> def => rtg
    ; <create-rtgnf(|dir)> rtg => rtgnf
    ; <create-str-sig(|dir)> rtg => strsig
    ; <create-rtree-sig(|dir)> strsig => rtreesig

    ; <xtc-zip> [def, tbl, pp, app, rtg, rtgnf, strsig, rtreesig]

  /**
   * Creates a zip archive.
   *
   * The basenames of the files must be unique. Only the basename
   * will be stored.
   *
   * @todo  use a zip library instead of the command-line zip tool
   * @todo  register the zip file in the xtc temp files.
   *
   * @type  List(FILE) -> FILE
   */
  xtc-zip =
    where(<xtc-new-file> () => zip)
    ; <call> ("zip", ["-j", zip | <map(?FILE(<id>))>])
    ; !FILE(<conc-strings> (zip, ".zip"))

  /**
   * Creates an SDF definition by invoking pack-sdf.
   *
   * @type  FILE -> FILE
   */  
  create-syntax-def(|dir) =
    xtar-create(
      xtc-transform(!"pack-sdf", <conc> (<pass-verbose>, <pass-sdf-includes>))
    | dir, "def")

  pass-sdf-includes =
    <get-config> "-I"
    ; reverse
    ; map(
        \ IncludeDef(def) -> ["-Idef", def] \
      + \ IncludeDir(dir) -> ["-I",    dir] \
      )
    ; concat

    <+ ![]

  xtar-create(s | dir, ext) =
    s
    ; rename-to(<concat-strings> [dir, "/", <get-language-name> (), ".", ext])

  create-parse-table(|dir) =
    xtar-create(
      xtc-transform(!"sdf2table", !["-m", <get-language-name> ()])
    | dir, "tbl")

  create-rtg(|dir) =
    xtar-create(
      xtc-transform(!"sdf2rtg", !["-m", <get-language-name> ()])
    | dir, "rtg")

  create-rtgnf(|dir) =
    xtar-create(
      xtc-transform(!"parse-rtg")
      ; xtc-transform(!"rtg-reduce")
      ; xtc-transform(!"rtg-group")
    | dir, "rtgnf")

  create-str-sig(|dir) =
    xtar-create(
      xtc-transform(!"rtg2sig", !["--module", <get-language-name> ()])
    | dir, "str")

  create-rtree-sig(|dir) =
    xtar-create(
      xtc-transform(!"parse-stratego")
    | dir, "rtree")

  create-pp-table(|dir) =
    xtar-create(
      xtc-transform(!"ppgen")
    | dir, "pp")

  create-pp-table-parsed(|dir) =
    xtar-create(
      xtc-transform(!"parse-pp-table")
    | dir, "app")


/**
 * Options
 */
strategies

  xtar-options =
       xtar-input-option
    <+ xtar-output-option

    <+ Option(fail, fail, !"     ")
    <+ include-from-dir-option
    <+ include-from-def-option
    <+ dep-options

    <+ Option(fail, fail, !"     ")
    <+ xtar-verbose-option

  get-language-name =
    <get-config> "language-name"

  xtar-init-options =
    where(
      /* check if there is any input */
      if not(<get-config> "-i") then
        log(|Error(), "no input specified")
        ; <xtc-exit> 1
      end

      /* fetch language name from SDF module filename */
    ; <get-config> "-i"
    ; base-filename
    ; explode-string
    ; if at-suffix( \ ['.', 's', 'd', 'f'] -> [] \ ); ?lang then
        <implode-string> lang
        ; log(|Info(), <conc-strings> ("info: language name is ", <id>))
        ; <set-config> ("language-name", <id>)
      else
        log(|Error(), "error: input file must have the extension .sdf")
        ; <xtc-exit> 1
      end

      /* set the default output file */
    ; if not(<get-config> "-o") then
        <set-config> ("-o", <conc-strings> (<get-language-name> (), ".xtar"))
      end

      /* Set the default verbosity level */
    ; if not(<get-config> "--verbose") then
        <set-config> ("--verbose", 1)
      end
    )

  xtar-input-option =
    ArgOption("-i" + "--input"
    , where(<set-config> ("-i",<id>))
    , !"-i|--input <lang.sdf>    Create xtar for SDF module <lang.sdf> (required)"
    )

  xtar-output-option =
    ArgOption("-o" + "--output"
    , where(<set-config> ("-o",<id>)); !Output(<id>)
    , !"-o|--output <lang.xtar>  Write xtar to <lang.xtar> (default: lang.xtar)" 
    )

  xtar-verbose-option =
    ArgOption("--verbose"
    , where(<set-config> ("--verbose", <string-to-int>))
    , !"--verbose <i>    Verbosity level <i> (default: 0)"
    )

/**
 * Help and about
 */
strategies

  xtar-usage =
    <tool-doc> xtar-tool-usage()

  xtar-about =
    <tool-doc> xtar-tool-about()

overlays

  xtar-tool-usage =
    [ Usage("xtar -i <lang.sdf> [OPTIONS]")
    , Summary(
       "Creates an XT Archive (xtar)")
    , OptionUsage()
    , AutoReportBugs()
    ]

  xtar-tool-about =
    [ HSection("xtar", [
        DefList([
          Def("Package",  Paragraph(<package-name> ()))
        , Def("Version",  Paragraph(<package-version> ()))
        , Def("Revision", Paragraph(<prim("SVN_REVISION_TERM")> ()))
        ])
      ])
    , Authors([
        Person("Martin Bravenboer", "martin@cs.uu.nl")
      , Person("Karl Trygve Kalleberg", "karltk@ii.uib.no")
      ])
    , GNU_LGPL("2004-2005", "Eelco Visser <visser@acm.org>")
    , Config([
        DefaultXTCRepository()
      , CurrentXTCRepository()
      ])
    , AutoReportBugs()
    ]
