/**
 * Java backend for the Stratego compiler
 *
 * @author Karl Trygve Kalleberg
 * @author Lennart Kats
 * @author Eelco Visser
 */
module s2j
imports
  Stratego-Core 
  libstratego-lib
  Java-15 
  Java-EBlock 
  libstrc
  libstratego-xtc
  libstratego-rtg

signature
  constructors 
    Next  : Term
    Match : Term * Exp -> Exp
    Filename : Str -> Term
    
strategies

  io-s2j = io-wrap(java-backend)
 
rules
 
  fc =
    with(
      rtg-format-check(true, true|<java-rtg>())
    <+
      ?x
    ; where(err(|["Bad term : ", x]))
    ; fail
    | "Internal error: resulting term is not well-formed"
    )
    
  java-backend = 
      where(<xtc-find ; parse-rtg-file> "Java-15.rtg" => rtg)
    ; rules( java-rtg := rtg )
    ; extract-meta
    ; java-compile-spec
      
  extract-meta = 
     ?_{Meta(metas)}
    ; where(rules(metas := metas))
  
  meta-filename = 
      <metas>()
    ; filter(\ Filename(n) -> n \)
    ; Hd

  compute-classname = 
      meta-filename 
    ; base-filename 
    ; remove-extension
    
  java-compile-spec :
    Specification([Signature([Constructors(ops)]), Strategies(defs)]) -> 
     |[ 
     package pkgname;
     
     import org.strategoxt.lang.Strategy; 
     import org.strategoxt.lang.IStrategy; 
     import org.strategoxt.lang.Context;
     import org.spoofax.interpreter.terms.*;
         
     import static org.strategoxt.lang.Term.nonNull;
     import static org.strategoxt.lang.Term.checkTermList;
     import static org.strategoxt.lang.Term.matchConstructor;
     
     // TODO: Use static imports for libraries
     //       e.g., import static org.strategoxt.strategolib.*;
     		
     @SuppressWarnings("unused")
     public class x_name {

     ~*cnst-decls
     ~*cnstr-decls
     ~*lifted-defs
     
     private static void preInit(ITermFactory termFactory) {
       ~*cnstr-inits
       ~*cnst-inits
     }

	 public static String testMain(String input) {
	    Context context = new Context();
	    preInit(context.getFactory());
	    IStrategoTerm result = main_0_0.getInstance().invoke(context, context.getFactory().parseFromString(input));
        return result == null ? null : result.toString();
     }
        
	 public static void main(String args[]) {
	    Context context = new Context();
	    preInit(context.getFactory());
	    IStrategoTerm result = context.invokeStrategyCLI(main_0_0.getInstance(), "~x_name", args);
        if (result == null) {
          System.err.println("Rewriting failed"); 
          System.exit(1); 
        } else {
          System.out.println(result);
        }
	 }

     ~*function-defs

     }
     ]|
  with 
      alltd(JavaTranslate)
    ; x_name        := <compute-classname> ()
    ; pkgname       := PackageName([Id("org.strategoxt.compiler.tests.cases")])
    ; function-defs := <map(translate-outer-definition)> defs
    ; lifted-defs   := <bagof-Fragment> "LiftedDef"
    ; cnstr-decls   := <bagof-Fragment> "ConstructorDecl"
    ; cnstr-inits   := <bagof-Fragment; concat> "ConstructorInit"
    ; cnst-decls    := <bagof-Fragment> "ConstDecl"
    ; cnst-inits    := <bagof-Fragment; concat; reverse> "ConstInit"
    

rules
  translate-inner-definition = translate-any-definition(|[Final(), Static()])
  translate-outer-definition = translate-any-definition(|[Public(), Static()])
  
  translate-any-definition(|modifiers):
    SDefT(x, strats*, terms*, s) -> type-dec |[ 
            mod* class x extends Strategy {
              private static x instance = new x();
              
              protected x() {}
              
              public static final x getInstance() {
                return instance;
              }
              
              public static final void setInstance(x strategy) {
                instance = strategy;
              }
              
              public IStrategoTerm invoke(Context context, IStrategoTerm term, param*) {
                // TODO: if (instance != this) instance.invoke(term, arg*);
                ITermFactory termFactory = context.getFactory();
              
                int nextState = 1;
                Root:
                while(true)
                  switch(nextState) {
                  case 0: return null;
                  case 1: ~stm:body; break Root;
                  }
                return term;
              }
              
              @Override
              public IStrategoTerm invokeDynamic(Context context, IStrategoTerm term, IStrategy[] sargs,
                  IStrategoTerm[] targs) {
                // FIXME: Pass strategy and term args
                return invoke(context, term);
              }
            }
         ]|
    with
      prelude(|"translate-any-definition")
    ; mod*    := modifiers
    ; strats' := <map(translate-parameter)> strats*
    ; terms'  := <map(translate-parameter)> terms*
    ; param*  := <conc> (strats', terms')
    ; body    := <translate-body(|Next(), (Id("Root"), Lit(Deci("0"))))> s
  
  translate-any-definition(|modifiers):
    ExtSDef(_, _, _) -> Semicolon()
    where prelude(|"translate-any-definition")

  // XXX: add Strategy parameter
  translate-parameter :
  VarDec(x, ConstType(_)) -> param |[ final IStrategoTerm x ]|
  where prelude(|"translate-parameter - term")
  
  translate-parameter : 
  VarDec(x, FunType(_, _)) -> param |[ final Strategy x ]|
  where prelude(|"translate-parameter - strategy")
  
strategies
  prelude(|msg) = where(?x#(_) ; log(|Debug(), [ msg, " : ", x ]))
  
  verify-SwitchGroup(|stratname) = ?SwitchGroup(_, _) <+ log(|Error(), [ stratname, ": not a SwitchGroup!" ])
  
  /** @type Strategy -> List(SwitchGroup) */
  translate-body(|S,F) = 
    prelude(|"translate-body") 
    ; translate-strategy(|S,F)
    //; //verify-SwitchGroup(|"translate-body")
      //debug(!"after translate-body :")

  translation-failed = ?x#(_) ; log(|Error(), [ "Unknown constructor type ", x ]) ; fail
  
  translate-Scope(|S, F) : 
  Scope(vars, e) -> Block(<conc> (vars', [<translate-strategy(|S,F)> e]))
  where prelude(|"translate-Scope") ;
    <map(\ x -> bstm |[ IStrategoTerm x = null; ]| \)> vars => vars'
    
  /** @type Id -> Stm */
  translate-Id(|S,F) :
    Id() -> <Goto> S
  where prelude(|"translate-Id")
    
  /** @type Id -> Stm */
  translate-Fail(|S,F) :
    Fail() -> <Goto> F
  where prelude(|"translate-Fail")

  /** @type Seq -> Stm */
  translate-Seq(|S, F) :
    Seq(s1, s2) -> 
    bstm |[ 
      { 
        ~stm:<translate-strategy(|Next(), F)> s1 
        ~stm:<translate-strategy(|Next(), F)> s2 
      } 
    ]|
  where prelude(|"translate-Seq")

  /** @type GuardedLChoice -> Block */  
  translate-GuardedLChoice(|S,F) :
    GuardedLChoice(s1, s2, s3) ->
      Block(bstm* |[
      nextState = ~n0;
      IStrategoTerm ~x = null ; 
      ~id:label:
      while(true)
      switch(nextState) {
          case ~n0: 
           ~x = term ; 
            ~stm:<translate-strategy(|n1, n2)> s1
          case ~n1: 
            ~stm:<translate-strategy(|S, F)> s2
            ~stm:<Goto> S
          case ~n2: 
            term = ~<!ExprName(x)>; 
            ~stm:<translate-strategy(|S, F)> s2
            ~stm:<Goto> S
      }
    ]|)
   where prelude(|"translate-GuardedLChoice")
     ; <not(Next())> S
     ; x     := Id(<newname> "trm")
     ; label := <newlabel>
     ; n0    := (label, <newcase>)
     ; n1    := (label, <newcase>)
     ; n2    := (label, <newcase>)

  translate-GuardedLChoice(|S,F) :
    GuardedLChoice(s1, s2, s3) ->
    Block(bstm* |[ {
      nextState = 0;
      IStrategoTerm ~x = null ;
      boolean ~id:loop = true; 
      ~id:label:
      while(~expr:ExprName(loop))
      switch(nextState) {
        case 0: 
          ~x = term ; 
          ~stm:<translate-strategy(|Next(), (label, Lit(Deci("2"))))> s1
        case 1: 
          ~stm:<translate-strategy(|S, F)> s2 ;
           break;
        case 2: term = ~<!ExprName(x)>; 
           ~stm:<translate-strategy(|S, F)> s2 ; 
           break;
       }
       }
    ]|)
  where prelude(|"translate-GuardedLChoice") 
    ; !S => Next()
    ; x     := Id(<newname> "trm")
    ; label := <newlabel> ()
    ; loop  := <newloop> ()

  /** @type -> Lit(Deci(String))) */
  newcase  = !Lit(Deci("1001"))
  newlabel = !Id(<newname> "label")
  newloop  = !Id(<newname> "loop")

  goto(|label) = <Goto> label 
  
  Goto: Next() -> Empty()
  Goto: (scopeLabel, caseNumber) -> bstm |[ { nextState = ~caseNumber; continue ~scopeLabel; } ]|
  
  translate-strategy(|S,F) = 
       ( 
       translate-CallT(|S,F) ; fc
    <+ translate-Build(|S,F) ; fc
    <+ translate-Seq(|S,F) ; fc
    <+ translate-PrimT(|S,F) ; fc
    <+ translate-Id(|S,F) ; fc
    <+ translate-Fail(|S,F) ; fc
    <+ translate-GuardedLChoice(|S,F) ; fc
    <+ translate-Scope(|S,F) ; fc
    <+ translate-Match(|S,F) ; fc
    <+ translate-Let(|S,F) ; fc
//    <+ debug(!"translate-strategy failed ") ; fail
       ) 
//    ; debug(!"translated ")

  translate-Let(|S,F): 
  Let(defs, body) -> Block([<translate-strategy(|S,F)> body])
  with
      prelude(|"translate-Let")
    ; debug
    ; <map(fragment(translate-inner-definition | "LiftedDef"))> defs
 
  make-strategy-class : 
  (x, body) -> type-dec |[ 
        static final class x extends Strategy { 
          private static final x instance = new x();
          
          public static final x getInstance() {
            return instance;
          }
          
          @Override
          public IStrategoTerm invoke(Context context, IStrategoTerm term) { 
            int nextState = 1;
            Root:
            while(true)
              switch(nextState) {
                case 0: return null;
                case 1: ~stm:body break Root;
              }
              return term;
          }
          
          @Override
          public IStrategoTerm invokeDynamic(Context context, IStrategoTerm term, IStrategy[] sargs, IStrategy[] targs) {
            invoke(context, term);
          }
        } 
    ]|
  
  translate-CallT(|S,F):
    CallT(SVar(x), s*, t*) -> Block(call)
    with
      prelude(|"translate-CallT"); 
      // FIXME do actual lifting
      args1  := <map(\ x -> (<newname> "lifted", <translate-strategy(|S,F)> x) \)> s*
    ; <map(fragment(make-strategy-class|"LiftedDef"))> args1
//  ; bodies := <map(make-strategy-class)> args1
    ; args2  := <map(java-construct-term)> t* => args2
    ; <debug(!"args1  ")> args1
//  ; <debug(!"bodies ")> bodies
    ; <debug(!"args2  ")> args2
    ; e*  := <conc> (<map(!Field(ExprName(Id(<Fst>)),Id("instance")))> args1, args2)
    ; <debug(!"e*     ")> e*
    ; <debug(!"F      ")> F
    ; <debug(!"S      ")> S
      // TODO: Call invokeDynamic as required
    ; call := bstm* |[ 
	             term = x.getInstance().invoke(e*,term); 
	             if(term == null) 
	               ~stm:<Goto> F
	             else
	               ~stm:<Goto> S
	           ]|
    ; <debug(!"call      ")> call

  java-ArgToCl :
    CallT(SVar(f{Closure(x), Frames([])}), [], []) -> expr|[ x ]|
//    Strat|[ f~{Closure(x),Frames([])}(|) ]| -> exp|[ x ]|

  java-ArgToCl :
    CallT(SVar(f{Offset(i),Closure(x),Frames([])}), [], []) -> expr|[ x ]|
//    Strat|[ f~{Offset(i),Closure(x),Frames([])}(|) ]| -> exp|[ x ]|

  // passing a remote closure to a function

  java-ArgToCl :
//    Strat|[ f~{Offset(i),Closure(x),Frames(frames)}(|) ]| -> 
    CallT(SVar(f{Offset(i),Closure(x),Frames(frames)}), [], []) ->
    expr|[ sl_fun_cl(~expr:<mk-int-const> i, ~expr:sl) ]|
  with prelude(|"java-ArgToCl") ;
    sl := <not([]); java-frames-to-static-link> frames


  translate-PrimT(|S,F) :
    PrimT(f, s*, t*) -> 
    <java-check-unbound-args(|F,t*)>  
    Block(bstm* |[ 
      term = context.invokePrimitive("~f", term, new IStrategy[] { ~*args1 }, new IStrategoTerm[] { ~*args2 });
      if (term == null) ~<Goto> F else ~<Goto> S
    ]|)
  with prelude(|"translate-PrimT") ;
       <debug(!"s* ")> s*
     ; args1 := <map(java-ArgToCl)> s*
     ; <debug(!"foo ")> t*
     ; args2 := <map(java-construct-term)> t*
     ; <debug(!"zap ")> args2
     ; args  := <conc> (args1, args2)
  
/* ================================ Build  */

  translate-Build(|S,F):
    Build(t) -> 
    <java-check-unbound-args(|F,t)> 
    Block(bstm*|[
               ~*decs
               ~*stms
               term = ~expr:e;
               ~stm:<Goto> S
               ]|)
  where prelude(|"translate-Build")
    ; {| java-CheckList, java-FailureLabel : 
	     rules( java-FailureLabel : _ -> F )
	   ; <java-construct-term> t => e
	   ; debug(!"term ")
	   ; bagof-java-CheckList; unzip; (id, reverse; concat) => (decs, stms)
	   ; debug(!"bagof-CheckList ")
      |} 

  java-check-unbound-args(|F,t) =
    try(java-CheckUnboundArgs(|F,t))

  java-CheckUnboundArgs(|F,t) :
    s -> 
    stm|[ 
      if(~expr:e)
        ~stm:<Goto> F
      else  
        ~stm:s
    ]|
    where prelude(|"CheckUnboundArgs") ;
      <collect({?Var(_){t*}; where(<not(["bound"])> t* )})> t
    ; debug(!"unbound ")
	; not([])
	; foldr1(!expr|[ ~expr:<Hd; java-ConstructVar> == null ]|
	        ,!expr|[ ~expr:<Fst; java-ConstructVar> == null || ~expr:<Snd> ]|) => e
    ; debug(!"expr ")

/**
 * Term construction with constant term caching
 */
rules 

  java-init-term-caching =
    where(
         t1 := Op("Nil", [])
       ; t2 := Anno(Op("Nil", []), Op("Nil", []))
       ; e  := expr|[ atermFactory.makeList() ]|
    )
    ; rules( java-Cache : t1 -> e )
    ; rules( java-Cache : t2 -> e )

  /** @type Term -> Exp */
  java-construct-term = prelude(|"java-construct-term");
       java-Cache 
    <+ java-ConstructAnnoVar
    <+ java-ConstructVar
    <+ !(<id>, <java-ConstructTerm>)
       ; (java-CacheConstant <+ Snd)

  java-CacheConstant :
    (t, e1) -> e2
  where prelude(|"CacheConstant") ; <debug> (t, "->", e1) ;
      <(Op(id, map(java-Cache)); proper-list 
            + Anno(java-Cache, java-Cache) 
            + Int(id) + Str(id) + Real(id) )> t
      ; x  := <newname> "term"
      ; e2 := ExprName(Id(x))
      ; rules( java-Cache : t -> e2 )
      ; !(x, e1)
      ; fragment(java-DeclareTermId|"ConstDecl")
      ; fragment(java-InitTermId|"ConstInit")

// helpers

strategies
    
  is-TupleOp = ?Op("", _)

  java-DeclareTermId :
    (x, e) ->
    class-body-dec|[ static IStrategoTerm ~<!Id(x)>; ]|

  java-InitTermId :
    (x, e) -> bstm*|[ x = e; ]|

  java-mk-int-const = prelude(|"mk-int-const") ;
    !Lit(Deci(<try(int-to-string)>))
    

rules

  java-ConstructTerm :
    Int(i) -> 
    expr|[ 
      termFactory.makeInt(~expr:<java-mk-int-const> i)
    ]|
  where prelude(|"java-ConstructTerm - Int")

  java-ConstructTerm :
    Real(x) -> 
    expr|[ 
      termFactory.makeReal(~expr:Lit(Float(<try(real-to-string)> x)))
    ]|
  where prelude(|"java-ConstructTerm - Real")

  java-ConstructTerm :
    Str(x) -> 
    expr|[ 
      termFactory.makeString(~expr:Lit(String([Chars(<escape> x)])))
    ]|
  where 
      prelude(|"java-ConstructTerm - Str")

  java-ConstructVar :
    x@Var(_) -> e
  with 
      prelude(|"java-ConstructVar") 
    ; e := <java-VarToReadRef> x
    ; debug(!"var to read ref: ")

  java-ConstructAnnoVar :
    Anno(Var(x), pt2) -> 
    expr|[ 
      termFactory.annotateTerm(~expr:e, ~expr:<java-construct-term> pt2)
    ]|
  where
      prelude(|"java-ConstructAnnoVar") 
    ; <java-ConstructVar> Var(x) => e

  java-ConstructTerm :
    Anno(pt1, Op("Nil", [])) -> <java-construct-term> pt1
  where 
      prelude(|"java-ConstructTerm - pt1, Op(Nil, [])") 
    ; <not(?Var(_))> pt1

  java-ConstructTerm :
    Anno(pt1, pt2) -> 
    expr|[ 
      termFactory.setAnnotations(~expr:<java-construct-term> pt1, ~expr:<java-construct-term> pt2)
    ]|
  where 
      prelude(|"java-ConstructTerm - pt1{pt2}")
    ; <not(?Op("Nil", []))> pt2
    ; <not(?Var(_))> pt1

//  java-ConstructTerm :
//    |[ _ t ]| -> <java-construct-term> t

  java-ConstructTerm :
    Op(c, t*) -> expr|[ termFactory.makeAppl(~*[cn | args]) ]|
  where
      not(is-ListOp + is-TupleOp)
  with
      prelude(|"java-ConstructTerm - Op(c, t*)")
    ; cn   := ExprName(Id(<java-ConstructorName> (c, <length> t*)))
    ; args := <map(java-construct-term)> t*
    ; debug(!"not listop")
    
  java-ConstructTerm :
    Explode(t1, t2) -> 
    expr|[ 
      SSL_mkterm(~expr:<java-construct-term> t1, ~expr:<java-construct-term> t2) 
    ]|
  with 
      prelude(|"java-ConstructTerm - Explode")

  java-ConstructTerm : 
    Op("", args) -> expr|[ termFactory.makeTuple(~*args') ]|
  with
      prelude(|"java-ConstructTerm - Op(\"\", [...])") 
    ; args' := <map(java-construct-term)> args
    
  java-ConstructTerm :
    Op("Nil", []) -> expr|[ termFactory.makeList() ]|
  where prelude(|"java-ConstructTerm - Op(Nil, [])")

  java-ConstructTerm :
    Op("Cons", [t1, t2]) -> 
    expr|[ 
      ((IStrategoList)(~expr:e)).prepend(~expr:<java-construct-term> t1)
    ]|
  with
      prelude(|"java-ConstructTerm - Cons")
    ; <java-construct-term> t2
	    ; if <not(Anno(is-ListOp,id))> t2 then 
	        java-check-aterm-list 
	      else
	          if ?Id(_) then
	            !|[ (IStrategoList) ~expr:<id> ]|
	          else
	            try(java-UncastATerm)
	          end
	          ; try(java-CastATermListSetAnnotations)
	        end => e

  java-check-aterm-list :
    e -> expr|[ (IStrategoList)~id:x ]|
  where 
      prelude(|"java-check-aterm-list") 
    ; x := Id(<newname> "trm")
	; F := <java-FailureLabel> ()
    ; stms := 
	     (bstm |[ IStrategoList ~id:x; ]|, 
          bstm*|[ ~id:x = checkTermList(~expr:e);
                  if(~id:x == null) ~stm:<Goto> F ]|)
    ; rules( java-CheckList :+ _ -> stms )

  java-CastATerm :
    e -> expr|[ (IStrategoTerm) ~expr:e ]|

  java-CastATermListSetAnnotations :
    expr|[ termFactory.setAnnotations(~expr:e1, ~expr:e2) ]| -> expr|[ (IStrategoList) termFactory.setAnnotations(~expr:e1, ~expr:e2) ]|

  java-UncastATerm :
    expr|[ (IStrategoTerm)~expr:e ]| -> e

  java-VarToReadRef : 
    Var(x{}) -> ExprName(Id(x{}))

  java-VarToReadRef :
    Var(x{Frames([])}) -> ExprName(Id(x{}))

  java-VarToReadRef :
    Var(x{Offset(i), Frames([])}) -> ExprName(Id(x{}))

  java-VarToReadRef : 
    Var(x{Offset(i), Frames(frames)}) -> 
    expr|[ sl_readvar(~expr:<java-mk-int-const> i, ~expr:sl) ]|
  with
    sl := <not([]); java-frames-to-static-link> frames

signature
  constructors
  
    Offset: Int -> Term
    Frames: List(Term) -> Term
    
/**
 * Signatures
 */
strategies

  JavaTranslate =
      fragment(JavaDeclareConstructor|"ConstructorDecl")
    ; fragment(JavaInitConstructorQ <+ JavaInitConstructor|"ConstructorInit")

  JavaDeclareConstructor :
    OpDecl(c, t) -> class-body-dec|[ static IStrategoConstructor ~id:Id(csym); ]|
  where 
      prelude(|"DeclareConstructor") 
    ; n    := <Arity> t
    ; csym := <concat-strings> ["sym_", <cify> c, "_", <int-to-string> n]
    ; rules(java-ConstructorName : (c, n) -> csym)

  JavaInitConstructorQ :
    OpDecl(c, t) ->
    bstm*|[
      ~id:csym = ATmakeSymbol(~expr:Lit(String([Chars(<double-quote> c')])), 
			                  ~expr:<java-mk-int-const> ar,
			                  ATtrue);
    ]|
  where 
      <string-as-chars(unescape-chars; un-double-quote-chars)> c => c' 
    ; ar   := <Arity> t
    ; csym := <java-ConstructorName> (c, ar)

  JavaInitConstructor :
    OpDecl(c, t) ->
    bstm*|[
      ~id:csym = termFactory.makeConstructor(~expr:Lit(String([Chars(c)])), 
                                             ~expr:<java-mk-int-const> ar);
    ]|
  where 
      ar   := <Arity> t
    ; csym := Id(<java-ConstructorName> (c, ar))

/* ================================ Match */
strategies

  translate-Match(|S,F) :
    Match(t) -> result
  with prelude(|"translate-Match - Match/1")
     ; <debug(!"term ")> t
     ; result := <translate-Match(|S,F)> Match(t, ExprName(Id("term")))

  translate-Match(|S,F) :
    Match(Wld(), trm) -> <Goto> S
  with prelude(|"translate-Match - Match(Wld,trm)")

  translate-Match(|S,F) :
    Match(As(t1,t2), trm) ->
    stm|[ 
      { 
        stm_match1
        stm_match2
      }
    ]|
  with prelude(|"translate-Match - Match(As(t1,t2), trm)")
     ; stm_match1 := <translate-Match(|Next(),F)> Match(t1,trm)
     ; stm_match2 := <translate-Match(|S,F)> Match(t2,trm)

  translate-Match(|S,F) :
    Match(Var(x){"unbound"}, trm) ->
    stm|[ { ~e = ~expr:trm; ~stm:<Goto> S } ]|
  with prelude(|"translate-Match - Match(Var(x){unbound}, trm)");
    ExprName(e) := <java-VarToWriteRef> x

  translate-Match(|S,F) :
    Match(Var(x){"bound"}, trm) ->
    stm|[ 
      if( ~expr:e == ~expr:trm ) 
        ~stm:<Goto> S
      else 
        ~stm:<Goto> S
     ]|
  with
      prelude(|"translate-Match - Match(Var(x){bound}, trm)")
    ; e := <java-VarToReadRef> Var(x)

  translate-Match(|S,F) :
    Match(Var(x){"(un)bound"}, expr) ->
    stm|[ 
      if(~expr:e1 == null) {
          lhs = ~expr:expr;
          ~stm:<Goto> S
      } else if(!(~expr:e1).equals(~expr:expr)) {
        ~stm:<Goto> F
      } else
        ~stm:<Goto> S
    ]|
  with 
      prelude(|"translate-Match - Match(Var(x){(un)bound})")
    ; <debug(!"XX ")> Var(x)
    ; e1 := <java-VarToReadRef> Var(x)
    ; <debug(!"PP")> ()
	; e2 := <java-VarToWriteRef> x
	; if !e2 => ExprName(_) then
        lhs := e2
	  else
        lhs := e2
	  end
    ; <debug(!"e1 ")> e1
    ; <debug(!"e2 ")> e2
    ; <debug(!"lhs  ")> lhs

  java-VarToWriteRef :
    x{} -> ExprName(Id(x))

  java-VarToWriteRef :
    x{Frames([])} -> ExprName(Id(x))

  java-VarToWriteRef : 
    x{Offset(i), Frames([])} -> ExprName(Id(x))

  java-VarToWriteRef : 
    x{Offset(i), Frames(frames)} -> 
    expr|[ sl_readvar(~expr:<java-mk-int-const> i, ~expr:sl).var ]|
  where 
    <not([]); java-frames-to-static-link> frames => sl

  java-frames-to-static-link = prelude(|"java-frames-to-static-link") ; 
    (Tl <+ err(|"*** frames-to-static-link error: "); fail)
    ; foldr(!expr|[ sl ]|,
	        !expr|[ sl_up(~expr:<Snd>) ]|)

  translate-Match(|S,F) :
    Match(Var(x){}, trm) -> <translate-Match(|S,F)> Match(Var(x){"(un)bound"}, trm)
  with prelude(|"translate-Match - Match(Var(x){}")
    ; <debug(!"ZZ ")> x

  translate-Match(|S,F) :
    Match(Explode(t1, t2), expr) ->
    stm|[ 
    {
      IStrategoTerm ~id:n0, ~id:n1;
	 ~id:n0 = SSL_get_constructor(~expr:expr);
	 ~id:n1 = SSL_get_arguments(~expr:expr);
	 ~stm:(<translate-Match(|Next(),F)> Match(t1, ExprName(n0)))
	 ~stm:(<translate-Match(|S     ,F)> Match(t2, ExprName(n1)))
     } ]|
  with
      !expr => ExprName(Id(_))
    ; prelude(|"translate-Match - Match(Explode)") 
    ; n0 := Id(<newname> "trm")
    ; n1 := Id(<newname> "trm")

  translate-Match(|S,F) =
       ( translate-MatchNoAnno(|S,F)
      <+ translate-MatchAnno(|S,F) ) 
      ; where(<debug> "after anno matching")

  translate-MatchNoAnno(|S,F) :
    Match(Anno(pt1, Wld()), expr) -> result
    with prelude(|"translate-MatchNoAnno - Match(pt1{^_}")
       ; result := <translate-Match(|S,F)> Match(pt1, expr)

  translate-MatchAnno(|S,F) :
    Match(Anno(pt1, pt2), expr) -> 
    stm|[ 
      { 
        ~stm:stm0
        IStrategoList ~id:x = ~expr.getAnnotations();
        ~stm:stm1
      } 
    ]|
  where 
      <not(Wld)> pt2
  with
      prelude(|"translate-MatchAnno - Match(pt1{^pt2})")
    ; <debug(!"pt1 ")> pt1
    ; <debug(!"pt2 ")> pt2
    ; x    := Id(<newname> "trm")
    ; stm0 := <translate-Match(|Next(),F)> Match(pt1, expr)
    ; stm1 := <translate-Match(|S     ,F)> Match(pt2, ExprName(x))

  /**
   * @todo  String constants should probably be cached, just like constructor names
   */
  translate-Match(|S,F) :
    Match(Str(x), expr) -> 
    stm|[
      if((~expr:expr).getTermType() == IStrategoTerm.STRING && ((IStrategoString)(~expr:expr)).getValue().equals(~expr:x'))
        ~stm:<Goto> S
      else 
        ~stm:<Goto> F
    ]|
  with
      !expr => ExprName(Id(_))
    ; prelude(|"translate-Match - Match(Str)")
    ; x' := Lit(String([Chars(<escape> x)]))

  translate-Match(|S,F) :
    Match(Int(i), expr) -> 
    stm|[
      if((~expr:expr).getTermType() == IStrategoTerm.INT && ((IStrategoInt)~expr:expr).getValue() == ~expr:x')
        ~stm:<Goto> S
      else 
        ~stm:<Goto> F
    ]|
  with
      !expr => ExprName(Id(_))
    ; prelude(|"translate-Match - Match(Int)")
    ; <debug(!"zz ")> expr
    ; x' := <java-mk-int-const> i
    ; debug(!"xx ")

  translate-Match(|S,F) :
    Match(Real(i),expr) -> 
    stm|[
      if((~expr:expr).getTermType() == IStrategoTerm.REAL && ((IStrategoReal)~expr:expr).getValue() == ~expr:x')
        ~stm:<Goto> S
      else 
        ~stm:<Goto> F
    ]|
  with
      !expr => ExprName(Id(_))
    ; prelude(|"translate-Match - Match(Real)")
    ; x' := Lit(Float(<try(real-to-string)> i))

  /**
   * Translate a match of an Op, but not a Cons/2 or Nil/0.
   */
  translate-Match(|S,F) :
    Match(op@Op(c, ts), expr) ->
    stm|[
      if( matchConstructor(~expr:expr, e_cname) )
        { 
          ~bstm*:match-args
          ~stm:<Goto> S
        }
      else
        ~stm:<Goto> F
    ]|
  where
       <not(is-ListOp)> op
  with
       prelude(|"translate-Match - Match(Op)");
       e_cname := ExprName(Id(<java-ConstructorName> (c, <length> ts)))
     <+
       <is-TupleOp> op;
       e_cname := Lit(Null())
     <+
       <fatal-err(|"Undeclared constructor")> (c, <length> ts)
  with
       (match-args, _) := <thread-map(\ (t,i) ->
             (
               <java-MatchArg1(|Next(),F) <+ java-MatchArg2(|Next(),F)> 
                  (t, expr|[ (~expr:expr).getSubterm(~expr:<java-mk-int-const> i) ]|)
             , <add>(i, 1)
             )
           \ )> (ts,0)

  java-MatchArg1(|S,F) : 
    (Var(x){t*}, expr) -> result
  with 
      prelude(|"java-MatchArg1")
    ; result := <translate-Match(|S,F)> Match(Var(x){t*}, expr)
    

  java-MatchArg2(|S,F) : 
    (t, expr) -> 
    stm|[ 
      { 
         IStrategoTerm x = ~expr:expr; 
         stm
      } 
    ]|
  with
      prelude(|"java-MatchArg2")
    ; x   := <newname> "trm"
    ; stm := <translate-Match(|S,F)> Match(t, ExprName(Id(x)))

  translate-Match(|S,F) :
    Match(Op("Nil", []), expr) ->
    stm|[
      if((~expr:expr).getTermType() == IStrategoTerm.LIST && ((IStrategoList)~expr:expr).isEmpty())
        ~<Goto> S
      else 
        ~<Goto> F
    ]|
  with prelude(|"translate-Match - Match(Op(Nil))")


  translate-Match(|S,F) :
    Match(Op("Cons", [hd, tl]), expr) ->
    stm|[
      if((~expr:expr).getTermType() == IStrategoTerm.LIST && !((IStrategoList)~expr:expr).isEmpty())
        { 
          ~bstm*:match-args 
          ~stm:<Goto> S
        }
      else
        ~stm:<Goto> F
    ]|
  with 
    <map(java-MatchArg1(|Next(),F) <+ java-MatchArg2(|Next(),F))> 
	   [ (hd, expr|[ ((IStrategoList)~expr:expr).head() ]|)
	   , (tl, expr|[ ((IStrategoList)~expr:expr).tail() ]|) ] => match-args
