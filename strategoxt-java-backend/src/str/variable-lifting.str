/**
 * Lifting of local variables.
 *
 * @author Lennart Kats
 */
module variable-lifting

rules
 
  collect-lifted-tvars =
    tvars
  ; filter(where(
      get-annos
    ; one(?Frames([_ | _]))
    ))
  ; make-set-with-annos
  
  collect-lifted-svars =
    svars
  ; filter(
      where(
        get-annos
      ; one(?ApplyClosure())
      )
    ; not(is-tail-call)
    )
  ; make-set-with-annos
  
  make-set-with-annos =
    foldr(![], union(strip-annos; eq), ![<id>])
  
  is-tail-call =
    with(
      x := <id>{}
    )
  ; get-annos
  ; one(?Frames([x]))
  
  lifted-vars-to-constructor(|x_name) :
    (svar*, tvar*) ->
    |[ public x_name(param_svar*, param_tvar*) {
         bstm*
       }
    ]|
    with
      param_svar* := <map(\x -> param |[ IStrategy x ]|\)> svar*
    ; param_tvar* := <map(\x -> param |[ ITermReference x ]|\)> tvar*
    ; var*        := <conc> (tvar*, svar*)
    ; bstm* := <map(\x -> |[ this.x = x; ]|\)> var*
