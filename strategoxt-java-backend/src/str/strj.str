/**
 * Stratego compiler for Java
 *
 * @author Lennart Kats
 */
module strj

imports
  libstrc
  libstratego-lib
  libstratego-xtc
  libstratego-gpp
  libjava-front
    
  strj-options
  s2j

strategies

  main-strj =
    strj-parse-options
  ; log-timed(
      xtc-input(strj)
    | "Compilation succeeded", 1
    )
  ; <exit> 0
  <+
    log(|Error(), ["Compilation failed (", <run-time ; real-to-string(|2)>, " secs)"])
  ; <exit> 1
  
  strj =
    if <get-extension> <get-config> "-i" => "ctree" then
      read-from
    <+
      <get-config> "-i"
    ; err(|"File does not exist")
    ; fail
    else
      strc-front-end
    ; strc-optimize
    end
  ; log-timed(s2j         | "Back-end succeeded", 1)
  ; log-timed(output-java | "Output succeeded", 1)

  output-java :
    ast -> file
    with
      box  := <pp-java5-to-abox>
    ; file := <fopen> (<get-config> "-o", "w")
    ; <box2text-stream(|80, file)> box
    ; <fclose> file
