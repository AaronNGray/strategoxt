/**
 * Java output postprocessing.
 *
 * @author Lennart Kats
 */
module java-postprocess

imports
  Java-EBlock

strategies

  java-relabel =
    rules(CurrentLabel := "NO_OUTER_LABEL")
  ; rec rec(
      java-relabel-outer-label
    <+
      java-relabel-stm
    <+
      all(rec)
    )
  
  java-relabel-outer-label :
    |[ OuterFail: { bstm* } ]| -> bstm |[ { bstm'* } ]|
    with
      x_label := <OuterLabel>
    ; {| Relabel:
        rules(
          CurrentLabel := x_label
          Relabel: "OuterFail" -> x_label
        )
      ; bstm'* := <java-relabel> bstm*
      |}
  
  java-relabel-stm :
    |[ x_label: { bstm* } ]| -> |[ x_label': { bstm'* } ]|
    with
      x_label' := <newname> x_label
    ; {| Relabel:
        rules(
          OuterLabel   := <CurrentLabel>
          CurrentLabel := x_label
          Relabel: x_label -> x_label'
        )
      ; bstm'* := <java-relabel> bstm*
      |}
   
   java-relabel-stm :
     x_label -> x_label'
     where
       x_label' := <Relabel> x_label

strategies

  // TODO: Using a consistent variable renaming scheme, eliminate duplicate lifted defs
  //       Or can we do this in the strc middle-end?

  java-simplify =
    bottomup-consnil(repeat(java-simplify-stm + java-simplify-expr))
  
  java-simplify-stm :
    [Empty() | s*] -> s*
    
  java-simplify-stm :
    [Block([ExprStm(s)]) | s*] -> [ExprStm(s) | s*]
  
  java-simplify-stm :
    Block([s, Block(s*)]) -> Block([s | s*])
  
  java-simplify-stm :
    Block([Block(s*)]) -> Block(s*)
  
  java-simplify-stm :
    Block([Block(s*), s]) -> Block([s*, s])
    where
      <is-Stm> s
  
  java-simplify-stm :
    If(e, s1, Empty()) -> If(e, s1)
    
  java-simplify-stm :
    Labeled(l, s*) -> Labeled(l, Block(s*)) // must contain Block
    where
      <not(?Block(_))> s*
  
  java-simplify-stm:
    [Block(s1*) | s2*] -> [s1*, s2*]
    where
      <list-loop(is-Stm)> s1*
  
  java-simplify-stm :
    |[ if (e1 == e2) ; else stm2 ]| -> |[ if (e1 != e2) stm2 ]|
  
  java-simplify-expr :
    |[ new IStrategy[] {} ]| -> |[ EMPTY_STRATEGY_LIST ]|
  
  java-simplify-expr :
    |[ new IStrategoTerm[] {} ]| -> |[ EMPTY_TERM_LIST ]|

strategies

  /**
   * Determines if a Java construct is a non-declaration statement.
   *
   */
  is-Stm =
    ?Try(_, _, _)
    + ?Try(_, _)
    + ?Synchronized(_, _)
    + ?Throw(_)
    + ?Return(_)
    + ?Continue(_)
    + ?Break(_)
    + ?ForEach(_, _, _)
    + ?For(_, _, _, _)
    + ?For(_, _, _, _)
    + ?DoWhile(_, _)
    + ?While(_, _)
    + ?Switch(_, _)
    + ?AssertStm(_, _)
    + ?AssertStm(_)
    + ?If(_, _, _)
    + ?If(_, _)
    + ?ExprStm(_)
    + ?Labeled(_, _)
    + ?Empty()
    + ?Block(_)
