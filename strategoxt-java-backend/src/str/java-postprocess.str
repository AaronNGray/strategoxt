/**
 * Java output postprocessing.
 *
 * @author Lennart Kats
 */
module java-postprocess

imports
  Java-EBlock

strategies

  java-relabel =
    rec rec(
      java-relabel-outer-label
    <+
      java-relabel-success-label
    <+
      java-relabel-stm
    <+
      all-consnil(rec)
    )
  
  java-relabel-outer-label :
    |[ OuterFail: { bstm* } ]| -> bstm |[ { bstm'* } ]|
    with
      {| Relabel, OuterLabel, CurrentLabel:
        x_label  := <OldLabel>
      ; x_label' := <OuterLabel>
      ; rules(
          CurrentLabel := x_label'
          Relabel: x_label -> x_label'
        )
      ; bstm'* := <java-relabel> bstm*
      |}
  
  java-relabel-success-label :
    |[ Success: { bstm* } ]| -> bstm |[ x_label': { bstm'* } ]|
    with
      {| Relabel:
        x_label' := <newname> "Success"
      ; rules(
          Relabel: "Success" -> x_label'
        )
      ; bstm'* := <java-relabel> bstm*
      |}
  
  java-relabel-stm :
    |[ x_label: { bstm* } ]| -> |[ x_label': { bstm'* } ]|
    with
      {| Relabel, OuterLabel, CurrentLabel, OldLabel:
        x_label' := <newname> x_label
      ; rules(
          OuterLabel   := <CurrentLabel <+ !"NO_OUTER_LABEL">
          OldLabel     := x_label
          CurrentLabel := x_label'
          Relabel: x_label -> x_label'
        )
      ; bstm'* := <java-relabel> bstm*
      |}
   
   java-relabel-stm :
     x_label -> x_label'
     where
       x_label' := <Relabel> x_label

strategies

  java-simplify =
    topdown-consnil(
      try(Block(flatten-list))
    ; repeat(java-simplify-stm + java-simplify-expr)
    )

  topdown-consnil(s) =
    rec rec(s; all-consnil(rec))
  
  java-simplify-stm :
    [Empty() | s*] -> s*
  
  java-simplify-stm :
    If(e, s1, Empty()) -> If(e, s1)
  
  java-simplify-stm :
    |[ if (e1 == e2) ; else stm2 ]| -> |[ if (e1 != e2) stm2 ]|
    
  java-simplify-stm :
    bstm* |[ if (TRACES_ENABLED) stm bstm* ]| -> bstm*
    where
      <get-config> "--stacktrace" => 0
    
  java-simplify-stm :
    bstm* |[ if (TRACES_ENABLED) stm bstm* ]| -> bstm* |[ stm bstm* ]|
    where
      <get-config> "--stacktrace" => 1
  
  java-simplify-expr :
    |[ new IStrategy[] {} ]| -> |[ EMPTY_STRATEGY_LIST ]|
  
  java-simplify-expr :
    |[ new IStrategoTerm[] {} ]| -> |[ EMPTY_TERM_LIST ]|
