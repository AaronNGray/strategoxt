module ppgen
imports
  libstratego-lib
  libstratego-xtc
  libstratego-sdf
  sdf-2-pp-table
  pp-pp-table

signature
  constructors
    PlainText : InputType
    ParseTree : InputType
    AST       : InputType

strategies
  
  main-ppgen =
    xtc-io-wrap(ppgen-options, ppgen)

  ppgen = 
    <pre-process-sdf> (<get-input-type>, <id>)
    ; ppgenerate
    ; if not(<get-config> "--output-type" => AST()) then
        pp-pp-table; print-to
      end

  pre-process-sdf : (PlainText(), f) -> <xtc-ensure-file => FILE(<id>); parse-sdf-definition-file> f
  pre-process-sdf : (ParseTree(), f) -> <implode-asfix> f
  pre-process-sdf : (AST()      , f) -> f

  get-input-type =
    <get-config> "--input-type"  <+ !PlainText()

  ppgen-options =
      Option("-A"
      , where(<set-config> ("--input-type", ParseTree()))
      ,	!"-A          Input is parse tree."
      )
    + Option("-a"
      , where(<set-config> ("--input-type", AST()))
      ,	!"-a          Input is abstract syntax tree."
      )
    + Option("-t"
      , where(<set-config> ("--output-type", AST()))
      ,	!"-t          Output is abstract syntax tree."
      )

