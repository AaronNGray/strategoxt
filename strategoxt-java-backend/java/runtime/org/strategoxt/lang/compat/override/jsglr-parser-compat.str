/**
 * Overrides the standard libstratego-sglr SGLR/SGLRI strategies to use JSGR.
 *
 * @author Lennart Kats
 */
module jsglr-parser-compat

imports
  libstratego-lib
  libstratego-sglr
  libstratego-xtc
  jsglr-parser

strategies // parsing and parse tables

  // TODO: new "jsglr enable recovery" strategy

  /**
   * @type SerializedParseTable -> ParseTable
   */
  override open-parse-table =
    jsglr-open-parsetable

  override parse-file(on-open-error, on-parse-error | tbl, start-symbol, path) =
    where(!tbl => ParseTable(internal-tbl))
    ; (<fopen> (<id>, "r") => stream <+ on-open-error; fail)
    ; finally(
        parse-stream(on-parse-error | tbl, start-symbol, path)
      , <fclose> stream
      )

  override parse-xtc-file(on-open-error, on-parse-error |tbl, start-symbol, path) =
    where(!tbl => ParseTable(internal-tbl))
    
    ; let open-stream =
            ?FILE(<id>); (<fopen> (<id>, "r") <+ on-open-error)
            <+ ?stdin(); stdin-stream
            
       in open-stream => stream
          ; finally(
              parse-stream(on-parse-error |tbl, start-symbol, path)
            , <fclose> stream
            )
      end
  
  override parse-xtc-file-report-errors(|tbl, sort) =
    if is-parse-table-open(|tbl) then
      parse-xtc-file(strsglr-perror, strsglr-report-parse-error | tbl, sort)
    else
      // open the parse table and call recursive
      let parse(|tbl') = parse-xtc-file-report-errors(|tbl', sort)
       in open-parse-table-wrap-report-errors(parse| tbl)
      end
    end
  
  override parse-stream(on-parse-error |tbl, start-symbol, path):
    stream -> term
    where
      read-text-from-stream;
      jsglr-parse-string(on-parse-error |tbl, start-symbol, path) => term
  
  override parse-stream-pt(on-parse-error |tbl, start-symbol, path):
    stream -> term
    where
      read-text-from-stream;
      jsglr-parse-string-pt(on-parse-error |tbl, start-symbol, path) => term

  override parse-string(on-parse-error | tbl, start-symbol, path) =
    jsglr-parse-string(on-parse-error | tbl, start-symbol, path)
  
  override parse-string-pt(on-parse-error | tbl, start-symbol, path) =
    jsglr-parse-string-pt(on-parse-error | tbl, start-symbol, path)

  jsglr-parse-string(on-parse-error |tbl, start-symbol, path) =
    jsglr-parse-string-pt(on-parse-error |tbl, start-symbol, path);
    implode-asfix

strategies

  override strsglr-perror =
    err(|"Unable to open file:")

strategies // location information

  override asfix-anno-location = // TODO: asfix-anno-location?
    AsfixAnnoLocationWarned
  <+
    rules(AsfixAnnoLocationWarned: _);
    warn-msg(|"Asfix location annotation not implemented for this platform")

  override asfix-anno-position-info(|name) =
    with(
      <warn(|"Asfix position annotation not supported for this platform; try asfix-anno-location instead")> name
    )

