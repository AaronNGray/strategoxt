/**
 * Overrides the standard libstratego-sglr SGLR/SGLRI strategies to use JSGR.
 *
 * @author Lennart Kats
 */
module jsglr-parser-compat

imports
  libstratego-lib
  libstratego-sglr
  libstratego-xtc
  jsglr-parser

strategies // parsing and parse tables

  /**
   * @type SerializedParseTable -> ParseTable
   */
  override open-parse-table =
    jsglr-open-parsetable

  override parse-file(on-open-error, on-parse-error | tbl, start-symbol, path) =
    where(!tbl => ParseTable(internal-tbl))
    ; (<fopen> (<id>, "r") => stream <+ on-open-error; fail)
    ; finally(
        parse-stream(on-parse-error | tbl, start-symbol, path)
      , <fclose> stream
      )

  override parse-xtc-file(on-open-error, on-parse-error |tbl, start-symbol, path) =
    where(!tbl => ParseTable(internal-tbl))
    
    ; let open-stream =
            ?FILE(<id>); (<fopen> (<id>, "r") <+ on-open-error)
            <+ ?stdin(); stdin-stream
            
       in open-stream => stream
          ; finally(
              parse-stream(on-parse-error |tbl, start-symbol, path)
            , <fclose> stream
            )
      end
  
  override parse-xtc-file-report-errors(|tbl, sort) =
    if is-parse-table-open(|tbl) then
      parse-xtc-file(strsglr-perror, strsglr-report-parse-error | tbl, sort)
    else
      // open the parse table and call recursive
      let parse(|tbl') = parse-xtc-file-report-errors(|tbl', sort)
       in open-parse-table-wrap-report-errors(parse| tbl)
      end
    end
  
  override parse-stream(on-parse-error |tbl, start-symbol, path):
    stream -> term
    where
      read-text-from-stream;
      jsglr-parse-string(on-parse-error |tbl, start-symbol, path) => term
  
  override parse-stream-pt(on-parse-error |tbl, start-symbol, path):
    stream -> term
    where
      read-text-from-stream;
      jsglr-parse-string-pt(on-parse-error |tbl, start-symbol, path) => term

  override parse-string(on-parse-error | tbl, start-symbol, path) =
    jsglr-parse-string(on-parse-error | tbl, start-symbol, path)
  
  override parse-string-pt(on-parse-error | tbl, start-symbol, path) =
    jsglr-parse-string-pt(on-parse-error | tbl, start-symbol, path)

strategies // configuration

  internal set-any-filter(s) =
    if s then
      warn(|"Heuristic filters are not supported by JSGLR")
    end

  override set-filter-direct-eagerness(s) = set-any-filter(s)

  override get-filter-direct-eagerness = fail

  override set-filter-eagerness(s) = set-any-filter(s)

  override get-filter-eagerness = fail

  override set-filter-injection-count(s) = set-any-filter(s)

  override get-filter-injection-count = fail

  override set-filter-priority(s) = set-any-filter(s)

  override get-filter-priority = fail
  
  override set-filter-reject(s) = set-any-filter(s)

  override get-filter-reject = fail

  override set-heuristic-filters(s) = set-any-filter(s)

  override set-default-filters = id
