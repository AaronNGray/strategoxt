/**
 * @author Lennart Kats
 */
module lib/dr-scoping

imports
  libstratego-lib

strategies

  dr-scope-all-verbose(s) =
    dr-scope-all-start
  ; s
  <+
    stack := <prim("SSL_stacktrace_get_all_frame_names")>
  ; if if-verbose3(?true); !true then
      !stack
    ; report-failure(
        log(|Error(), ["Compilation failed (", <run-time ; real-to-string(|2)>, " secs)"])
      ; <exit> 1
      )
    else
      dr-scope-all-end; fail
    end

  dr-scope-all(s) =
    finally(
      dr-scope-all-start; s
    , dr-scope-all-end
    )

  dr-scope-all-start =
    where(
      table := Hashtable(<dr-rule-sets-hashtable>);
      names := ["DrScopedRules" | <hashtable-keys> table];
      
      dr-begin-scopes(|names);
      rules(DrScopedRules := names)
    )

  dr-scope-all-end =
    where(
      table := Hashtable(<dr-rule-sets-hashtable>);
      names := <DrScopedRules>;
      
      dr-end-scopes(|names);
  
      // Remove other, newly defined rules
      <diff> (<hashtable-keys> table, names);
      list-loop(
        { key: ?key;
          <hashtable-remove(|key)> table
        }
      )
    )

