/**
 * Stratego compiler for Java
 *
 * @author Lennart Kats
 */
module strj

imports
  shared-constructors

  libstrc
  libstratego-lib
  libstratego-xtc
  libstratego-gpp
  libjava-front
    
  strj-options
  s2j
  lib/dr-scoping

strategies

  main-strj =
    strj-parse-options
  ; log-timed(
      xtc-input(strj)
    | "Compilation succeeded", 1
    )
  ; <exit> 0
  <+
    log(|Error(), ["Compilation failed (", <run-time ; real-to-string(|2)>, " secs)"])
  ; <exit> 1
  
  strj =
    if <get-extension> <get-config> "-i" => "ctree" then
      read-from
    <+
      <get-config> "-i"
    ; err(|"File does not exist")
    ; fail
    else
      dr-scope-all-verbose(
        strc-front-end
      )
    end
  ; try(mark-always-needed(|<get-config> "-m", 0, 0))
  ; strc-optimize
  ; s2j
  ; log-timed(output-java | "Pretty printing succeeded", 1)

  output-java :
    ast -> file
    with
      box  := <pp-java5-to-abox <+ output-java-error>
    ; file := <fopen <+ fatal-err(|"Could not open output file")> (<get-config> "-o", "w")
    ; <box2text-stream(|80, file)> box
    ; <fclose> file

   // Using XTC for debugging:
   // using something like rtg-format-check(true, true|<java-rtg>) is more expensive atm,
   // because of the import-term involved)
   output-java-error =
      err-msg(|"Internal error: generated Java AST is malformed")
    ; write-to => FILE(file)
    ; try(<xtc-command(!"format-check")> ["--vis", "-i", file, "--rtg", <xtc-find> "Java-15.rtg" | <pass-verbose>])
    ; log(|Critical(), "Fatal errors encountered in generated code")
    ; <xtc-exit> 1
