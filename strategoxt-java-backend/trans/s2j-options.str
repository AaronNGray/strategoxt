/**
 * s2j options
 *
 * @author Lennart Kats
 */
module s2j-options

rules
  
  s2j-options =
     s2j-general-options + s2j-library-options
  
  s2j-library-options =
    ArgOption(
      "-p"
    , <set-config> ("-p", <id>); !()
    , !"-p <name>          Set package name <name>"
    )
  + ArgOption(
      "-la"
    , <post-extend-config> ("-la", [<translate-default-library>]); !()
    , !"-la <name>         Include library <name>"
    )
  + Option(
      "--library" + "--lib"
    , <set-config> ("--library", ()); !()
    , !"--library | --lib  Build a library instead of an application"
    )
  
  s2j-general-options =
    ArgOption(
      "-I" + "--Include"
    , where(<post-extend-config>("-I", ["-I", <id>]))
    ; !()
    , !"-I d | --Include d Include modules from directory d"
    )
  + ArgOption(
      "--stacktrace" + "-s"
    , <set-config> ("--stacktrace", <string-to-int; (0 + 1 + 2)>); !()
    , <conc-strings> ("--stacktrace i | -s i  Enable stacktracing ",
                      "(0 = no, 1 = yes [default], 2 = only if assertions disabled for this class)")
    )
  + ArgOption(
      "-D"
    , <post-extend-config> ("-D", [<parse-define-option>]); !()
    ,!"-D name=value      Define a constant value strategy"
    )
  + ArgOption(
      "-sc"
    , <set-config> ("-sc", <id>); !()
    ,!"-sc <on|off>       Assume all term constructors are shared (default: on)"
    )
  + ArgOption(
      "-O"
    , <set-config>("-O", <string-to-int; strc-set-opt-level>); !()
    , !"-O n              Optimization level (0 = no optimization)"
    )
    
  translate-default-library =
    if "stratego-lib" + "stratego-xtc" + "stratego-sglr" + "stratego-rtg" + "stratego-aterm" + "stratego-gpp" + "strc" then
      <conc-strings> ("org.strategoxt.lib", <id>)
    end
  ; if not(is-substring(!".")) then
      err(|"Imported libraries must include a package name")
    ; fail
    end
 
  parse-define-option =
    risky(
      string-tokenize(|['='])
    ; \[name, value] -> (name, value)\
    | "option -D must be followed by name=value pair")
  ; risky(
      (id, read-from-string; trm-explode; raise-in-build; stratego-desugar)
    | "illegal constant value for option -D"
    )
 
  s2j-init-options =
    if not(<get-config> "--stacktrace") then
      <set-config> ("--stacktrace", 1)
    end
