/**
 * @author  Martin Bravenboer <martin@cs.uu.nl>
 */
module regular-options
imports tool-doc


strategies
  input-to-rtg-nf =
    (  get-rtg-nf-config
    <+ get-rtg-config; xtc-rtg-front-end
    <+ fatal-err(|"No tree grammar specified")
     )
   ; ?FILE(<id>)
			    

/**
 * rtg-nf options
 */
strategies

  rtg-nf-option(|descr) =
    ArgOption("--rtg-nf"
    , where(<set-config> ("--rtg-nf", <id>))
    , !HelpString("--rtg-nf file", descr)
    )

  /**
   * Returns the specified rhg-nf or terminates execution if there was not rhg specified.
   *
   * @type  _ -> RHG
   */
  read-rtg-nf-config =
      (<get-config> "--rtg-nf" <+ fatal-err(|"no rtg specified. Use --rtg-nf to specify an rtg."))
    ; ReadFromFile
    <+ fatal-err(|<conc-strings> ("cannot read rtg-nf from specified file: ", <id>))

  /**
   * Returns the specified rtg-nf. Fails if there was not rhg specified.
   *
   * @type  _ -> FILE
   */
  get-rtg-nf-config =
    <get-config> "--rtg-nf"; !FILE(<id>)

strategies

  rtg-option(|descr) =
    ArgOption("--rtg"
    , where(<set-config> ("--rtg", <id>))
    , !HelpString("--rtg file", descr)
    )

  /**
   * Returns the specified rtg. Fails if there was not rtg specified.
   *
   * @type  _ -> FILE
   */
  get-rtg-config =
    <get-config> "--rtg"; !FILE(<id>)

strategies

  start-nonterm-option(|msg) =
    ArgOption("-s" + "--start"
    , set-start-nonterm
    , !HelpString("-s|--start s", msg)
    )

  set-start-nonterm =
    <set-config> ("start-nonterm", <id>)

  get-start-nonterm = 
    <get-config> "start-nonterm"

  pass-start-nonterm =
    !["--start", <get-config> "start-nonterm"] <+ ![]

strategies

  fast-option(|msg) =
    Option("--fast"
    , enable-fast-mode
    , !HelpString("--fast", msg)
    )

  enable-fast-mode =
    <set-config> ("--fast", "true")

  if-fast-mode(s) =
    if <get-config> "--fast" => "true" then s else id end

  pass-fast-mode =
    ![]
    ; if-fast-mode(!["--fast"])

/**
 * Workaround for tools that don't import xtc.
 */
signature
  constructors
    FILE : String -> FILE



