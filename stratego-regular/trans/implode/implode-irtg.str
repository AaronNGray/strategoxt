module implode-irtg
imports rtg-util liblib logging

strategies

  main-implode-irtg =
    io-wrap(implode-irtg)

  implode-irtg = 
    bottomup-irtg(
       Ignore
    <+ Implode
    <+ log(|Error(), "Cannot implode irtg tree", <id>); fail
    )

rules

  Implode : Annotated(ch, anno*) -> ch{anno*}

  Implode : Anno(t, v) -> (t#([]), v)

  Implode : Prod(nt, c) -> c

/**
 * Terminal applications
 */
rules

  Implode : Appl(Term(t), cs) -> t#(cs)

  Implode : Appl(ConcTerm(), [cs1, cs2]) -> Conc(cs1, cs2)

  Implode : Appl(ConsTerm(), [c, cs]) -> [c | cs]

  Implode : Appl(NilTerm(), []) -> []

  Implode : Appl(ConsTerm(), [c, cs]) -> [c | cs]

  Implode : Appl(TupleTerm(_), cs) -> ""#(cs)

  Implode : Term(s) -> s

/**
 * Built-in non-terminals
 */
rules

  Implode : Int(x) -> x

  Implode : String(s) -> s

strategies

  Ignore = ?Nonterm(_)

