/**
 * @todo    Support annotations
 * @author  Martin Bravenboer <martin@cs.uu.nl>
 */
module core-format-check-to-xhtml
imports
  liblib 
  xml-doc
  core-format-check
  xml-text-tools

strategies

  main-core-format-check-to-xhtml =
    io-wrap(core-fc-options,
      format-check-to-xhtml
    )

strategies

  format-check-to-xhtml =
    main-format-check
  ; print-to-xhtml-doc
  ; where(output(id))
  ; <get-config> "--exit-code"
  ; exit

strategies

  print-to-xhtml-doc =
    !%>
      <?xml version="1.0"?>
      <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd" >

      <html>
        <head>
          <title>format-check report</title>
          <style type="text/css">

            .typo {
              padding: 2pt;
              border-style: solid;
              border-width: 1px;
              border-color: red;
              background-color : rgb(255,220,220);
            }

            .fixed-info {
              position : fixed;
              z-index  : 1;
              width    : 125pt;
              top      : 10pt;
              right    : 10pt;
              padding  : 5pt;
              background-color : #E0E0E0;
              border-style: solid;
              border-width: 1px;
              border-color: black;
            }
          </style>

          <script type="text/javascript">
            function displayType(t) {
              var info = document.getElementById('type-info');
              info.firstChild.nodeValue = t;
            }

            function stopHandling(e) {
              if (!e)
                var e = window.event;

              e.cancelBubble = true;

              if (e.stopPropagation)
                e.stopPropagation();
            }
          </script>
        </head>
        <body>
          <h1>format-check report</h1>

          <p class="fixed-info">
            Type info: <span id="type-info">no selection</span>
          </p>

          <p>
            <% print-to-xhtml %>
          </p>
        </body>
      </html>
    <%
    ; concat-content

  print-to-xhtml =
    print-type(
       print-Int
    <+ print-Real
    <+ print-Str
    <+ print-List
    <+ print-Tuple
    <+ print-Cnst
    <+ print-App
    )

  print-type(s) =
    if is-typed <+ one-consnil(not(is-typed)) then
      where(get-types-to-chardata => type)
    ; s
    ; wrap-in-attrs(| [@> class="typed" <@, @>onmouseover="displayType('<% !type %>'); stopHandling(event)"<@])
    else
      s
    ; wrap-in-class(|"typo")
    end

  wrap-in-class(|class) =
    wrap-in-attrs(| [@> class="<% !class %>" <@])

  wrap-in-attrs(|att*) =
    switch id
      case ?%><div><%= children :: * %></div><% :
        !%><div att*><% !children :: *%></div><%

      case ?%><div @*><%= _ :: * %></div><% :
        !%><div att*><% id %></div><%

      otherwise:
        !%><span att*><% id %></span><%
    end

rules

  /**
   * @todo  escape the string.
   */
  print-Str :
    s -> %>"<% !s :: cdata %>"<%
      where <is-string> s

  print-Int :
    x -> %><% !s :: cdata %><%
      where <is-int> x
          ; <int-to-string> x => s

  print-Real :
    x ->  %><% !s :: cdata %><%
      where <is-real> x
          ; <real-to-string> x => s

/**
 * Constructor applications
 */
rules 

  print-Cnst : 
    f#([]) -> %><% !f :: cdata %>()<%

  print-App =
    if <gt> (<term-size>, 8) then
      print-App-worstcase
    else
      print-App-nicecase
    end

  print-App-nicecase:  
    f#([x | xs]) ->
      %><span><% !f :: cdata %>(<% !children :: content* %>)</span><%
      where < map(print-to-xhtml)
            ; <separate-by> (%>, <%, <id>)
            > [x | xs] => children

  get-types-to-chardata =
    retrieve-types
  ; iset-elements
  ; types-to-string
  ; xml-escape

  print-App-worstcase:  
    t@f#([x | xs]) ->
      %><div>
          <% !f :: cdata %>(
            <div style="padding-left: 1em"><% <print-to-xhtml> x %></div>
            <% <map(
                 print-to-xhtml
               ; !%><div>
                      <span style="position: absolute">,</span>
                      <div style="padding-left: 1em"><% id %></div>
                    </div><%
               )> xs
             :: content* %>
          )
        </div><%

rules
         
  print-Tuple =
    ?""#(<id>); pp-generic-list(!%>()<%, !%>(<%, !%>)<%)
            
  print-List =
    pp-generic-list(!%>[]<%, !%>[<%, !%>]<%)
    
strategies
      
  pp-generic-list(empty, before, after) =
    ?[_ | _];
    if <gt> (<term-size>, 10) then
      generic-list-worstcase(empty, before, after)
    else
      generic-list-nicecase(empty, before, after)
    end
    
  pp-generic-list(empty, before, after) :
    [] -> <empty> ()

  generic-list-nicecase(empty, before, after) :
    [x | xs] ->
      %><span><%
            before
          %><%
            <print-to-xhtml> x
          %><%
             <map-consnil(
                print-type(
                  ?[<id> | _]
                ; print-to-xhtml
                ; !%><span>, <% id %></span><%
                )
              )> xs
           :: content*
          %><%
            after
          %></span><%

  /**
   * @type  List(a) -> List(b)
   * @type  s List(a) -> b
   */
  map-consnil(s) =
    rec x({tail:
      ?[_ | tail]
    ; s
    ; ![<id> | <x> tail]
    <+ []
    })

  generic-list-worstcase(empty, before, after) :
    [x | xs] -> 
      %><div>
          <div>
            <span style="position: absolute"><% before %></span>
            <div style="padding-left: 1em"><% <print-to-xhtml> x %></div>
          </div>
          <% <map(
               print-to-xhtml
             ; !%><div>
                    <span style="position: absolute">,</span>
                    <div style="padding-left: 1em"><% id %></div>
                  </div><%
             )> xs
           :: content* %>
          <div><% after %></div>
        </div><%

strategies 

  concat-content = 
    bottomup( try( \ Conc(l1, l2) -> <conc> (l1, l2) \ ))
