/**
 * @author  Martin Bravenboer <martin@cs.uu.nl>
 */
module core-format-check-pp-aterm
imports
  liblib 
  ugly-print
  core-format-check

strategies

  main-core-format-check-pp-aterm =
    io-wrap(core-fc-options,
      format-check-pp-aterm
    )

strategies

  format-check-pp-aterm =
    main-format-check
    ; pp-aterm-types

strategies

  pp-aterm-types =
      ugly-print-type
    ; concat-content

strategies

  my-ugly-print =
       UP-Int
    <+ UP-Real
    <+ UP-Str
    <+ my-UP-List(ugly-print-type)
    <+ UP-Tuple(ugly-print-type)
    <+ UP-Cnst	
    <+ UP-App(ugly-print-type)

  ugly-print-type =
    ugly-print-type(my-ugly-print)

  ugly-print-type(s) =
    if is-typed <+ one-consnil(not(is-typed)) then
      s
    else
      s => b
      ; where(code-red   => s1)
      ; where(code-plain => s2)
      ; !|[ H hs=0 [s2 s1 b s1 s2] ]|
    end

  my-UP-List(s) =
    my-pp-generic-list(s, !|[ "[]" ]|, !|[ "[" ]|, !|[ "]" ]|)
    
strategies
      
  my-pp-generic-list(s, empty, before, after) =
      where(<gt> (<term-size>, 10))
    < my-generic-list-worstcase(s, empty, before, after)
    + my-generic-list-nicecase(s, empty, before, after)
    
  my-pp-generic-list(s, empty, before, after) :
    [] -> <empty> ()
    
  my-generic-list-nicecase(s, empty, before, after) :
    [x | xs] -> |[
      H hs=0 [
        ~<before> ()
        ~<s> x ~*tail
        ~<after> ()
      ]
    ]|
      where <map-consnil(
                ugly-print-type(
                  ?[<id> | _]
                ; ! |[ H hs=1 [ "," ~<s> ] ]|
                )
              )> xs => tail

  my-generic-list-worstcase(s, empty, before, after) :
    [x | xs] -> |[
      V vs=0 [
        H hs=1 [
          ~<before> ()
          ~<s> x
        ]
        ~*tail
        ~<after> ()
      ]
    ]|
      where <map-consnil(
               ugly-print-type(
                 ?[<id> | _]
               ; ! |[ H hs=1 [ "," ~<s> ] ]|
               )
             )> xs => tail

strategies

  code-red =
    <conc-strings> (<implode-string> [27], "[01;31m")

  code-plain =
    <conc-strings> (<implode-string> [27], "[m")

  is-typed = 
    where(get-types; iset-elements => [_ | _])

strategies

  concat-content =
    bottomup( try( \ Conc(l1, l2) -> <conc> (l1, l2) \ ))
